#+
# Name:
#    _GET_CARTESIAN_TELESCOPE_OFFSETS_
#
# Purpose:
#    Finds the telescope offsets for the current frame measured in pixels.
#
# Language:
#    Perl5
#
# Description:
#    This primitive obtains the telescope offsets from the headers of the
#    current frame, and converts them to Cartesian pixel offsets.  It
#    applies x and y scaling and the rotation matrix.  The offsets are
#    returned in two arguments.
#
# Arguments:
#    ANGLE = REAL (Given)
#       Rotation angle of the Declination axis with respect to the
#       frame's y axis measured counter clockwise.  [Value of CROTA2
#       header or 0.0 if the header does not exist]
#    XOFF = REAL (Returned)
#       The telescope offset along the x axis of the current frame
#       measured in pixels.
#    YOFF = REAL (Returned)
#       The telescope offset along the y axis of the current frame
#       measured in pixels.
#
# Notes:
#    -  This primitive is suitable for both UFTI and IRCAM.
#    Instrument-specific headers are obtained where appropriate.
#    -  Processing only occurs for object and sky frames.
#    -  Header CROTA2---the angle of the chip's y axis with respect
#    to North (positive is anti-clockwise)---is used to convert sky
#    co-ordinate displacements into pixels using a simple rotation
#    matrix.  If this header is absent, no rotation is assumed.   
#    This formulation is satisfactory for the UKIRT's instrument scales
#    and sizes, and its declination range.  A more-sophisticated
#    transformation would be needed near the poles or for wide fields.
#    -  The telescope-offset headers are TRAOFF and TDECOFF for UFTI;
#    and RAOFF and DECOFF for IRCAM.  The platescale headers are CDELT1
#    and CDELT2 for UFTI; there is a single platescale for IRCAM given
#    by header PIXELSIZ.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC)
#
# Copyright:
#    Copyright (C) 1998-2000 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# This should only be performed on OBJECT frames.
    if ( $Frm->hdr( "OBSTYPE" ) eq "OBJECT" ||
         $Frm->hdr( "OBSTYPE" ) eq "SKY") {

# Obtain the rotation angle in degrees.  The numeric default is UFTI
# and time specific.  For (T)UFTI it should read the rotation angle from
# the CROTA2 header.
       my $defrot = $Frm->hdr( "CROTA2" );
       $defrot = defined( $defrot ) ? $defrot : 0.0;
       my $rotangle = ( $_GET_CARTESIAN_TELESCOPE_OFFSETS_{ANGLE} || $defrot );

# Define some useful variables to apply the rotation matrix.
       my $pi = atan2( 1, 1 ) * 4;
       my $dtor = $pi / 180.0;
       my $cosrot = cos( $rotangle * $dtor );
       my $sinrot = sin( $rotangle * $dtor );

# Obtain the plate scales for the instrument from the headers.
       my ( $ra_pixelscale, $dec_pixelscale );
       my $instrument = $Frm->hdr( "INSTRUME" );
       if ( $instrument =~ /^UFTI/ ) {
          $ra_pixelscale = $Frm->hdr( "CDELT1" );
          $dec_pixelscale = $Frm->hdr( "CDELT2" );
       } else {
          $ra_pixelscale = $Frm->hdr( "PIXELSIZ" );
          $dec_pixelscale = $ra_pixelscale;
       }

# Obtain the telescope offsets in arcseconds using the instrument-specific
# headers.
       my ( $ra_off_as, $dec_off_as );
       if ( $instrument =~ /^UFTI/ ) {
          $ra_off_as = $Frm->hdr( "TRAOFF" );
          $dec_off_as = $Frm->hdr( "TDECOFF" );
       } else {
          $ra_off_as = $Frm->hdr( "RAOFF" );
          $dec_off_as = $Frm->hdr( "DECOFF" );
       }

# Convert header offsets in arcseconds to pixel offsets of the object
# in the Cartesian sense.
       my $ra_off_p = $ra_off_as / $ra_pixelscale;
       my $dec_off_p = -1.0 * $dec_off_as / $dec_pixelscale;

# Apply the rotation matrix to obtain Cartesian pixel offsets.
       my $xoffset = $ra_off_p * $cosrot - $dec_off_p * $sinrot;
       my $yoffset = $ra_off_p * $sinrot + $dec_off_p * $cosrot;

# Set the returned arguments.
       $_GET_CARTESIAN_TELESCOPE_OFFSETS_{XOFF} = $xoffset;
       $_GET_CARTESIAN_TELESCOPE_OFFSETS_{YOFF} = $yoffset;
    }

# Podule
# ======

=head1 NAME

_GET_CARTESIAN_TELESCOPE_OFFSETS_ -- Finds the telescope offsets for the current frame measured in pixels.

=head1 DESCRIPTION

This primitive obtains the telescope offsets from the headers of the
current frame, and converts them to Cartesian pixel offsets.  It
applies x and y scaling and the rotation matrix.  The offsets are
returned in two arguments.

=head1 ARGUMENTS

=over 4

=item ANGLE = REAL (Given)

Rotation angle of the Declination axis with respect to the
frame's y axis measured counter clockwise.  [Value of CROTA2
header or 0.0 if the header does not exist]

=item XOFF = REAL (Returned)

The telescope offset along the x axis of the current frame
measured in pixels.

=item YOFF = REAL (Returned)

The telescope offset along the y axis of the current frame
measured in pixels.

=back

=head1 NOTES

=over 4

=item *

This primitive is suitable for both UFTI and IRCAM.
Instrument-specific headers are obtained where appropriate.

=item 4

Processing only occurs for object and sky frames.

=item *

Header CROTA2---the angle of the chip's y axis with respect
to North (positive is anti-clockwise)---is used to convert sky
co-ordinate displacements into pixels using a simple rotation
matrix.  If this header is absent, no rotation is assumed.   
This formulation is satisfactory for the UKIRT's instrument scales
and sizes, and its declination range.  A more-sophisticated
transformation would be needed near the poles or for wide fields.

=item *

The telescope-offset headers are TRAOFF and TDECOFF for UFTI;
and RAOFF and DECOFF for IRCAM.  The platescale headers are CDELT1
and CDELT2 for UFTI; there is a single platescale for IRCAM given
by header PIXELSIZ.

=back

=head1 AUTHORS

MJC: Malcolm J. Currie (JAC)

=head1 COPYRIGHT

Copyright (C) 1998-2000 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut

