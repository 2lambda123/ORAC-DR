# -*-cperl-*-

=head1 NAME

_REDUCE_SCIENCE_STEER_ - Sets up steering parameters for REDUCE_SCIENCE recipes.

=head1 DESCRIPTION

This creates the generic steering metadata in the form of internal
headers for the REDUCE_SCIENCE... recipes.  It also checks (via
L<_VERIFY_HEADERS_|_VERIFY_HEADERS_>) to see whether FITS headers
missing from old data are in fact missing, and then creates any
missing headers.

This primitive should be invoked before the recipe-specific steering primitive.

=head1 ARGUMENTS

=over 4

=item COPY_FRAME = BOOLEAN (Given)

Whether or not to copy Frame products to Group products when there
is only one member in the group.  [1]

=item SUMMIT = BOOLEAN (Given)

This should be set true (1) for SUMMIT recipes.  It sets a Frame
internal header so that later primitives can take different action
for summit recipes.  [0]

=back

=head1 NOTES

=over 4

=item * It sets the following internal Frame headers.

=over 4

=item COPY_FRAME_CUBE---whether or not to copy Frame products to Group products

=item ISHYBRID---whether or not the data are hybrid

=item MAP_ASTREGION---the map region for JCMT::MapArea from raw scan data

=item SUMMIT--the value of the SUMMIT argument

=back

=item * It examines headers (using L<_CHECK_OBSEVATORY_STATUS|_CHECK_OBSEVATORY_STATUS>)
that indicate the status of the observatory.

=back

=head1 OUTPUT DATA

=over 4

None.

=back

=head1 TASKS

None.

=head1 REQUIRED PERL MODULES

JCMT::MapArea

=head1 AUTHORS

Brad Cavanagh E<lt>b.cavanagh@jach.hawaii.eduE<gt>

Malcolm J. Currie E<lt>mjc@star.rl.ac.ukE<gt>

=head1 COPYRIGHT

Copyright (C) 2008, 2017, 2018 Science and Technology Facilities Council.
All Rights Reserved.

=head1 LICENCE

This program is free software; you can redistribute it and/or
modify it under the terms of the GNU General Public License as
published by the Free Software Foundation; either Version 3 of
the License, or (at your option) any later version.

This program is distributed in the hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied
warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public
License along with this program; if not, write to the Free
Software Foundation, Inc., 59 Temple Place, Suite 330, Boston,
MA 02111-1307, USA.

=cut

# Handle arguments.
my $copy = get_prim_arg( $_PRIM_ARGS_, "COPY_FRAME", 1 );

my $summit = get_prim_arg( $_PRIM_ARGS_, "SUMMIT", 0 );
$Frm->uhdr( "SUMMIT", $summit );

_VERIFY_HEADERS_

# Figure out if we are going to copy the Group cube from the Frame cube.
if( $Grp->num == 0 && $Grp->lastallmembers( $Frm ) && $copy ) {
  $Grp->uhdr( "COPY_FRAME_CUBE", 1 );
} else {
  $Grp->uhdr( "COPY_FRAME_CUBE", 0 );
}

# Figure out if we're doing hybrid-mode or not.
my $ishybrid = 0;
if( $Frm->can( "subsysnrs" ) ) {
  my $nsubsys = $Frm->subsysnrs;
  $ishybrid = 1 if $nsubsys > 1;
}
$Frm->uhdr( "ISHYBRID", $ishybrid );

orac_print( "Observing Mode: " . $Frm->uhdr( "ORAC_INSTRUMENT" ) .
            " / " . $Frm->uhdr( "ORAC_SAMPLE_MODE" ) .
            " / " . $Frm->hdr( "SW_MODE" ) .
            ( $Frm->hdr( "STANDARD" ) ? " / standard" : "" ) .
            ( $Frm->uhdr( "ISHYBRID" ) ? " / hybrid mode" : "" ) .
            "\n" );

_CHECK_OBSERVATORY_STATUS_

# Set a default OBSIDSS for hybrid-mode observations.
if( $Frm->uhdr( "ISHYBRID" ) ) {
  $Frm->hdr( "OBSIDSS", $Frm->hdrval( "OBSIDSS" ) );
}

# Calculate the requested map region if we're doing a scan.
# but only if we are processing raw data [proxied by availability
# of jcmtstate method]
if( $Frm->can("jcmtstate") && $Frm->uhdr( "ORAC_SAMPLE_MODE" ) eq 'scan' ) {

  use JCMT::MapArea;

  my $hdr = $Frm->hdr;
  my $region = &JCMT::MapArea::region( header => $hdr );
  $Frm->uhdr( "MAP_ASTREGION", $region );

}
