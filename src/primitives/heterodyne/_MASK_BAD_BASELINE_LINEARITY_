# -*-cperl-*-

=head1 REQUIRED PERL MODULES

JCMT::ACSIS::Array, Statistics::Descriptive.

=cut

use JCMT::ACSIS::Array;
use Statistics::Descriptive;

# Handle arguments.
# =================

my $linear_clip = get_prim_arg( $_PRIM_ARGS_, "CLIP", "2.5,3.0" );
my $central_mask = get_prim_arg( $_PRIM_ARGS_, "LINEWIDTH", 25.0 );
my $smooth_scale = get_prim_arg( $_PRIM_ARGS_, "SCALELENGTH", 101 );

# Handle recipe parameter overrides.
$linear_clip = ( defined( $Frm->uhdr( "RECPAR_BLLINEAR_CLIP" ) ) ?
             $Frm->uhdr( "RECPAR_BLLINEAR_CLIP" )              :
             $linear_clip );

$central_mask = ( defined( $Frm->uhdr( "RECPAR_BLLINEAR_LINEWIDTH" ) ) ?
             $Frm->uhdr( "RECPAR_BLLINEAR_LINEWIDTH" )         :
             $central_mask );

$smooth_scale = ( defined( $Frm->uhdr( "RECPAR_BLLINEAR_SCALELENGTH" ) ) ?
             $Frm->uhdr( "RECPAR_BLLINEAR_SCALELENGTH" )       :
             $smooth_scale );

# Convert list of standard-deviation clip levels to an array.
my @clips = split( ',', $linear_clip );

# Constrain parameters to allowed values.
# ---------------------------------------
$central_mask = max( 1.0, min( 100.0, $central_mask ) );
$smooth_scale = max( 51, $smooth_scale );

# Sigma clipping levels should not decrease. Do not set the initial
# threshold too low lest we exclude acceptable but slightly deviant
# detectors in an otherwise good set.
my @clip_sigma = ();
my $last = 2.0;
foreach ( @clips ) {
   push @clip_sigma, max( $last, $_ );
}

# Specify output and temporary filenames.
# =======================================
my $params;
my @bad_receptors;

# Store the name of the first file in the list for the index.qa file
my ( $first_file_in, $first_file_out)  = $Frm->inout( "_blmasked", 1 );

foreach my $i ( 1 .. $Frm->nfiles ) {
   orac_say( "Performing bad-baseline detection comparing trend with linear fit" );
   if ( $i == 1 ) { orac_say( "The first file is $first_file_in." ) };
   my @bad_receptors_local;

# Set/get filenames.
   my ( $in, $out ) = $Frm->inout( "_blmasked", $i );
   ( $in, my $collapsed ) = $Frm->inout( "_spco", $i );
   ( $in, my $residual ) = $Frm->inout( "_resp", $i );
   ( $in, my $smoothed ) = $Frm->inout( "_fbsp", $i );

# We need the dimensions of the input file for later.
   _GET_DIMENSIONS_ FILE=$in
   my $lbnd = $_GET_DIMENSIONS_{LBOUND};
   my $ubnd = $_GET_DIMENSIONS_{UBOUND};

#   my $temp1file = new ORAC::TempFile( 0 );
   my $temp1file = $collapsed;
   my $temp2file = new ORAC::TempFile( 0 );
   my $temp3file = new ORAC::TempFile( 0 );
#   my $temp4file = new ORAC::TempFile( 0 );
   my $temp4file = $smoothed;
#   my $temp5file = new ORAC::TempFile( 0 );
   my $temp5file = $residual;

   my $temp6file = new ORAC::TempFile( 0 );
   my $temp7file = new ORAC::TempFile( 0 );
   my $temp8file = new ORAC::TempFile( 0 );

# Form arrays of bad detectors.
# =============================

# Retrieve the list of bad detectors from the calibration system ONLY
# ignoring the ones set locally.  The reading of INDEXORMASTER is
# done to setup the survey (survey_br).
   $Cal->bad_receptors( "MASTER" );
   my @baddets = $Cal->bad_receptors_list;
   orac_say( " Receptors disabled by the calibration system: " . join( ",", sort @baddets ) );

# Still need to invoke the bad_receptors_list method although the
# resulting array is not used.
   $Cal->bad_receptors( "INDEXORMASTER" );
   my @tmpdets = $Cal->bad_receptors_list;
   orac_say( " Receptors indexed by the calibration system: " . join( ",", sort @tmpdets ) );

# Get average noise for each receptor not in the bad_receptors list.
# ==================================================================

# Collapse time axis using a clipped mean to form average spectra for
# each detector.  Although would like to use variance weighting, the
# covariance matrix becomes huge.
   orac_say( " Collapsing time-series cube..." );
   $params = "in=$in out=$temp1file estimator=Cmean clip=2.0";
   $params .= " axis=time trim wlim=0.0 variance=false";
   $Mon{'kappa_mon'}->obeyw( "collapse", "$params" );

# Apply threshold to exclude spikes in the spectra.
   orac_say( " Thresholding the spectra at +/- 20 to exclude spikes." );
   $params = "in=$temp1file out=$temp2file thrlo=-20 newlo=bad thrhi=20 newhi=bad";
   $Mon{'kappa_mon'}->obeyw( "thresh", "$params" );

# Mask out the central region to exclude narrow astronomical line, but
# even that has wings.  Allow for variable spectral range by using a
# velocity range.
   orac_say( " Masking the central $central_mask km/s to exclude the astronomical line." );
   my $chpix_section = "~$central_mask,";
   my $params = "in=$temp2file out=$temp3file section='$chpix_section' newval=bad";
   $Mon{'kappa_mon'}->obeyw( "chpix", "$params" );

# Find the trends in the background.  This step prevents the rms from a
# baseline fit being swamped by the noise.  The aim is to find whether
# the linear fit is a good match to global shape of the baseline.  This aims to
# identify ripples or turned-up ends of the spectra.
   orac_say( " Determine the smooth trends of the spectra using a filter width of $smooth_scale pixels." );
   my $boxparam = "[$smooth_scale,1]";
   $params = "in=$temp3file out=$temp4file box=$boxparam sub=false accept";
   $Mon{'cupid_mon'}->obeyw( "findback", "$params" );

# Subtract linear baselines from smoothed spectrum for each detector.
   orac_say( " Fit linear baselines and subtract these from the trends." );
   $params = "in=$temp4file out=$temp5file modifyin=false subtract auto";
   $params .= " order=1 axis=spec method=single";
   $Mon{'kappa_mon'}->obeyw( "mfittrend", "$params" );

   my %receptor_stats;
   my %receptor_rms;
   my $array_stats = new Statistics::Descriptive::Sparse;
   my $array = new JCMT::ACSIS::Array( File => $temp5file );

# Form set of receptors that are not bad in the master list.
   my %keep;
   @keep{ $array->receptors } = ( );
   delete @keep{ @baddets };
   my @receptors = keys %keep;

   foreach my $receptor ( @receptors ) {
      my $pixel = $array->pixel( $receptor );
      $Mon{'kappa_mon'}->obeyw( "stats", "ndf=$temp5file(,$pixel)" );
      my ( $ORAC_STATUS, $nel ) = $Mon{'kappa_mon'}->get( "stats", "numgood" );

# Calculate rms from the linear fit for the current receptor.  Exclude
# the ends of each spectrum where the noise increases dramatically,
# and could swamping the signal being measured.
      if ( $nel > 0 ) {
         $params = "ia=$temp5file(,$pixel) out=$temp6file exp='IA*IA/$nel'";
         $Mon{'kappa_mon'}->obeyw( "maths", "$params" );
         $Mon{'kappa_mon'}->obeyw( "stats", "ndf=$temp6file(~85%,)" );
         ( $ORAC_STATUS, my $ms ) = $Mon{'kappa_mon'}->get( "stats", "total" );
         my $rms = sqrt( $ms );

         if ( ! defined( $receptor_stats{$receptor} ) ) {
            $receptor_stats{$receptor} = new Statistics::Descriptive::Sparse;
         }

         if ( $rms > 0 ) {
            $receptor_stats{$receptor}->add_data( $rms );
         }
      }
   }

   foreach my $receptor ( keys %receptor_stats ) {
      if ( $receptor_stats{$receptor}->count != 0 ) {
         $receptor_rms{$receptor} = $receptor_stats{$receptor}->mean();
      } else {
         $receptor_rms{$receptor} = 'bad';
      }
   }

   my $type = "RMS from linear baseline";
   _PRETTYPRINT_RECEPTOR_QA_ HASH=\%receptor_rms TYPE=$type

# Measure the median and standard deviation of the residuals from the
# linear trend across the detectors.
   my @values;
   my $sigma;
   my $median;
   my $loop = 1;
   my $i = 0;

   while ( $i <= $#clip_sigma && $loop ) {
      my $stat = Statistics::Descriptive::Full->new();

# Assume no rejection.
      $loop = 0;

# Extract the rms values from the hash.
      while ( my ($k,$v) = each %receptor_rms ) {
         if ( $v ne "bad" ) { push @values, $v }
      }

# Remove the flagged values before adding the values to the statistics
# object.
#      @values = map{ $_ eq 'bad' ? ( ) : $_ } @values;
      $stat->add_data( @values );

      if ( $stat->count != 0 ) {
         $sigma = $stat->standard_deviation();
         $median = $stat->median();
      }

# Flag those detectors whose rms deviation from the linear trend exceeds
# median plus the current sigma-clipping level.
      my $threshold = $median + $clip_sigma[$i] * $sigma;
      orac_say( " Turning off detectors with spectral rms from the trend above " . sprintf( "%.3f", $threshold ) );
      orac_say( " corresponding to the median plus $clip_sigma[$i] standard deviations." );

# This would be cleaner if there were a remove_data method in
# Statistics::Descriptive::Full.
      while ( my ($k,$v) = each %receptor_rms ) {
         if ( $v ne 'bad' ) {
            if ( $v > $threshold ) {
               push @bad_receptors_local, $k;
               $receptor_rms{$k} = 'bad';

# Continue iteratively clipping as a detector has been flagged for
# removal.
               $loop = 1;
            }
         }
      }
      $i++;
      undef $stat;
      undef @values;
   }

   orac_say( " List of bad receptor for file $in:" .
             join( ",", sort( @bad_receptors_local ) ) );

# Display if necessary.
   $Display->display_data( $Frm ) if defined $Display;

# Add the bad receptors for this frame to the master list.
   foreach my $receptor ( @bad_receptors_local ) { push ( @bad_receptors, $receptor ) }
}

# Make sure each bad detector is mentioned only once.
my %seen = ();
my @bad_receptors_uniq = ();

foreach my $item ( @bad_receptors ) {
   push( @bad_receptors_uniq, $item ) unless $seen{$item}++;
}

my $lofreq = $Frm->hdr( "LOFREQS" );

my $survey_br = defined( $Frm->uhdr( "SURVEY_BR" ) ) ? $Frm->uhdr( "SURVEY_BR" ) : 'Telescope';

# Sort out bad receptors.  They'll be added to the calibration system
# for either the specific survey or for everything, and only for the
# current LOFREQ +/- 1 GHz.
if ( scalar( @bad_receptors ) > 0 ) {
   my %recep_hash = ( %{$Frm->hdr },
                      LOFREQ_MAX => $lofreq + 1,
                      LOFREQ_MIN => $lofreq - 1,
                      DETECTORS => ( join ',',@bad_receptors_uniq ),
                      SURVEY_BR => $survey_br,
                    );

   orac_say( " Masking receptor(s) " .
             join( ",", sort @bad_receptors_uniq ) . " for survey $survey_br." );

   $Cal->bad_receptors_qa_index->add( $first_file_in, \%recep_hash );

}

# Tidy-up output.
orac_print "\n";
