# -*-perl-*-

=head1 NAME

_QA_SYSTEM_TEMPERATURE_ - Run system temperature QA checks.

=head1 AUTHORS

Brad Cavanagh <b.cavanagh@jach.hawaii.edu>

=head1 COPYRIGHT

Copyright (C) 2008 Science and Technology Facilities Council. All
Rights Reserved.

=cut

use Starlink::HDSPACK qw/ create_hdsobj copy_hdsobj /;
use JCMT::ACSIS::Array;

my %tsys;

foreach my $i ( 1 .. $Frm->nfiles ) {

  my %receptor_tsys;

  my $infile = $Frm->file( $i );

  orac_print "Retrieving Tsys values for $infile.\n";

  my $tmptsys = new ORAC::TempFile;

  # Copy the Tsys array out to a temporary NDF.
  my $ORAC_STATUS = ( create_hdsobj( "$tmptsys", 'NDF' ) ? ORAC__OK : ORAC__ERROR );
  $ORAC_STATUS = ( copy_hdsobj( "$infile.MORE.ACSIS.TSYS", "$tmptsys.DATA_ARRAY" ) ? ORAC__OK : ORAC__ERROR );

  # Get stats on the Tsys array.
  $Mon{'kappa_mon'}->obeyw( "stats", "ndf=$tmptsys" );

  ( $ORAC_STATUS, my $mean ) = $Mon{'kappa_mon'}->get( "stats", "mean" );

  orac_print sprintf( " Tsys for entire array:   %7.2f K\n", $mean );

  # Get stats for each receptor.
  my $array = new JCMT::ACSIS::Array( File => $infile );
  my @receptors = $array->receptors;

  my $dstring = '';
  my $rstring = '';
  my @dstrings;
  my @rstrings;

  foreach my $receptor ( @receptors ) {
    my $pixel = $array->pixel( $receptor );
    $Mon{'kappa_mon'}->obeyw( "stats", "ndf=$tmptsys($pixel,)" );
    ( $ORAC_STATUS, my $pixmean ) = $Mon{'kappa_mon'}->get( "stats", "mean" );

    if( $pixmean > 0 ) {
      $rstring .= sprintf( " %8.3f", $pixmean );
      $receptor_tsys{$receptor} = $pixmean;
    } else {
      $rstring .= "      bad";
      $receptor_tsys{$receptor} = "bad";
    }

    $dstring .= "      $receptor";

    if( length( $dstring ) > 72 ) {
      push @dstrings, $dstring;
      push @rstrings, $rstring;
      $dstring = '';
      $rstring = '';
    }
  }

  push @dstrings, $dstring;
  push @rstrings, $rstring;

  orac_print " Tsys values for each receptor:\n";
  foreach my $i ( 0 .. $#dstrings ) {
    orac_print "$dstrings[$i]\n$rstrings[$i]\n";
  }

  # Store the information on Tsys values.
  $tsys{$infile} = \%receptor_tsys;

}

# Store the hash of Tsys values.
$Frm->uhdr( "QA_TSYS_VALUES", \%tsys );

# Tidy-up output.
orac_print "\n";
