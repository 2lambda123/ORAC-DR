# -*-cperl-*-

=head1 NAME

_MERGE_HYBRID_MODE_ - Merge hybrid mode observations in the frequency
domain.

=head1 DESCRIPTION

This primitive operates on hybrid-mode observations.  It first
determines a DC-level offset between corresponding subband
observations, using the median of entire spectra.  The DC offset is
added to or removed from the subband spectra, and the corresponding
subband spectra are mosaicked together to form time-series cubes with
a greater frequency extent.

=head1 ARGUMENTS

=over 4

None.

=back

=head1 NOTES

=over 4

=item *

This primitive is suitable for ACSIS data taken in hybrid mode.

=item *

This primitive only operates if the ISHYBRID user header is set for
the current Frame object.

=back

=head1 OUTPUT DATA

=over 4

=item *

The DC-corrected time-series cubes with suffix _dc.

=item *

The merged time-series cube with suffix _merge.

=back

=head1 TASKS

KAPPA: COLLAPSE, MANIC, MATHS, NDFTRACE, WCSMOSAIC.

=head1 REQUIRED PERL MODULES

File::Basename.

=head1 AUTHORS

Brad Cavanagh E<lt>b.cavanagh@jach.hawaii.eduE<gt>
Malcolm J. Currie E<lt>mjc@jach.hawaii.eduE<gt>

=head1 COPYRIGHT

Copyright (C) 2008, 2013 Science and Technology Facilities Council.  All
Rights Reserved.

=cut

use File::Basename;

if ( $Frm->uhdr( "ISHYBRID" ) ) {

  orac_print "Hybrid mode observations. Merging spectra.\n";

  my @newfrmout;
  my @outfiles;
  my $params;

  # Previous subband is the initially the first.
  my $last_dc_corrected = $Frm->file( 1 );

  # Retrieve bounds for the first file.
  _GET_DIMENSIONS_ FILE=$last_dc_corrected
  my $lbounds1 = $_GET_DIMENSIONS_{LBOUND};
  my $ubounds1 = $_GET_DIMENSIONS_{UBOUND};

  # Retrieve the units so we can set them back to whatever they are
  # now.  MATHS trounces units.
  $Mon{'ndfpack_mon'}->obeyw( "ndftrace", "ndf=$last_dc_corrected" );
  my ( $ORAC_STATUS, $units ) = $Mon{'ndfpack_mon'}->get( "ndftrace", "units" );

  foreach my $nsubband ( 2 .. $Frm->nfiles ) {

    my $next_subband = $Frm->file( $nsubband );
    orac_say " Calculating overlap for subband $nsubband.";

    # Get the overlap range.
    _CALCULATE_HYBRID_OVERLAP_ FILE1=$last_dc_corrected FILE2=$next_subband
    my $low = $_CALCULATE_HYBRID_OVERLAP_{LBOUND};
    my $high = $_CALCULATE_HYBRID_OVERLAP_{UBOUND};

    # Retrieve bounds for the next subband file.
    _GET_DIMENSIONS_ FILE=$next_subband
    my $lbounds2 = $_GET_DIMENSIONS_{LBOUND};
    my $ubounds2 = $_GET_DIMENSIONS_{UBOUND};

    orac_print sprintf( " Overlap range: [%d:%d]\n", $low, $high );

    my $range = "";
    if ( $low > $high ) {
      orac_warn "No overlap between the input cubes.";
    }

    # Use COLLAPSE to find the median for each spectrum.
    my $temp1file = new ORAC::TempFile( 0 );
    my $temp2file = new ORAC::TempFile( 0 );

    $params = "in=$last_dc_corrected($range,,) out=$temp1file";
    $params .= " estimator=median axis=spec variance wlim=0.0";
    $Mon{'kappa_mon'}->obeyw( "collapse", "$params" );

    $params = "in=$next_subband($range,,) out=$temp2file";
    $params .= " estimator=median axis=spec variance wlim=0.0";
    $Mon{'kappa_mon'}->obeyw( "collapse", "$params" );

    # DC level adjustment is half the difference.
    my $temp3file = new ORAC::TempFile( 0 );
    $params = "exp=((ib-ia)/2) ia=$temp1file ib=$temp2file out=$temp3file";
    $Mon{'kappa_mon'}->obeyw( "maths", "$params" );

    # Grow this DC adjustment into a cube and add it to the first file.
    my $temp4file = new ORAC::TempFile( 0 );
    $params = "in=$temp3file out=$temp4file axes=[0,1,2]";
    $params .= "lbound=" . $lbounds1->[0] . " ubound=" . $ubounds1->[0];
    $Mon{'kappa_mon'}->obeyw( "manic", "$params" );

    # Must avoid having the same input and output NDF names.  After the
    # first pair the lower of the two for comparison will have the same
    # suffix.
    my $in = $last_dc_corrected;
    if ( $nsubband > 2 ) {
       my $temp5file = new ORAC::TempFile( 0 );
       $Mon{'ndfpack_mon'}->obeyw( "ndfcopy", "in=$last_dc_corrected out=$temp5file" );
       $in = $temp5file;
    }

    ( undef, my $out ) = $Frm->inout( "_dc", $nsubband - 1 );
    $params = "in1=$in in2=$temp4file out=$out";
    $Mon{'kappa_mon'}->obeyw( "add", "$params" );

    # Assume that the units are the same for all files.  If not, then
    # the offsetting process makes not sense anyway.
    $Mon{'ndfpack_mon'}->obeyw( "setunits", "ndf=$out units=$units" );

    orac_print $last_dc_corrected . " to $out:\n";
    orac_print " Removed DC-level offset.\n";

    if ( $nsubband == 2 ) { push @outfiles, $out }

    # Grow the DC adjustment into another cube and subtract it from
    # the second file.
    my $temp5file = new ORAC::TempFile( 0 );
    $params = "in=$temp3file out=$temp5file axes=[0,1,2]";
    $params .= "lbound=" . $lbounds2->[0] . " ubound=" . $ubounds2->[0];
    $Mon{'kappa_mon'}->obeyw( "manic", "$params" );

    $out = $next_subband;
    $out =~ s/_[a-z]+/_dc/;
    $params = "in1=$next_subband in2=$temp5file out=$out";
    $Mon{'kappa_mon'}->obeyw( "sub", "$params" );

    # Assume that the units are the same for all files.  If not, then
    # the offsetting process makes not sense anyway.
    $Mon{'ndfpack_mon'}->obeyw( "setunits", "ndf=$out units=$units" );

    orac_print "$next_subband to $out:\n";
    orac_print " Removed DC-level offset.\n";

    # Record the DC-corrected spectra for mosaicking.
    push @outfiles, $out;

    # Want to offset against the already corrected nth subband.
    $last_dc_corrected = $out;

    # The upper subband now becomes the lower.
    $lbounds1 = $lbounds2;
    $ubounds1 = $ubounds2;
  }

  # We have $nsubband DC-corrected files.  Form text file listing
  # the NDFs to mosaic.
  my $mos_file = write_file_list( @outfiles );
  my $mos_filename = basename( $mos_file->file );
  ( undef, my $out ) = $Frm->inout( "_merge", 1 );
  $params = "in='^$mos_filename' out=$out method=nearest";
  $params .= " lbnd=! ubnd=! ref=! variance genvar=false wlim=0";
  $Mon{'kappa_mon'}->obeyw( "wcsmosaic", "$params" );

  orac_print join ',', @outfiles;
  orac_print " to $out:\n";
  orac_print " Merged hybrid-mode files.\n";

  push @newfrmout, $out;

  $Frm->product( "merge" );
  $Frm->files( @newfrmout );

  # Tidy-up output.
  orac_print "\n";
}
