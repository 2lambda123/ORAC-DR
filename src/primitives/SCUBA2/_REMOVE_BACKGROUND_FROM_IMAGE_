# -*-cperl-*-

=head1 NAME

_REMOVE_BACKGROUND_FROM_IMAGE_ -  fit and remove a background from an image

=head1 SYNOPSIS

 _REMOVE_BACKGROUND_FROM_IMAGE_

 _REMOVE_BACKGROUND_FROM_IMAGE_ METHOD=FINDBACK BOX=15

 _REMOVE_BACKGROUND_FROM_IMAGE_ METHOD=FITSURFACE MASK=1 RADIUS=25

 _REMOVE_BACKGROUND_FROM_IMAGE_ METHOD=PLANE

=head1 DESCRIPTION

This primitive fits and subtracts a background from an image. The user
may choose the type of fit with the B<METHOD> parameter. Four methods
are supported:

=over 2

=item *

find and remove a general background of arbitrary shape (using
CUPID C<FINDBACK>);

=item *

fit and remove a polynomial surface after optionally masking
out an aperture centred on the source (using KAPPA C<FITSURFACE> and
C<MAKESURFACE>;

=item *

fit and remove a 2-D plane;

=item *

subtract a simple mean level.

=back

It is assumed that this is amounts to removing the bulk atmospheric
contribution from the data, with the result that the Frame has the
uhdr entry C<SKY_SUBTRACTION> set to 1 on success. It will not work on
raw time series data.

=head1 ARGUMENTS

The following arguments are supported:

=over 2

=item B<BOX>

Size of box in pixels to use when estimating the background. Only
accessed if B<METHOD> is C<BACKGROUND>. Default is 11.

=item B<CALONLY>

Flag to indicate that the background removal should only done for
known calibrators. Overrides the B<FORCE> parameter below but has no
effect if sky subtraction has not taken place. Default is 0 (remove
background for all sources).

=item B<FITPAR>

Parameters for controlling the polynomial order or spline fitting for
fitsurface. May be one value, indicating the same fit us to be carried
out in the X and Y directions, or two to indicate a different order or
number of knots for each axis. Default depends on B<FITTYPE> and is 2
for C<polynomial>, 4 for C<spline>.

=item B<FITTYPE>

Type of fit to be carried out by C<fitsurface>. May be C<polynomial>
or C<spline>. Default is C<polynomial>.

=item B<FORCE>

A flag to override the sky subtraction uhdr entry. Default is 0 (do
not override).

=item B<MASK>

A flag to denote whether to mask the source and estimate a background
from the masked image. Default is 0 (do not mask).

=item B<METHOD>

Method to use for background estimate. May be C<BACKGROUND>, C<DC> or
C<PLANE>. Default is C<BACKGROUND>.

=item B<RADIUS>

The radius of a circular region in arcsec used to define the mask. Only
accessed if B<MASK> is true. Default is 30 arcsec.

=item B<VERBOSE>

Flag to denote whether to print extra messages. Default is 1 (yes).

=back

=head1 EXTERNAL TASKS

The following external tasks are used:

=over 2

=item CUPID:

FINDBACK

=item KAPPA:

FITSURFACE, MAKESURFACE, SUB

=item ORAC-DR PRIMITIVES:

_MASK_IMAGE_, _REMOVE_DC_SKY_, _REMOVE_PLANE_FROM_IMAGE_

=back

=head1 OUTPUT DATA

Sets the C<SKY_SUBTRACTION> flag in the Frame uhdr.

=head1 OUTPUT FILES

Creates output files with the suffix C<_sky> or C<_back>. The latter
suffix is used if the sky subtraction uhdr flag has already been
set. The Frame is updated with the sky-subtracted files.

=head1 NOTES

Only works on Frames. Does not work on time-series (3-d) data.

Will only work on Frames with a product of C<newimage>.

No action is taken if the C<SKY_SUBTRACTION> flag is already set in
the Frame uhdr unless the user supplies the B<FORCE> parameter.

If B<MASK> is true, the image is masked using the default behaviour
for L<_MASK_IMAGE_|_MASK_IMAGE_>.

=head1 AUTHOR

Andy Gibb  E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2009-2010,2012 University of British Columbia. All
Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# Force background removal regardless of SKY_SUBTRACTION uhdr
my $force = get_prim_arg( $_PRIM_ARGS_, "FORCE", 0 );
# Override with recipe parameter if given
if ($force == -1) {
  $force = (defined $Frm->uhdr("RECPAR_REMOVE_BACKGROUND")) ?
    $Frm->uhdr("RECPAR_REMOVE_BACKGROUND") : 0;
}

# Do not remove a background for non-calibrators if calonly is set
my $calonly = get_prim_arg( $_PRIM_ARGS_, "CALONLY", 0 );
if ($calonly) {
  $force = 0 unless $Cal->iscalsource($Frm->uhdr("ORAC_OBJECT"));
}

# Do not proceed if the Frame is bad
$force = ($Frm->isgood) ? 1 : 0;

# Only proceed if a new image has been created
$force = 0 unless ($Frm->tagexists("newimage"));

if ( $force || !$Frm->uhdr("SKY_SUBTRACTION") ) {
  # Should I be verbose (default to yes)
  my $verbose = get_prim_arg( $_PRIM_ARGS_, "VERBOSE", 1 );

  # Method of background removal
  my $bgmethod = lc(get_prim_arg( $_PRIM_ARGS_, "METHOD", "findback"));

  # Fall back on the default if not recognized
  if ( $bgmethod !~ /^[fitsur|findba|plan|dc]/ ) {
    orac_warn "Unrecognized background-removal option, $bgmethod: using default (findback)\n";
    $bgmethod = "findback";
  }

  my $nfiles = $Frm->nfiles;

  if ( $bgmethod eq "fitsurface" || $bgmethod eq "findback" ) {
    my $findback = ($bgmethod eq "findback") ? 1 : 0;
    # Fit and remove a background
    orac_print "Fitting and removing background..."
      if $verbose;

    # Use a different suffix if this is a background removal (post
    # sky-subtraction) rather than a simple sky subtraction
    my $suffix = ( $Frm->uhdr("SKY_SUBTRACTION") ) ? "_back" : "_sky";

    # Background-subtracted files
    my @out;

    if ( $findback ) {
      # Size of region to use for smoothing. A default of 11 seems to
      # work well for point sources, preserving the peak flux while
      # fitting the background.
      my $box = get_prim_arg( $_PRIM_ARGS_, "BOX", 11 );
      # Fit and remove background from each file
      foreach my $i ( 1 .. $nfiles ) {
	my ($in, $out) = $Frm->inout( $suffix, $i);
	my $args = "in=$in out=$out box=$box sub=true accept";
	$Mon{cupid_mon}->obeyw("findback","$args");
	push(@out, $out);
      }

    } else {

      # Mask out a circular region centred on the source if requested
      my $mask = get_prim_arg( $_PRIM_ARGS_, "MASK", 0 );
      # Aperture for masking out the source
      my $radius = get_prim_arg( $_PRIM_ARGS_, "RADIUS",
				 ($Frm->hdr("FILTER") =~ /850/) ? 30 : 15 );
      orac_print " masking with ".2.*$radius." arcsec aperture..."
	if ($verbose && $mask);

      # Temporary files for fitting background
      my $tmpsurface = new ORAC::TempFile;
      my $tmpsurfacefile = $tmpsurface->file;
      # Obtain a filename via inout to avoid temp files in the intermediates
      my $maskfile = $Frm->inout("_temp");
      # Flag to keep the fitted surface on disk - default to no
      my $keepsurface = (defined $Frm->uhdr("RECPAR_FITSURFACE_KEEPSURFACE"))
	? $Frm->uhdr("RECPAR_FITSURFACE_KEEPSURFACE") : 0;

      # Fit surface and subtract from input image
      foreach my $i ( 1 .. $nfiles ) {
	my ($in, $out) = $Frm->inout( $suffix, $i);

	# Mask out the source if required
	if ( $mask ) {
	  _MASK_IMAGE_ IMAGE=$in MASKED=$maskfile RADIUS=$radius
	}

	# Fit and create a background surface - default to a polynomial fit
	my $fittype = get_prim_arg($_PRIM_ARGS_, "FITTYPE", "polynomial");
	my $fitpar = get_prim_arg($_PRIM_ARGS_, "FITPAR",
				  ($fittype eq "polynomial") ? [2,2] : [4,4]);
	my @par = @{$fitpar};
	# Arguments for fitsurface depend on the fittype
	my $infile = ($mask) ? $maskfile : $in;
	my $args = "ndf=$infile fittype=$fittype ";
	$args .= ($fittype eq "spline") ? "knots=[$par[0],$par[1]]"
	  : "nxpar=$par[0] nypar=$par[1]";
	$Mon{kappa_mon}->obeyw("fitsurface","$args accept");

	# Now construct the surface from the fit - optionally keep the
	# fitted surface on disk (derive name from current file)
	my $surface = ($keepsurface) ? $Frm->inout("_surface", $i) : $tmpsurfacefile;
	$args = "in=$infile out=$surface like=$infile variance=true accept";
	$Mon{kappa_mon}->obeyw("makesurface","$args");
	# Subtract fitted surface from original image
	$Mon{kappa_mon}->obeyw("sub","in1=$in in2=$surface out=$out");
	push(@out, $out);
      }
    }

    # Store output files
    $Frm->files(@out);
    orac_print " done\n" if $verbose;

    # Set SKY_SUBTRACTION flag
    $Frm->uhdr("SKY_SUBTRACTION", 1);
    # Need to add log file creation here too
  } elsif ( $bgmethod eq "plane" ) {
    # Use existing primitive for fitting and removing a 2-D plane
    _REMOVE_PLANE_FROM_IMAGE_
  } elsif ( $bgmethod eq "dc") {
    # DC sky removal
    _REMOVE_DC_SKY_ METHOD=MEAN VERBOSE=0
  }
}
