# -*-cperl-*-

=head1 NAME

_MASK_IMAGE_ - mask out a circular region in an image

=head1 SYNOPSIS

 _MASK_IMAGE_

 _MASK_IMAGE_ IMAGE=$image MASKED=$masked XYPOS=\@xy RADIUS=50

=head1 DESCRIPTION

This primitive masks a circular region within the given image,
returning the masked image. The user may optionally choose to mask the
image outside the aperture, rather than within (the default
behaviour).

=head1 ARGUMENTS

The following arguments are supported:

=over 4

=item B<APERTURE>

A hash reference containing the details for a suitable aperture as
returned by, say, L<_DEFINE_APERTURE_|_DEFINE_APERTURE_>. If not
given one will be created at the map centre using the given radius.

=item B<GROUP>

If true, use the current Group file. Otherwise, use the current
Frame. Default is 0 (false).

=item B<IMAGE>

Input image. Default is the file in the current object.

=item B<INVERT>

Flag to denote whether to mask all data within the aperture or
without. Default is 0 (mask within).

=item B<MASKED>

Output masked image. Default is the input file with the suffix
C<_mask> appended.

=item B<RADIUS>

The radius of the aperture in arcsec. Default is 20 arcsec.

=item B<VERBOSE>

Flag to indicate whether informational messages should be given to the
user. Default is true.

=item B<XYPOS>

An array reference containing the formatted X, Y coordinates in the
chosen frame. Default is to use the position in the SkyRef WCS
attribute for absolute coordinate systems or (0,0) for offset
coordinate systems. Primitive aborts with an error if no default could
be identified.

=back

=head1 EXTERNAL TASKS

The following external tasks are used:

=over 4

=item KAPPA:

ARDMASK, WCSATTRIB

=item ORAC-DR PRIMITIVES

_DEFINE_APERTURE

=back

=head1 OUTPUT DATA

The Frame object is not updated.

=head1 OUTPUT FILES

None.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2009,2012 University of British Columbia. All Rights
Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

my $group = get_prim_arg($_PRIM_ARGS_, "GROUP", 0);
my $Obj = ($group) ? $Grp : $Frm;

# Set the input/output file names
my $in = get_prim_arg( $_PRIM_ARGS_, "IMAGE", $Obj->file );

my $masked = get_prim_arg( $_PRIM_ARGS_, "MASKED", $in ."_mask");

# Should I be verbose (default to yes)
my $verbose = get_prim_arg( $_PRIM_ARGS_, "VERBOSE", 1 );

# User-defined aperture - if none, create one using the given position and radius
my $aperture = get_prim_arg( $_PRIM_ARGS_, "APERTURE", undef );
unless ( defined $aperture ) {
  # Optional position of aperture
  my $xypos = get_prim_arg( $_PRIM_ARGS_, "XYPOS", undef );

  # Set radius of mask - default is 20 arcsec
  my $radius = get_prim_arg( $_PRIM_ARGS_,"RADIUS", 20.0 );

  _DEFINE_APERTURE_ IMAGE=$in XYPOS=$xypos RADIUS=$radius
  $aperture = $_DEFINE_APERTURE_{APERTURE};
}

# Create the temporary file for the ard description
my $ard = new ORAC::TempFile;

# Are we masking everything outside the aperture?
my $invert = get_prim_arg( $_PRIM_ARGS_, "INVERT", 0 );
my $circle = ( $invert ) ? ".NOT.CIRCLE" : "CIRCLE";

# Write the ard description - start with the coordinate frame
print {$ard->handle} "COFRAME(SKY,System=".$aperture->{cosys}.")\n";

print {$ard->handle} "$circle(".$aperture->{coords}
                    .", ".($aperture->{radius}).")\n";

# Close the file
$ard->handle->close;

my $ardin = $ard->file;

# Run ardmask
$Mon{kappa_mon}->obeyw("ardmask", "in=$in out=$masked ardfile=$ardin");

# Now repeat the process on the weights and exp_time extensions
orac_print "Masking the weights and exposure time images...\n" if ($verbose);
foreach my $ext ( "weights", "exp_time" ) {
  my $fullcomp = "more.smurf." . $ext;
  _COMPONENT_EXISTS_ COMPONENT=$fullcomp FILE=$masked
  if ($_COMPONENT_EXISTS_{EXISTS}) {
    my $inext = $masked . "." . $fullcomp;
    my $outext = ORAC::TempFile->new(0);
    $Mon{kappa_mon}->obeyw("ardmask", "in=$inext out=$outext ardfile=$ardin");
    $Mon{ndfpack_mon}->obeyw("ndfcopy","in=$outext out=$inext trimbad");
  } else {
    orac_warn "Masked file, $masked, has no $ext component\n";
  }
}

$Obj->files($masked);
$Obj->tagset("mask");
