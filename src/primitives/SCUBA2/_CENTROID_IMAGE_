# -*-perl-*-

=head1 NAME

_CENTROID_IMAGE_

=head1 DESCRIPTION

Find the centroid of the given image. Assumes that the centroid is
near the origin in axis coordinates, and that there is only one source
in the image.

Assumes that the peak value in the map is the value of the pixel at
the centroid position. This is not necessarily true though if the
centroid position contains a spike.

=head1 TASKS

Uses the KAPPA CENTROID task.

=head1 ARGUMENTS

=over 4

=item B<IMAGE>

Input image. If no image is given then the GROUP argument is used to
determine whether to use the current Group or Frame image.

=item B<BOX>

Size of search box in each dimension. Default is 9 pixels.

=item B<GROUP>

Flag to denote whether to use the current Group image or Frame
image. True (1) indicates use the Group image; else use the Frame
image. The presence of the IMAGE argument overrides this parameter.

=item B<COORD_FRAME>

Coordinate system in which to determine the centroid position. Default
is the current system.

=item B<OFFSET>

A flag to denote whether to return the centroid in offset
coordinates. A value of 1 will force the primitive to set the SkyRefIs
attribute to C<Origin>. Default is 0 (false) which will use the
current value.

=item B<VERBOSE>

Flag to indicate whether informational messages should be 
given to the user. Default is 1 (true).

=back

=head1 OUTPUT DATA

On completion the primitive hash C<%_CENTROID_IMAGE_> contains
the following keys:

=over 4

=item RESULTS

An array containing the X, Y position of the source in the current
coordinate frame.

=back

=head1 NOTE

The frame object is not updated.

This primitive is essentially a single-image version of
_CENTROID_FRAME_ and as such looks very similar.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

Copyright (C) 2007 the University of British Columbia. All Rights
Reserved.

=cut


# Set input file - leave undefined if not specified
my $in = ( exists $_CENTROID_IMAGE_{IMAGE} ) && 
         ( defined $_CENTROID_IMAGE_{IMAGE}) ? 
         $_CENTROID_IMAGE_{IMAGE} : undef;

# Should I be verbose? (Default to yes)
my $verbose = (exists $_CENTROID_IMAGE_{VERBOSE} && 
	       defined $_CENTROID_IMAGE_{VERBOSE}) ?
               $_CENTROID_IMAGE_{VERBOSE} : 1;

# If not input file was defined use the current group or frame
unless ( defined $in ) {
  # Determine if the user wants to use the current stacked Grp or Frm
  # image
  my ($Obj, $Frmgrp);
  my $group = defined( $_CENTROID_IMAGE_{GROUP} ) ? 
    $_CENTROID_IMAGE_{GROUP} : 0;
  if( $group ) {
    $Obj = $Grp;
    $Frmgrp = "Grp";
  } else {
    $Obj = $Frm;
    $Frmgrp = "Frm";
  }
  if ( $Obj->nfiles == 1 ) {
    $in = $Obj->file;
  } else {
    orac_throw "Error: _CENTROID_IMAGE_ must be run on the $Frmgrp after images have been combined\n";
  }
}

my $box = ( $_CENTROID_IMAGE_{BOX} || 9 );

my $ORAC_STATUS;

# Select the requested coordinate frame
my $cosys_out = (exists $_CENTROID_IMAGE_{COORD_FRAME} &&
		 defined $_CENTROID_IMAGE_{COORD_FRAME}) ? 
                 lc($_CENTROID_IMAGE_{COORD_FRAME}) : ""; 

my $cosys_in;
unless ( $cosys_out eq "" ) {
  $Mon{ndfpack_mon}->obeyw("wcsattrib","ndf=$in mode=get name=system");
  ($ORAC_STATUS, $cosys_in) = $Mon{ndfpack_mon}->get("wcsattrib","value");
  $Mon{ndfpack_mon}->obeyw("wcsattrib","ndf=$in mode=set name=system newval=$cosys_out");
}

# Is the centroid to be returned in Offset coordinates?
my $offset = ($_CENTROID_IMAGE_{OFFSET} || 0);
my $skyrefis;
if ( $offset ) {
  # Check if we need to set it first
  $Mon{ndfpack_mon}->obeyw("wcsattrib","ndf=$in mode=get name=skyrefis");
  ($ORAC_STATUS, $skyrefis) = $Mon{ndfpack_mon}->get("wcsattrib","value");
  if ( lc($skyrefis) eq "ignored" ) {
    $Mon{ndfpack_mon}->obeyw("wcsattrib","ndf=$in mode=set name=skyrefis newval=origin");
  }
  orac_print "Centroid will be determined in offset coordinates\n" if ($verbose);
}

# Initialize the results array
$_CENTROID_IMAGE_{RESULTS} = [];

# Need to check for kappa 0.14 or higher -- use the KAPVERSION_MINOR
# global variable
my $args;
if (starversion_lt('kappa','V0.14-0')) {
  $args = " cosys=d init=[0,0]";
} else {
  # If we have KAPPA14 we have to first make sure the wcsframe is AXIS
  # so that we can start searching from the coordinate centre
#  $Mon{ndfpack_mon}->obeyw("wcsframe","ndf=$in frame=axis");
  $args = " init='0,0' ";
}

$args .= " mode=i cerror=true search=$box";

$Mon{kappa_mon}->obeyw("centroid","ndf=$in $args");

# Read the result
my @centre;

# KAPPA 14 returns the centre pos as a string, earlier versions
# return an array -- just use XCEN and YCEN with two ADAM calls
($ORAC_STATUS, $centre[0]) = $Mon{kappa_mon}->get("centroid","xcen");
($ORAC_STATUS, $centre[1]) = $Mon{kappa_mon}->get("centroid","ycen");

# Store the results
$_CENTROID_IMAGE_{RESULTS} = [@centre];

# Reset the coordinate system if necessary
unless ( $cosys_out eq "" ) {
  $Mon{ndfpack_mon}->obeyw("wcsattrib","ndf=$in mode=set name=system newval=$cosys_in");
}
# Reset the SkyRefIs parameter if necessary
if ( $offset ) {
  if ( lc($skyrefis) eq "ignored" ) {
    $Mon{ndfpack_mon}->obeyw("wcsattrib","ndf=$in mode=set name=skyrefis newval=$skyrefis");
  }
}
