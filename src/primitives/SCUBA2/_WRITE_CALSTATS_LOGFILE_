# -*-cperl-*-

=head1 NAME

_WRITE_CALSTATS_LOGFILE_ - write logfile with results from calibrator analysis

=head1 SYNOPSIS

 _WRITE_CALSTATS_LOGFILE_

=head1 DESCRIPTION

Write out a log file with results obtained as part of analyzing a
calibrator. The log file includes the fitted and measured fluxes and
the FCFs derived from those values, plus an estimate of the beam
parameters.

=head1 ARGUMENTS

None.

=head1 EXTERNAL TASKS

=over 4

=item ORAC-DR PRIMITIVES:

_ADD_LOG_ENTRY_, _FIND_IMAGE_STATISTICS_

=back

=head1 OUTPUT DATA

None.

=head1 OUTPUT FILES

The log file, C<log.calstats> is created.

=head1 LOGFILE FORMAT

The log file contains the following additional entries (see
L<_ADD_LOG_ENTRY_|_ADD_LOG_ENTRY_> for details of the common entries):

=over 4

=item Radius

Radius of aperture (arcsec) used for photometry

=item Flux_ap

Flux (pW) estimated from aperture photometry

=item Err_ap

Uncertainty (pW) in above value

=item Noise

Noise (pW) in uncalibrated map

=item FCFasec

C<ARCSEC> flux conversion factor (Jy/arcsec**2/pW)

=item FCFasec_err

Uncertainty in above value

=item FCFbeam

C<ARCSEC> flux conversion factor (Jy/beam/pW)

=item FCFbeam_err

Uncertainty in above value

=item FCFmatch

C<BEAMMATCH> flux conversion factor (Jy/pW)

=item FCFmatch_err

Uncertainty in above value

=item FWHMmain

FWHM (arcsec) of main beam component

=item Error_beam

FWHM (arcsec) of secondary (error) beam component if derived (C<NaN>
if no error beam could be fitted)

=back

=head1 NOTES

None.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2014 University of British Columbia.
All Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

if ($Cal->iscalsource($Frm->hdr("OBJECT"))) {
  my %hdr = %{$Cal->thing};
  my (%fcf, %fluxes);
  my @units = qw/ARCSEC BEAM BEAMMATCH/;
  foreach my $units (@units) {
    $hdr{UNITS} = $units;
    my $match = $Cal->gainsindex->choosebydt('ORACTIME', \%hdr, 0);
    my $entry = $Cal->gainsindex->indexentry($match);
    $fcf{$units} = {FCF => $entry->{GAIN}, FCFERR => $entry->{GAINERR}};
    if ($units eq "ARCSEC") {
      $fluxes{FLUX} = $entry->{FLUX};
      $fluxes{FLUXERR} = $entry->{FLUXERR};
    } elsif ($units eq "BEAM") {
      $fluxes{PEAK} = $entry->{FLUX};
    }
  }

  $Frm->tagset("calstats");
  # The calibration entries were written for the file straight out of
  # the map-maker so retrieve this
  $Frm->tagretrieve("newimage");
  # Find the stats within the same region as the aperture used for the
  # total flux calculation - this can be used directly because
  # calibrator maps have a pixel scale of 1 arcsec
  my $region =  2 * $Frm->uhdr("FCF_APERTURE_RADIUS");
  _FIND_IMAGE_STATISTICS_ VERBOSE=0 REGION=$region
  my $peak = $_FIND_IMAGE_STATISTICS_{RESULTS}->{MAX};
  _FIND_IMAGE_STATISTICS_ VERBOSE=0 COMP=ERR REGION=$region
  my $results = $_FIND_IMAGE_STATISTICS_{RESULTS};
  $Frm->tagretrieve("calstats");

  my $printval = sub {
    my $value = shift;
    my $n = (@_) ? $_[0]."f" : "4f";
    return (defined $value) ? (sprintf "%.$n", $value) : "NaN";
  };

  my $logtype = "calstats";
  my @params = qw/ Flux_ap Err_ap Noise Peak_obs Peak_fit FCFasec FCFasec_err FCFbeam FCFbeam_err FCFmatch FCFmatch_err FWHMmain FWHMerr Error_beam/;
  my @values = ($printval->($fluxes{FLUX}), $printval->($fluxes{FLUXERR}),
		$printval->($results->{MEDIAN}, 5),
		$printval->($peak, 5), $printval->($fluxes{PEAK}, 5),
		(map { $printval->($fcf{$_}->{FCF}),
			 $printval->($fcf{$_}->{FCFERR})} @units),
		$printval->($Cal->fwhm_fit, 2), $printval->($Cal->fwhm_err, 2),
		$printval->()
	       );

  _ADD_LOG_ENTRY_ PARLIST=FULLER PARAMS=\@params ENTRIES=\@values GROUP=0 LOGFILE=$logtype
}
