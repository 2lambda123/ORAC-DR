# -*-perl-*-

=head1 NAME

_ALIGN_AND_MOSAIC_ - Align and mosaic images

=head1 SYNOPSIS

_ALIGN_AND_MOSAIC_ METHOD=BILINEAR REBIN=1 VERBOSE=0

=head1 DESCRIPTION

This primitive aligns and mosaics images either by using KAPPA
WCSALIGN followed by CCDPACK MAKEMOS or performing both steps with
KAPPA WCSMOSAIC. The former approach calls two other primitives to
perform the aligning and mosaicking. A check is made to see if the
version of KAPPA supports WCSMOSAIC and the primitive will fall back
to the two-step method if it does not.

If a group file exists, that is the reference for the alignment, else
the first image is chosen as reference.

This primitive operates on the current Frame object and mosaics all
files in the current Frame to produce a single output mosaic.

=head1 ARGUMENTS

=over 4

=item B<COMBINE>

The method to be used to combine the images. In principle could be any
value supported by MAKEMOS (see MAKEMOS documentation for further
details). Currently, only MEAN or MEDIAN are supported. MEDIAN is the
default. Only accessed if B<SINGLESTEP> is 0.

=item B<METHOD>

Keyword specifying the method for determing how the output pixels are
populated. The default is nearest-neighbour resampling
(C<NEAREST>). See the documentation for WCSMOSAIC or WCSALIGN for
further details. The recommended choices are C<NEAREST>, C<BILINEAR>,
or C<SINCSINC>.

=item B<REBIN>

A flag to determine whether to rebin or resample. Default is 0
(resample). See the WCSMOSAIC/WCSALIGN documentation for further
information.

=item B<SINGLESTEP>

A flag to determine whether to perform the alignment and mosaicking in
one step using WCSMOSAIC. Default is 1 (true) and should be used if
possible as it is significantly faster. If set to 0, then the
B<METHOD> argument MUST be set. Note that the values of the B<METHOD>
and B<REBIN> parameters are passed on to the primitives called if
B<SINGLESTEP> is 0.

=item B<SKYALIGN>

A flag to denote whether to align the images to a celestial coordinate
frame. The default is 0 (false) and the images will be aligned with
either the group file if it exists or the first image in the Frame.

=item B<VERBOSE>

A flag to denote whether the primitive should issue verbose
information messages. Default is true (1).

=back

=head1 EXTERNAL TASKS

The following external tasks are used:

=over 4

=item KAPPA:

WCSMOSAIC

=item ORAC-DR PRIMITIVES:

_CREATE_REFERENCE_IMAGE_, _ALIGN_ARRAYS_, _MAKE_MOSAIC_FRAME_

=back

=head1 OUTPUT FILES

Creates a single output mosaic image with a suffix of C<_fmos>.

=head1 NOTES

If no group file is present the primitive should create a non-rotated
RA/Dec reference based on the world coordinates of the first frame.
Currently, if the world coordinates are rotated the output aligned
frames will be rotated (as will be the case for a SCUBA-2 image
aligned with Nasmyth coordinates).

This primitive acts on Frames only.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2005-2007 University of British Columbia. All Rights
Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# First check for SINGLESTEP
my $singlestep = (exists $_ALIGN_AND_MOSAIC_{SINGLESTEP} &&
		  defined $_ALIGN_AND_MOSAIC_{SINGLESTEP}) ?
                  $_ALIGN_AND_MOSAIC_{SINGLESTEP} : 1;

# Rebin or resample? Default is resample (rebin = 0).
my $rebin = (exists $_ALIGN_AND_MOSAIC_{REBIN} &&
	     defined $_ALIGN_AND_MOSAIC_{REBIN}) ?
             $_ALIGN_AND_MOSAIC_{REBIN} : 0;

# Method for determining the output pixel values
my $method = (exists $_ALIGN_AND_MOSAIC_{METHOD} &&
	      defined $_ALIGN_AND_MOSAIC_{METHOD}) ? 
              lc($_ALIGN_AND_MOSAIC_{METHOD}) : "nearest"; 

# Should I be verbose? (default to yes)
my $verbose = (exists $_ALIGN_AND_MOSAIC_{VERBOSE} &&
               defined $_ALIGN_AND_MOSAIC_{VERBOSE}) ?
               $_ALIGN_AND_MOSAIC_{VERBOSE} : 1;

# Placeholder for future development to support alignment to a
# particular coordinate frame
my $skyalign = 0;

# If a single step, then check version of KAPPA is new enough
if (starversion_lt('kappa', 'V1.5')) {
  $singlestep = 0;
  orac_warn "Kappa version does not support WCSMOSAIC, falling back on dual-step method";
}

# Check if we're doing this all in one step
if ($singlestep) {

  orac_print("Aligning and mosaicking images with WCSMOSAIC\n");

  # Set the WLIM parameter for WCSMOSAIC
  my $wlim = 0.2;

  # Determine reference image for aligning all images to. If SKYALIGN
  # is set, then create a reference image in the specified Celestial
  # coordinate frame.
  my $refimage;
  if ( $skyalign ) {
    # Set the output coordinate frame
    my $celframe = "EQUATORIAL";# placeholder
    _CREATE_REFERENCE_IMAGE_ SYSTEM=$celframe VERBOSE=1
    $refimage = $_CREATE_REFERENCE_IMAGE_{REFIMAGE};
  } else {
    # if there's a Grp file, use that
    if ($Grp->file_exists) {
      $refimage = $Grp->file;
    } else {
      # Last resort use the first file in the current Frm
      $refimage = $Frm->file(1);
    }
  }

  # Write text file with list of files to align and mosaic.
  my $intmp = new ORAC::TempFile;

  # Write names of all files to temp file
  for my $i (1..$Frm->nfiles) {
    print {$intmp->handle} $Frm->file($i) ."\n";
  }
  # Create output filename, derived from first input file
  # since this is a many to one operation
  my ($in, $mosaic) = $Frm->inout('_fmos', 0);

  # Run WCSMOSAIC
  my $args = "ref=$refimage method=$method lbnd=! ubnd=! wlim=$wlim ".
    "genvar=true out=$mosaic in=^".$intmp->file;
  $Mon{kappa_mon}->obeyw("wcsmosaic","$args");

  # Remove temp file
  undef $intmp;

  # Register new file name
  $Frm->files( $mosaic );

} else { # OK, if SINGLESTEP is zero, check for COMBINE
  my $combine = (exists $_ALIGN_AND_MOSAIC_{COMBINE} &&
		 defined $_ALIGN_AND_MOSAIC_{COMBINE}) ?
		 lc($_ALIGN_AND_MOSAIC_{COMBINE}) : "median";

  # Call other primitives
  orac_print("Aligning and mosaicking subarray images in two stages \n");
  _ALIGN_ARRAYS_  METHOD=$method REBIN=$rebin
  _MAKE_MOSAIC_FRAME_ COMBINE=$combine
}
