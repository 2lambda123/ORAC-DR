=head1 NAME

_FIT_INTERFEROGRAM_PEAK_ - fits a peak in an interferogram

=head1 SYNOPSIS

 _SET_INTERFEROGRAM_PEAK_

=head1 DESCRIPTION

This primitive fits a peak in an interferogram.

=head1 ARGUMENTS

ESTIMATE can either be 'fixed' to search only near the
specified ZPD position estimate, or 'smooth' to smooth
the interferogram to determine an initial estimate of
the position.

=head1 EXTERNAL TASKS

KAPPA, SMURF, NDFPACK.

=head1 OUTPUT DATA

None

=head1 OUTPUT FILES

None.

=head1 NOTES

None.

=head1 AUTHOR

Graham Bell E<lt>g.bell@jach.hawaii.eduE<gt>

=head1 COPYRIGHT

Copyright (C) 2012 Science and Technology Facilities Council.
All Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

use IO::File;
use JSA::Headers qw/read_jcmtstate/;

my @output = ();
my @intermediate_display = ();

my $estimate = lc(get_prim_arg($_PRIM_ARGS_, 'ESTIMATE', 'fixed'));
my $zpd = $Frm->uhdr('FTS_ZPD_ESTIMATE');
orac_termerr('FTS_ZPD_ESTIMATE is missing') unless defined $zpd;
my $height = $Frm->uhdr('FTS_ZPD_PEAK_HEIGHT');
orac_termerr('FTS_ZPD_PEAK_HEIGHT is missing') unless defined $height;
my $width = $Frm->uhdr('FTS_ZPD_PEAK_WIDTH');
orac_termerr('FTS_ZPD_PEAK_WIDTH is missing') unless defined $width;
my $off = $Frm->uhdr('FTS_ZPD_BASE_OFFSET');
orac_termerr('FTS_ZPD_BASE_OFFSET is missing') unless defined $off;
my $wid = $Frm->uhdr('FTS_ZPD_BASE_WIDTH');
orac_termerr('FTS_ZPD_BASE_WIDTH is missing') unless defined $wid;
my $tol = $Frm->uhdr('FTS_ZPD_TOLERANCE');
orac_termerr('FTS_ZPD_TOLERANCE is missing') unless defined $tol;
my $pixpermm = $Frm->uhdr('FTS_PIXELS_PER_MM');
orac_termerr('FTS_PIXELS_PER_MM is missing') unless defined $pixpermm;


my $boxwidth_block = $Frm->hdr('FILTER') =~ /850/ ? 1.2 * $pixpermm : 0.6 * $pixpermm;
my $boxwidth_findback = $Frm->hdr('FILTER') =~ /850/ ? 0.6 * $pixpermm : 0.3 * $pixpermm;


my $range = sprintf('%.03f:%.03f', $zpd - ($off + $wid), $zpd - $off);


# Write FIT1D config file.

my $cfg = new ORAC::TempFile(OPEN => 1, SUFFIX => '.def');
{
  local $\ = "\n"; local $, = "\n";
  my $fh= $cfg->handle();
  print $fh '^'.$ENV{'SMURF_DIR'}.'/smurf_fit1d.def',
            'function=gaussian',
            'ncomp=1',
            'minwidth=1',
            'axis=3';
  $fh->close();
}


my $shutter = undef;
do {
  # Use FTS-2 shutter FITS headers if available.

  my $shut_8d = $Frm->hdrval('FTS_SH8D');
  my $shut_8c = $Frm->hdrval('FTS_SH8C');

  # Possible values for these headers are still TBC
  # so do not attempt to process them yet.
};
unless (defined($shutter)) {
  # Only necessary to determine the shutter status in this
  # way if we do not have the shutter FITS headers, e.g.
  # for data taken prior to the introduction of these headers.

  my $ocscfg = new ORAC::TempFile(OPEN => 0, SUFFIX => '.xml');
  my $opt = $Frm->raw() . '.MORE.JCMTOCS.CONFIG ' . $ocscfg;
  $Mon{'hdstools_mon'}->obeyw('hwrite', $opt);
  my $fh = new IO::File($ocscfg);
  local $/ = undef;
  $_ = <$fh>; s/\n//g;
  $fh->close();

  if (/SHUT8C +VALUE="INBEAM"/ and /SHUT8D +VALUE="OUTOFBEAM"/) {
    $shutter = 's8c';
  } elsif (/SHUT8D +VALUE="INBEAM"/ and /SHUT8C +VALUE="OUTOFBEAM"/) {
    $shutter = 's8d';
  } else {
    orac_termerr('Could not determine which shutter was in the beam.');
  }
}

# Which shutter the 450um subarrays correspond to:
my %shutter = (s4a => 's8d', s4b => 's8c');

for (my $i = 1; $i <= $Frm->nfiles(); $i ++) {
  my ($in, $neg) = $Frm->inout('_neg', $i);
  my $subarray = lc($Frm->subarray($in));

  # Invert interferogram if we expect negative fringes.
  my $beam = $shutter{$subarray} || $subarray;

  if (($subarray =~ /^s8/) ? ($shutter ne $beam)
                           : ($shutter eq $beam)) {
    orac_say("Shutter in $shutter beam so inverting $subarray interferogram.");
    $Frm->push_intermediates($neg);
    $Mon{'kappa_mon'}->obeyw('cmult', "$in -1.0 $neg");
    $in = $neg;
  }

  # Threshold interferogram (to remove negative fringes).

  my (undef, $thr) = $Frm->inout('_thr', $i);
  $Frm->push_intermediates($thr);
  $Mon{'kappa_mon'}->obeyw('thresh', "$in $thr thrlo=0 newlo=0 thrhi=1000 newhi=bad");

  my $fitshdr = new ORAC::TempFile(OPEN => 0, SUFFIX => '.lis');
  $Mon{'ndfpack_mon'}->obeyw('fitslist', "$thr $fitshdr");

  # Estimate interferogram RMS.
  # Currently do this by taking the 75% percentile of the baselined
  # interferogram in one of the baseline regions.  (Calculating
  # sigma directly gives a very high value, presumably due to
  # bad pixels.)

  $Mon{'kappa_mon'}->obeyw('stats', "$in(,,$range) data order=true percentiles=[75]");
  my ($ORAC_STATUS, @values) = $Mon{'kappa_mon'}->get('stats', qw/PERVAL/);
  my $rms = $values[0];

  # Determine initial ZPD estimate.

  my @fitparams = (
                    'comp1.c=' . $width,
                    'fix1.c=1',
                  );
  my $parndf = '!';

  if ($estimate eq 'fixed') {
    push @fitparams, 'comp1.b=' . $zpd,
                     'comp1.a=' . $height,
  }
  elsif ($estimate eq 'smooth') {
    # In this mode, we smooth the absolute value of the interferogram
    # to fit the ZPD feature.  This gives us an NDF we can feed back
    # into FIT1D to fit the central fringe.

    my (undef, $bak) = $Frm->inout('_bak', $i);
    $Frm->push_intermediates($bak);
    $Mon{'cupid_mon'}->obeyw('findback', "$in $bak box=[1,1,$boxwidth_findback] rms=!");
    my (undef, $abs) = $Frm->inout('_abs', $i);
    $Frm->push_intermediates($abs);
    $Mon{'kappa_mon'}->obeyw('sub', "$in $bak $abs");

    my (undef, $smo) = $Frm->inout('_smo', $i);
    $Frm->push_intermediates($smo);
    $Mon{'kappa_mon'}->obeyw('block', "$abs $smo box=[1,1,$boxwidth_block]");

    (undef, $parndf) = $Frm->inout('_sft', $i);
    $Frm->push_intermediates($parndf);
    $Mon{'smurf_mon'}->obeyw('fit1d', "in=$smo out=$parndf parndf=! rms=$rms config=^$cfg userval=\"comp1.c=$wid\"");

    my (undef, $sps) = $Frm->inout('_sps', $i);
    $Frm->push_intermediates($sps);
    $Mon{'ndfpack_mon'}->obeyw('ndfcopy', "$parndf.MORE.SMURF_FIT1D.COMP_1\(,,2\) $sps trim=false");

    _FIND_IMAGE_STATISTICS_ IMAGE=$sps COMP=data VERBOSE=0
    my $sps_median = $_FIND_IMAGE_STATISTICS_{RESULTS}->{MEDIAN};
    orac_say("Median fit to smoothed interferogram: $sps_median");

    my $min = $sps_median - $tol;
    my $max = $sps_median + $tol;
    my (undef, $sps_thr) = $Frm->inout('_spsthr', $i);
    $Frm->push_intermediates($sps_thr);
    $Mon{'kappa_mon'}->obeyw('thresh', "$sps $sps_thr $min $max bad bad");

    # Copy across FITS headers to stop ORAC-DR complaining.
    $Mon{'ndfpack_mon'}->obeyw('fitstext', "$sps_thr $fitshdr");

    push @intermediate_display, $sps_thr;

    my (undef, $sps_fil) = $Frm->inout('_spsfil', $i);
    $Frm->push_intermediates($sps_fil);
    $Mon{'kappa_mon'}->obeyw('fillbad', "$sps_thr $sps_fil niter=2 size=[2,2]");

    my (undef, $sps_pas) = $Frm->inout('_sftpas', $i);
    $Frm->push_intermediates($sps_pas);
    $Mon{'kappa_mon'}->obeyw('paste', "$parndf.MORE.SMURF_FIT1D.COMP_1 $sps_fil out=$sps_pas");

    $Mon{'hdstools_mon'}->obeyw('hcopy', "$sps_pas $parndf.MORE.SMURF_FIT1D.COMP_1");
  }
  else {
    orac_termerr('Unknown ZPD estimation method ' . $estimate);
  }

  # Write FIT1D values file.

  my $val = new ORAC::TempFile(OPEN => 1, SUFFIX => '.def');
  {
    local $\ = "\n"; local $, = "\n";
    my $fh= $val->handle();
    print $fh @fitparams;
    $fh->close();
  }

  # Perform fit.

  my (undef, $fit) = $Frm->inout('_fit', $i);
  $Frm->push_intermediates($fit);
  $Mon{'smurf_mon'}->obeyw('fit1d', "in=$thr out=$fit parndf=$parndf rms=$rms config=^$cfg userval=^$val");

  my (undef, $pos) = $Frm->inout('_pos', $i);
  $Mon{'ndfpack_mon'}->obeyw('ndfcopy', "$fit.MORE.SMURF_FIT1D.COMP_1\(,,2\) $pos trim=true");

  # Copy across FITS headers to stop ORAC-DR complaining.

  $Mon{'ndfpack_mon'}->obeyw('fitstext', "$pos $fitshdr");

  push @output, $pos;
}

if (@intermediate_display) {
  $Frm->files(@intermediate_display);
  _SET_TAG_ TAG=zpd_pos_estimate
}

$Frm->files(@output);

