# -*-cperl-*-

=head1 NAME

_GET_LOG_PARAMETERS_ - Obtain logging parameters common to all log files

=head1 SYNOPSIS

  _GET_LOG_PARAMETERS_ GROUP=1

=head1 DESCRIPTION

Retrieve logging parameters from the specified Frame or Group
object. The parameters returned are those common to all log files
written by the SCUBA-2 pipeline. They are returned as an array
reference (see C<OUTPUT DATA> below). The user may optionally return a
basic set of parameters suitable for non-tracking observing modes
(e.g. NOISE or FLATFIELD).

=head1 ARGUMENTS

The following arguments are supported:

=over 4

=item B<GROUP>

Flag to indicate whether to obtain parameters from the Group
object. Default is 0 (use the current Frame).

=item B<PARLIST>

Define the amount of information returned. May be C<FULL>, C<FULLER>
or C<BRIEF>.

=back

=head1 EXTERNAL TASKS

None.

=head1 OUTPUT DATA

On exit, the primitive hash, C<%_GET_LOG_PARAMETERS_> contains a
single key called PARAMS. The value stored in this key is an array
reference with the following elements:

=over 4

=item *

UT time of observation

=item *

HST of observation

=item *

Observation number

=item *

Source name

=item *

Observation mode or scan pattern if mode is C<scan>

=item *

Filter name for current observation

=item *

Mean elevation in degrees for duration of observation

=item *

Mean airmass for duration of observation (FULLER only)

=item *

Mean transmission along line of sight for duration of observation
(FULLER only)

=item *

Mean zenith optical depth at 225 GHz for duration of observation

=item *

Mean zenith tau corresponding to the current filter for duration of
observation

=back

=head1 OUTPUT FILES

None

=head1 KNOWN ISSUES

This primitive uses translated header entries, which may not be
defined for Group objects. Either C<UNKNOWN> or a string composed of
multiple asterisks is substituted.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>,
Tim Jenness <lt>t.jenness@jach.hawaii.eduE<gt>

Copyright (C) 2008-2009 Science & Technology Facilities Council.
Copyright (C) 2008,2013-2014 the University of British Columbia.
All Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# Read Frame or Group header...
my $Obj = ($_PRIM_ARGS_->{GROUP} ? $Grp : $Frm);

# Default parameter to write out
my $paramlist = uc(get_prim_arg($_PRIM_ARGS_, "PARLIST", "FULL"));

# ORACUT
my $utdate = $Obj->hdr('ORACUT');
my $uttime = sprintf "%.7f", $Obj->hdr('ORACTIME');
$uttime = sprintf "%17s", $uttime;

# HSTSTART (no decimal places)
my $hst    = $Obj->hdr('HSTSTART');
if (defined $hst) {
  my (@hst)  = split(/:/, $hst);
  $hst = sprintf '%5s', $hst[0] . ":" . $hst[1] .":". $hst[2];
} else {
  my $ut = $Obj->uhdr('ORAC_UTSTART');
  if (defined $ut) {
    $hst = sprintf('%02d:%02d:%02dUT', $ut->hour, $ut->min, $ut->sec);
  } else {
    $hst = "UNKNOWN";
  }
}

$hst = sprintf "%22s", $hst;

# Observation number
my $obsnum = sprintf "%4d", $Obj->uhdr('ORAC_OBSERVATION_NUMBER');

# OBJECT
my $src = substr((sprintf "%-20s",
                  (defined $Obj->uhdr('ORAC_OBJECT') ?
                   uc($Obj->uhdr('ORAC_OBJECT')) : "<none>" )),0,12);
# Replace spaces with underscores, remove any trailing spaces beforehand
$src =~ s/\s+$//;
$src =~ s/\s+/\_/g;
$src = sprintf "%20s", $src;

# MODE
my $mode = $Obj->uhdr("ORAC_OBSERVATION_MODE");
if ($mode eq 'scan' and defined ($Obj->hdr("SCAN_PAT"))) {
  $mode = "daisy" if ($Obj->hdr("SCAN_PAT") =~ /daisy/i);
  $mode = "pong" if ($Obj->hdr("SCAN_PAT") =~ /pong/i);
}
$mode = sprintf "%11s", $mode;

# Filter System
my $filter = $Obj->uhdr("ORAC_FILTER");
$filter = sprintf "%6s", $filter;

my @briefpar = ($uttime, $hst, $obsnum);

if ( $paramlist eq "BRIEF" ) {
  $_GET_LOG_PARAMETERS_{PARAMS} = \@briefpar;
} else {
  # Average of start and end elevation
  my $el = "**";
  if ( defined $Obj->uhdr('ORAC_ELEVATION_START') &&
       defined $Obj->uhdr('ORAC_ELEVATION_END') ) {
    $el = 0.5 * ($Obj->uhdr('ORAC_ELEVATION_START') +
                 $Obj->uhdr('ORAC_ELEVATION_END'));
    $el = sprintf "%5d", $el;
  }

  # Get tau for the current filter
  my $tau = sprintf "%-5s","*****";
  my ($airmass, $trans);
  my $wvmtau = 0.5 * ( $Obj->hdrval("WVMTAUST",0) +
                       $Obj->hdrval("WVMTAUEN",-1) );
  my $tau225 = (defined $wvmtau) ? (sprintf "%6.3f", $wvmtau) : "*****";
  if ($paramlist eq "FULL") {
    if ($wvmtau) {
      my ($tauval, $status) = JCMT::Tau::SCUBA2::get_tau($filter, 'CSO', $wvmtau);
      $tau = sprintf "%5.3f", $tauval if defined $tauval;
    }
  } else {
    # OK so airmass and transmission are also needed - mean
    # transmission is not simply derived from mean airmass * mean tau,
    # though it's probably close enough in most cases
    if ( defined $Obj->uhdr('ORAC_AIRMASS_START') &&
         defined $Obj->uhdr('ORAC_AIRMASS_END') ) {
      $airmass = 0.5 * ($Obj->uhdr('ORAC_AIRMASS_START') +
                        $Obj->uhdr('ORAC_AIRMASS_END'));
      $airmass = sprintf "%7.2f", $airmass;
    }
    $trans = "****";
    if ( defined $Obj->hdrval("WVMTAUST",0) &&
         defined $Obj->hdrval("WVMTAUEN",-1) &&
         defined $airmass) {
      my ($taust, $status) = JCMT::Tau::SCUBA2::get_tau($filter, 'CSO',
                                                        $Obj->hdrval("WVMTAUST",0));
      (my $tauen, $status) = JCMT::Tau::SCUBA2::get_tau($filter, 'CSO',
                                                        $Obj->hdrval("WVMTAUEN",-1));
      if (defined $taust && defined $tauen) {
        my $transt = exp(-$taust * $Obj->uhdr("ORAC_AIRMASS_START"));
        my $tranen = exp(-$tauen * $Obj->uhdr("ORAC_AIRMASS_END"));
        $trans = sprintf "%6.3f", (0.5 * ($transt + $tranen));
        $tau = sprintf "%5.3f", (0.5*($taust+$tauen));
      }
    }

  }
  $airmass  = "****" unless (defined $airmass);
  $airmass = sprintf "%7s", $airmass;
  $trans = (defined $trans) ? sprintf "%6s", $trans: "****";
  my @fullpar = (@briefpar, $src, $mode, $filter, $el);
  push(@fullpar, $airmass, $trans) if ($paramlist ne "FULL");
  push(@fullpar, $tau225, $tau);

  $_GET_LOG_PARAMETERS_{PARAMS} = \@fullpar;
}
