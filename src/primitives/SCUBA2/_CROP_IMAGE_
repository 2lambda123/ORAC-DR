# -*-cperl-*-

=head1 NAME

_CROP_IMAGE_ - Trim image to a desired size

=head1 SYNOPSIS

 _CROP_IMAGE_

 _CROP_IMAGE_ IN=$infile

 _CROP_IMAGE_ WIDTH=240 HEIGHT=400

=head1 DESCRIPTION

This primitive may be used to trim an image to a desired size. The
default behaviour is to use the map parameters defined in the Frame or
Group object header to define the size of the cropped image. The user
may override the map width and height using the parameters described
below.

All NDFs within a file are trimmed including, e.g., the EXP_TIME and
WEIGHTS components of SCUBA-2 images. There is no need to treat these
separately.

=head1 ARGUMENTS

The following arguments are supported:

=over 4

=item B<GROUP>

Flag to denote whether to use the current Frame or Group
object. Default is 0 (use Frame).

=item B<HEIGHT>

Output map height in arcsec. If not specified, the map height in the
header will be used.

=item B<IMAGE>

Name of input file. The primitive will use the current Frame file if
not specified.

=item B<OUT>

Optional parameter to specify the name of the trimmed output
file. Default is to use the Frame inout method to derive the name, or
just append C<_crop> if called from a PICARD recipe.

=item B<WIDTH>

Output map width in arcsec. If not specified, the map width in the
header will be used.

=back

=head1 EXTERNAL TASKS

The following external task is used:

=over 4

=item ORAC-DR PRIMITIVES

_MASK_AST_REGION_

=back

=head1 OUTPUT DATA

None.

=head1 OUTPUT FILES

Produces output files with the suffix C<_crop> if B<OUT> is not
specified.

=head1 NOTES

This primitive should be used on Frame objects only.

It is up to the user to register the output file with the Frame.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2010-2013 University of British Columbia. All Rights
Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# Input/output files
my $infile = get_prim_arg($_PRIM_ARGS_, "IMAGE", undef);

my $group = get_prim_arg($_PRIM_ARGS_, "GROUP", 0);
my $Obj = ($group) ? $Grp : $Frm;
$infile = $Obj->file unless ($infile);

my $outfile = get_prim_arg($_PRIM_ARGS_, "OUT", undef);
$outfile = ($Frm->uhdr("PICARD") ? $infile."_crop" : $Obj->inout("_crop"))
  unless ($outfile);

# Retrieve Frame header
my $hdr = $Frm->hdr;

# New map parameters
my $width  = get_prim_arg($_PRIM_ARGS_, "WIDTH", undef);
my $height = get_prim_arg($_PRIM_ARGS_, "HEIGHT", undef);

# Set new width/height if defined, but ensure the default is not too
# small: force a default size of 60 arcsec
use constant MINMAPSIZE => 60.0;
my $set_size = sub {
  my ($typ, $size) = @_;
  my $mapkey = "MAP_".$typ;
  if (defined $size) {
    $hdr->{$mapkey} = $size;
  } else {
    $hdr->{$mapkey} = MINMAPSIZE
      if ($hdr->{$mapkey} < MINMAPSIZE);
  }
};
$set_size->("WDTH", $width);
$set_size->("HGHT", $height);

# Check that the MAP_PA is also set - assume a value of 0 if not
unless (defined $hdr->{"MAP_PA"}) {
  orac_warn "MAP_PA is missing from FITS headers - assuming a value of 0\n";
  $hdr->{"MAP_PA"} = 0;
}

# Check the AlignSystem WCS attribute and set it the same as the
# tracking system if necessary
$Mon{ndfpack_mon}->obeyw("wcsattrib","ndf=$infile mode=get name=alignsystem(1)");
my ($ORAC_STATUS, $alignsys) = $Mon{ndfpack_mon}->get("wcsattrib","value");
# Catch synonymous values
$alignsys = "FK5" if (uc($alignsys) eq "ICRS");

my %TRACK2AST = ( 'J2000' => 'FK5',
                  'B1950' => 'FK4',
                  'APP' => 'GAPPT',
                  'GALACTIC' => 'GALACTIC',
                  'GAL' => 'GALACTIC' );
my $tracksys = $TRACK2AST{ uc( $Frm->hdr("TRACKSYS") ) };
if ( $alignsys ne $tracksys ) {
  $Mon{ndfpack_mon}->obeyw("wcsattrib","ndf=$infile mode=set name=alignsystem(1) newval=$tracksys");
}

# Create an AST region with the given map parameters
use JCMT::MapArea;
my $astregion = &JCMT::MapArea::region( header => $hdr );
# Store in Frm uhdr, regardless of object being used
$Frm->uhdr("MAP_ASTREGION",$astregion);

# Trim the image
_MASK_AST_REGION_ IN=$infile OUT=$outfile GROUP=0

# Restore AlignSystem to same as on entry
if ( $alignsys ne $tracksys ) {
  $Mon{ndfpack_mon}->obeyw("wcsattrib","ndf=$infile mode=set name=alignsystem(1) newval=$alignsys");
}
