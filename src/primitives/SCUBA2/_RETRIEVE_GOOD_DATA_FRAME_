# -*-cperl-*-

=head1 NAME

_RETRIEVE_GOOD_DATA_FRAME_ - retrieve files with good flatfields

=head1 SYNOPSIS

_RETRIEVE_GOOD_DATA_FRAME_

=head1 DESCRIPTION

This primitive examines the flatfield solutions from the current Frame
object and returns a list of data files for those subarrays which have
good flatfield solutions.

The files with good flatfield solutions are returned in the primitive
hash as a list (C<FILES>) or indexed by subarray (C<FILES_SUB>). If
there are no good flatfields, neither key is defined. Note that the
list of files in C<FILES> has the fastflats at the end of the list if
requested to accommodate the summit pipeline.

=head1 ARGUMENTS

The following arguments are supported:

=over 2

=item B<FASTFLATS>

Flag to denote whether to return the raw fastflat files along with the
data. Default is 1 (yes).

=item B<FRAME>

The Frame object from which good files are retrieved. Default is
C<$Frm>.

=back

=head1 EXTERNAL TASKS

The following external task is used:

=over 2

=item ORAC-DR PRIMITIVES

_RETRIEVE_FASTFLATS_

=back

=head1 OUTPUT DATA

The primitive hash contains two entries if successful: C<FILES> is an
array reference with the list of files (including raw fastflats if
necessary) and C<FILES_SUB> which contains a hash reference of the
files indexed by subarray.

=head1 OUTPUT FILES

None.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2012 University of British Columbia. All Rights
Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# Input Frame object to retrieve files from
my $Frmobj = get_prim_arg($_PRIM_ARGS_, "FRAME", $Frm);

# Default to including fastflats in the list of files returned
my $incfastflats = get_prim_arg($_PRIM_ARGS_, "FASTFLATS", 1);

# Localize $Frm here to enable flatfield retrieval
{
  my $Frm = $Frmobj;

  # Retrieve the list of datafiles in current Frame
  my %allfiles = $Frm->get_files_by_subarray;
  my (%goodfiles, @goodfiles);
  if ($Frm->tagretrieve("flatfield")) {
    # If the data are flatfielded, then by definition they will be good
    %goodfiles = %allfiles;
    @goodfiles = $Frm->files;
  } else {
    my $nbolosum = 0;
    # First step - retrieve the flatfield solutions to determine which
    # subarrays have good solutions
    _RETRIEVE_FASTFLATS_ TYPE=SOL
    $nbolosum = $_RETRIEVE_FASTFLATS_{NBOLO_SUM};

    # Proceed if there are good solutions
    if (defined $_RETRIEVE_FASTFLATS_{NBOLO_SUB}) {
      my %subarrays = %{$_RETRIEVE_FASTFLATS_{NBOLO_SUB}};
      # Store data files with good flatfields
      foreach my $subarray (sort keys %allfiles) {
	$goodfiles{$subarray} = $allfiles{$subarray} if ($subarrays{$subarray} > 0);
      }
      @goodfiles = map { @{$goodfiles{$_}} } sort keys %goodfiles;

      # Retrieve and store raw flatfield files if necessary. Note this
      # must be done as a separate step to ensure the summit pipeline
      # creates unique filenames for flatfielded data.
      my (%fastflats, @fastflats);
      if ($incfastflats && $nbolosum > 0) {
	# Retrieve from the calibration system
	_RETRIEVE_FASTFLATS_ TYPE=RAW
	if (defined $_RETRIEVE_FASTFLATS_{FASTFLATS_SUB}) {
	  %fastflats = %{$_RETRIEVE_FASTFLATS_{FASTFLATS_SUB}};
	  @fastflats = @{$_RETRIEVE_FASTFLATS_{FASTFLATS}};
	}
	# Now select the data files for which good flatfield solutions exist
	if (%fastflats) {
	  foreach my $subarray (sort keys %goodfiles) {
	    my @files = @{$goodfiles{$subarray}};
	    push(@files, $fastflats{$subarray});
	    $goodfiles{$subarray} = \@files;
	  }
	  # Adding the fastflats to the end of the list is necessary
	  # for the summit pipeline, except if the solutions were
	  # stored in the uhdr - in that case, the Frame will already
	  # contain the fastflat files.
	  push(@goodfiles, @fastflats) unless ($Frm->uhdr("FLATS"));
	} else {
	  orac_err "Flatfield solutions were found, but no corresponding raw files\n";
	  if ($Frm->uhdr("QUICKLOOK") || $Frm->uhdr("SUMMIT")) {
	    orac_err "Perhaps the initial fastflat was skipped for this observation?\n";
	    $Frm->uhdr("NOFASTFLATS",1);
	  }
	}
      }

      # Store the results or issue a warning
      unless (%goodfiles) {
	orac_warn "All flatfields failed - unable to retrieve any good data\n";
      }
    } else {
      # No results - but we can't distinguish between 1) no
      # calculations were made and 2) the calculations failed. Since
      # option 1 occurs in the offline pipeline (and we will always
      # reach here in the offline pipeline) this is only a problem for
      # the SUMMIT and QL pipelines. However, in the interests of
      # keeping the pipeline running, just issue a warning and
      # continue as if everything is fine.
      orac_warn "Unable to retrieve flatfield solutions - assuming all data are good\n"
	if ($Frm->uhdr("SUMMIT") || $Frm->uhdr("QUICKLOOK"));
      %goodfiles = %allfiles;
      @goodfiles = $Frm->files;
    }
  }
  $_PRIM_ARGS_->{FILES_SUB} = \%goodfiles;
  $_PRIM_ARGS_->{FILES} = [ @goodfiles ];

} # Temporary override of $Frm goes out of scope here
