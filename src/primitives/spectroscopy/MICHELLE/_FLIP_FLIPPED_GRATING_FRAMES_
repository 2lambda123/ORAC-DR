# _FLIP_LOWQ_GRATING_FRAMES_

=head1 NAME 

_FLIP_LOWQ_GRATING_FRAMES_  - Flips frames taken with the low-Q or MedN2 gratings

=head1 DESCRIPTION

The low-Q and MedN2 gratings are installed the wrong way round in the
cryostat. We work around this by simply driving the grating angle to
what would have been negative orders. A side effect of this is that
the images are flipped along their dispersion axis. This primitive flips
them back.

=head1 NOTES

=over 4

=item *

The flip is only done for the lowQ and MedN2 gratings.

=item *

The flip is only done for data taken before 20040309.

=back

=head1 AUTHORS

Paul Hirst <p.hirst@jach.hawaii.edu>
Brad Cavanagh <b.cavanagh@jach.hawaii.edu>

=head1 COPYRIGHT

Copyright (C) 2004 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut

# Only do this if the data is before 20040309.
my $utdate = $Frm->uhdr( "ORAC_UTDATE" );
if( defined( $utdate ) ) {
  $utdate =~ s/-//g;
} else {
  $utdate = 0;
  orac_warn "UT date not set. Will flip data in wavelength direction.\n";
}

if( $utdate < 20040309 ) {

  my $grating = $Frm->uhdr('ORAC_GRATING_NAME');
  my $nfiles = $Frm->nfiles;

# Only do this if we have the lowQ or MedN2 grating.
  if ( ( uc( $grating ) eq 'LOWQ') || ( uc( $grating ) eq 'MEDN2' ) ) {

# And do it for each subframe.
    foreach my $i (1 .. $nfiles) {
      (my $in, my $out) = $Frm->inout("_flg", $i);
      $Mon{'kappa_mon'}->obeyw("flip", "in=$in out=$out title=$out dim=1");
      orac_print "$in to $out: Flipped $grating grating\n";
      $Frm->file($i, $out);
    }
  }
}
