=head1 NAME

_PAIR_REDUCTION_SUBTRACT_ - Subtracts the off-source beam frame from the
on-source beam frame.

=head1 DESCRIPTION

Subtracts the sky / offset beam frame from the object / main beam frame
when taking pair-wise data. This primitive relies on a steering primitive
to set the ON_SOURCE user header to determine which frame is on-source. If
this user header is not set, the primitive will subtract two frames
regardless, warn the user, and continue on.

=head1 OUTPUT

An _ss (Sky subtracted) frame for every complete pair

=head1 AUTHOR

Paul Hirst <p.hirst@jach.hawaii.edu>

=head1 COPYRIGHT

Copyright (C) 1998-2003 Particle Physics and Astronomy Research
Council. All Rights Reserved.

=cut

my $main;
my $offset;
my $mFrm;

# If we're on source,
if ($Frm->uhdr("ON_SOURCE")) {
   # This is the object / main-beam frame
   $main = $Frm->file;
   $mFrm = $Frm;
   # The previous frame is the offset-beam frame
   my $oFrm = ($Grp->members)[-2];
   if ($oFrm->uhdr("ON_SOURCE")) {
      # Oh bugger, it's not.
      # Have a winge. 
      orac_warn "Problem figuring out which frame is the on-source frame\n Continuing regardless - this may or may not be doing something sensible.\n";
   }
   $offset = $oFrm->file;
} else {
   # This is the offset-beam frame
   $offset = $Frm->file;
   # The previous frame is the main beam frame
   my $oFrm = ($Grp->members)[-2];
   if (!($oFrm->uhdr("ON_SOURCE"))) {
      # Oh bugger, it's not.
      # Have a winge. 
      orac_warn "Problem figuring out which frame is the offset-from-source frame\n Continuing regardless - this may or may not be doing something sensible.\n"; 
   }
   $main = $oFrm->file;
   $mFrm = $oFrm;
}

# Get the $out filename. We won't actually use $in as
# $main and $offset now hold the appropriate filenames
my ($in, $out) = $mFrm->inout ("_ss");

# Do the sky subtraction
$Mon{'kappa_mon'}->obeyw("sub", "in1=$main in2=$offset out=$out title=$out");

# Update the Frm object
$Frm->file($out);

# Print message
orac_print "$offset subtracted from $main to give: $out\n";
orac_print "\n";

# Display if desired.
$Display->display_data( $Frm ) if defined $Display;
