# -*-perl-*-

=head1 NAME

COADD_MULTIPLE_EXPOSURES - coadd sub-frames to give one output frame

=head1 DESCRIPTION

Coadds sub-frames via averaging to give a single output frame.

=head1 PARAMETERS

=over 4

none

=back

=head1 ORAC

=head2 Engines referenced

KAPPA

=head2 Tasks called

=over 4

=item ccdpack_reg

makemos

=back

=head2 Objects addressed

$Frm

=head2 REVISION

  $Id$

=head1 AUTHOR

frossie@jach.hawaii.edu

=cut

# Determine the number of exposures there are to combine.
    my $nfiles = $Frm->nfiles;
    if ( $nfiles != 1 ) {

# Co-add the files
# ================

# Generate a temporary text file containing the list of files to
# combine.
       my $tmp = new ORAC::TempFile;
       if ( starversion_gt( "CCDPACK", 'V3.0-1' ) ) {
          foreach my $i ( 1..$nfiles ) {
             print {$tmp->handle} $Frm->file( $i ) . "\n";
          }

# Until recently CCDPACK could not handle the HDS container files of
# multiple NDFS.  So for these versions of CCDPACK, we need to copy
# the frames to simple NDFs, which are temporary files.
       } else {
          my @temp;
          foreach my $i ( 1..$nfiles ) {
             $temp[$i] = new ORAC::TempFile;
             my $tempname = $temp[ $i ]->file;
             my $orig = $Frm->file( $i );
             $Mon{'ndfpack_mon'}->obeyw( "ndfcopy", "in=$orig out=$tempname" );
             print {$tmp->handle} $tempname . "\n";
          }
       }

# Close the list of files to process.
       $tmp->handle->close;

# Generate the name of the output file.
       $Frm->files( $Frm->file );
       my ( $in, $out ) = $Frm->inout( "_co" );

# Average the files together without applying corrections for
# scaling and offset between frames.  Variance is not computed.  There is
# also no CCDPACK logging.
       my $param = "noscale nogenvar nousevar nozero logto=neither logfile=!";
       my $title = "$out (Coadded)";
       my $mosargs = "in=^" . $tmp->file . " method=mean title='$title'";
       $Mon{'ccdpack_reg'}->obeyw( "makemos", "$mosargs $param out=$out" );

# Housekeeping
# ============

# Update $Frm object, merging the headers.
       $Frm->file( $out );
       $Frm->mergehdr;

# Display the co-added image.
       $Display->display_data( $Frm ) if defined $Display;
  
# Print congratulatory message.
       orac_print "Frames coadded into $out\n";
    }
