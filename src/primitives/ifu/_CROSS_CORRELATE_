# -*-cperl-*-

=head1 NAME

_CROSS_CORRELATE_ - Perform cross-correlation between a reference arc
spectrum and the central row of the current Frame.

=head1 DESCRIPTION

This primitive performs cross-correlation to determine a shift between
a reference arc spectrum and the central row of the current Frame, which
should probably be an arc spectrum.

The reference arc spectrum is determined using the calibration system
and copied into the C<ORAC_DATA_OUT> directory. The central row of the
current frame is extracted to a file with a _crow suffix, and the two
are cross-correlated using FIGARO/SCROSS. The cross-correlation function
is stored in a file with a _ccf suffix. The resulting shift in pixels
is returned via the SHIFT parameter.

=head1 ARGUMENTS

=over 4

=item SHIFT = REAL (Returned)

The shift in pixels between the reference arc and the current Frame.

=back

=head1 NOTES

=over 4

=item *

As this primitive copies the reference arc from the C<ORAC_DATA_CAL>
directory, it will not use a reference arc supplied on the commandline.

=back

=head1 AUTHORS

Brad Cavanagh (b.cavanagh@jach.hawaii.edu) (JAC)

=head1 COPYRIGHT

Copyright (C) 2003 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut

# Find out what the shift is between our input image and the reference
# spectrum using cross-correlation.
my $shift = 0;
my $calibratedarc = $Cal->calibratedarc;
if( !defined( $calibratedarc ) ) {
  orac_warn( "Could not find calibrated arc spectrum. Will not do cross-correlation.\n" );

} else {

  # Copy the calibrated arc to a temporary file.
  $calibratedarc = $Cal->find_file($calibratedarc . ".sdf");

  orac_print( "Using $calibratedarc as reference arc spectrum.\n");

  $calibratedarc =~ s/\.sdf$//;

  my $tmp = new ORAC::TempFile;
  my $tmpfile = $tmp->file;
  $Mon{'ndfpack_mon'}->obeyw("ndfcopy", "in=$calibratedarc out=$tmpfile title=!");

  # Remove the axis because it causes scross to fall over.
  $Mon{'ndfpack_mon'}->obeyw("setaxis", "ndf=$tmpfile mode=Delete");

  ( my $in, my $out_row ) = $Frm->inout("_crow");

  # We have a reference arc (in $calibratedarc), now we need to extract
  # a central row from our image.
  my $param1 = "in=$in(,~1) out=$out_row title=!";
  my $param2 = "trim=true trimwcs=true";
  $Mon{'ndfpack_mon'}->obeyw("ndfcopy", "$param1 $param2");
  orac_print( "Copied central row of $in to $out_row for cross-correlation.\n" );

  # Cross-correlate this row with the reference arc.
  ($in, my $ccfout) = $Frm->inout("_ccf");
  $param1 = "spectrum=$out_row template=$tmpfile cross=$ccfout";
  $param2 = "record xstart=min xend=max";
  $Mon{'figaro1'}->obeyw("scross", "$param1 $param2");
  ( my $ORAC_STATUS, $shift ) = $Mon{'figaro1'}->get("scross", "shift");

  # Report to user.
  orac_print( "Cross-correlation function in $ccfout.\n" );
  orac_print( "Cross-correlation found a shift of $shift pixels.\n" );

}

# Return the shift to the caller.
$_CROSS_CORRELATE_{'SHIFT'} = $shift;
