=head1 NAME

_CONTROL_CGS4DR_ - send a REDUCE command to CGS4DR

=head1 DESCRIPTION

Command CGS4DR/QMAN to reduce the current data. This should be
called after the _EXPORT_CGS4DR_FORMAT_ primitive.

=head1 NOTES

=over 4

=item *

Creates a new object which is attached to a
QMAN monolith. This will fail if QMAN is not running.

=item *

CGS4DR must already be running on the same machine as ORAC-DR.

=back

=head1 AUTHOR

Tim Jenness <t.jenness@jach.hawaii.edu>
Frossie Economou <frossie@jach.hawaii.edu>

=head1 COPYRIGHT

Copyright (C) 1998-2001 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut


# Connect to the monolith
# This is made harder by the fact that CGS4DR uses PID-related
# ADAM names except when at UKIRT.
# QMAN is actually named  reverse($pid)_qman
# where $pid is the process ID of the parent CGS4DR_TCL task
# There are two ways to do this:
#   1. Search through the process table until I find CGS4DR_TCL
#       (Can do this with Proc::ProcessTable)
#   2. Search through ADAM_USER (or ~adam) for a pipe named
#      NNNN_qman_NNNN and extract the name from this. This is
#      okay except that we can not guarantee that ADAM_USER is
#      the same now as it was then (in fact $ENV{ADAM_USER} is
#      redefined when running ORAC-DR.

# At UKIRT the QMAN process is simply called QMAN so that tasks
# on other machines (including the VAX) know what it is called

# Cant store it in %Mon since %Mon is copied into this namespace
# each time this recipe is run. What we will do is cheat slightly
# and store the messaging object in a hidden package that only
# this primitive knows about. Not a problem since this is done
# on a need-to-know basis and only this primitive needs to know!

unless (defined $ORACDR::QMAN::TEMP::MsgObject) {

  # We are going to search the PID list to make sure that CGS4DR is
  # running. This does not check that qman is running...
  require Proc::ProcessTable;
  my $procs = new Proc::ProcessTable;

  my $cgs4dr_proc;

  foreach my $proc ( @{$procs->table} ) {

    if ($proc->cmndline =~ /\/cgs4dr_tcl/ ) {

      # only get here if we have a valid process name
      $cgs4dr_proc = $proc;

      # Abort from the loop
      last;

    }

  }

  # Check that we have a value
  unless (defined $cgs4dr_proc) {
    orac_err "CGS4-DR does not seem to be running. Unable to talk to QMAN.\n";
    my $ORAC_STATUS = ORAC__ERROR;
  }

  # print some diagnostics
  my $uid = $cgs4dr_proc->uid;
  my $pid = $cgs4dr_proc->pid;
  my $name = scalar getpwuid($uid);
  orac_print "Found cgs4dr_tcl process running as PID $pid under user $name (UID $uid)\n";


  # name of qman in ADAM messaging system and password access
  my ($qmanaccess, $qman);

  # If we are at UKIRT this is fairly straightforward
  my $domain = `domainname`;

  if ($domain =~ /ukirt/i) {

    # Have already checked that cgs4dr is running

    orac_print "Running pipeline at UKIRT.\n";

    $qman = "qman";

    $qmanaccess = '';

  } else {

    # Running elsewhere, need to know PID of CGS4DR

    # We are fine so calculate the name
    # Reverse the process id
    # Must be a more efficient way than reverse split???
    my $rpid = reverse split (//, $pid );
    $qman = $rpid . "_qman";

    # Password and lockword for the QMAN process are
    # These are derived from the pid as well
    my $lockword = $rpid . "lock";
    my $password = $rpid . "pass";

    $qmanaccess = "password=$password lockword=$lockword";

  }

  # These need to be stored in a global somewhere so use
  # So make one up [dont tell any one that this works since
  # it is very naughty]. There is no global hash for the use of the
  # pipeline -- this could well be a bug.
  $ORACDR::QMAN::TEMP::QmanAccess = $qmanaccess;

  # Create the object
  $ORACDR::QMAN::TEMP::MsgObject = new ORAC::Msg::ADAM::Task("$qman");

  # Try to talk to it
  unless ($ORACDR::QMAN::TEMP::MsgObject->contact) {
    orac_err "Could not talk to QMAN ($qman). Aborting\n";
    orac_err "Make sure you are running oracdr with the -nomsgtmp option\n";
    undef $ORACDR::QMAN::TEMP::MsgObject;
    my $ORAC_STATUS = ORAC__ERROR;
  }


}




# Get the observation number [see also _EXPORT_CGS4DR_FORMAT_
# since that uses exactly the same technique
my $num = $Frm->number;
my $ut  = $Frm->hdr('ORACUT');
my $out = substr($ut, 2) . '_' . $num;

# Send the command
$ORACDR::QMAN::TEMP::MsgObject->obeyw("WRITE","string='REDUCE o$out' qposition=oldest $ORACDR::QMAN::TEMP::QmanAccess");

# ..and the end
$ORACDR::QMAN::TEMP::MsgObject->obeyw("WRITE","string='END o$out' qposition=oldest $ORACDR::QMAN::TEMP::QmanAccess");
