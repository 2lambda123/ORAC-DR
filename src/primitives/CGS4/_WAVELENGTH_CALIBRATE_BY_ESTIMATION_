# _WAVELENGTH_CALIBRATION_BY_ESTIMATION_          -*- perl -*-
#
# wavelength calibrates using header information

=head1 NAME

WAVELENGTH_CALIBRATION_BY_ESTIMATION - wavelength calibrates observations by estimating 
wavelength scale

=head1 DESCRIPTION

Using information stored in the header, this primitive wavelength calibrates observations.

=head1 PARAMETERS

=over 4

none

=back

=head1 ORAC

=head2 Engines referenced

KAPPA, FIGARO

=head2 Tasks called

=over 4

=item ndfpack_mon

ndftrace, axlabel, axunits

=item figaro1

lxset

=back

=head2 Objects addressed

$Frm

=head2 REVISION

  $Id$

=head1 AUTHOR

b.cavanagh@jach.hawaii.edu

=cut

foreach my $i (1..$Frm->nfiles) {

  # generate the input and output filenames

  my ($in, $out) = $Frm->inout("_wce",$i);

  # get actual size

  my $ORAC_STATUS;

  $Mon{'ndfpack_mon'}->obeyw("ndftrace","ndf=$in quiet");
  
  ($ORAC_STATUS, my @lbound) = $Mon{'ndfpack_mon'}->get('ndftrace','lbound');
  ($ORAC_STATUS, my @ubound) = $Mon{'ndfpack_mon'}->get('ndftrace','ubound');

  my $actxmin = $lbound[0];
  my $actymin = $lbound[1];
  my $actxmax = $ubound[0];
  my $actymax = $ubound[1];

  # get header information

  my $xdim = $Frm->hdr("DCOLUMNS");
  my $ydim = $Frm->hdr("DCOLS");

  my $factor = $xdim / ($actxmax - $actxmin + 1);

  my $dispersion = $Frm->hdr("GDISP") * $factor;
  my $refwave = $Frm->hdr("GLAMBDA");

  my $firstwave = $refwave - (($actxmax - $actxmin + 1) / 2) * $dispersion;
  my $lastwave = $refwave + (($actxmax - $actxmin + 1) / 2) * $dispersion;

  # set the wavelength scale

  $Mon{'figaro1'}->obeyw("lxset","image=$in wstart=$firstwave wend=$lastwave output=$out log=false");

  # set AXIS label

  $Mon{'ndfpack_mon'}->obeyw("axlabel","ndf=$out label=\'Estimated wavelength\' dim=1");

  # set AXIS units

  $Mon{'ndfpack_mon'}->obeyw("axunits","ndf=$out units=microns dim=1");

  # Update Frame object

  $Frm->file($i,$out);

  # print congratulatory message

  orac_print("Orac says: $in to $out: wavelength calibrated by estimation\n");

}

