# -*-cperl-*-

=head1 NAME

_STACK_JCMT_FRAMES_ - create a 3-d stack of images from JCMT instruments

=head1 SYNOPSIS

_STACK_JCMT_IMAGES_

=head1 DESCRIPTION

This primitive sorts the input files using a combination of
pre-defined and user-specified FITS headers and creates a 3-d cube for
each set of criteria using those images. The third axis is time.

By default SCUBA-2 data will be sorted by the shutter value.

=head1 ARGUMENTS

This primitive has no arguments.

=head1 EXTERNAL TASKS

The following external tasks are used:

=over 2

=item KAPPA

CREFRAME, NDFCOPY, WCSMOSAIC

=item SMURF

STACKFRAMES

=ITEM ORAC-DR PRIMITIVES

_DELETE_TEMP_FILES_

=back

=head1 OUTPUT DATA

None.

=head1 OUTPUT FILES

A file with the suffix C<_stack> is created for each group of files
that matches the given sorting criteria.

=head1 NOTES

If only a single file matches the sorting criteria then no stack is
created.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2012 University of British Columbia. All Rights
Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# Only stack the images once the final Frame is reached
if ($Grp->lastmember($Frm)) {

  # Define the FITS header keys for grouping files to stack
  my @keys;
  if ($Frm->hdr("INSTRUME") =~ /scuba-2/i) {
    # SCUBA2 frames must be sorted by shutter
    push(@keys, "SHUTTER");
  }
  # Add any user-defined keys - note no checking is made that these are valid!
  if (defined $Frm->uhdr("RECPAR_STACK_KEYS")) {
    # Must check if we have an array reference
    if (ref($Frm->uhdr("RECPAR_STACK_KEYS"))) {
      push(@keys, @{$Frm->uhdr("RECPAR_STACK_KEYS")});
    } else {
      push(@keys, $Frm->uhdr("RECPAR_STACK_KEYS"));
    }
  }

  # Sort the files by the stacking key
  orac_print "Sorting files to stack:";
  my %stacks;
  foreach my $frm ($Grp->members) {
    my $key = join("_", map { $frm->hdr("$_") } @keys);
    my $file = $frm->file;
    if ($stacks{$key}) {
      my @files = @{ $stacks{$key} };
      push(@files, $file);
      $stacks{$key} = \@files;
    } else {
      $stacks{$key} = [$file];
    }
  }
  orac_print " done\n";
  $Frm->tagset("stack_jcmt_frames");

  # Stack each group of files
  my $suffix = ($Frm->uhdr("RECPAR_NDF_EXTEN")) ?
    "_".$Frm->uhdr("RECPAR_NDF_EXTEN") ."_stack" : "_stack";

  foreach my $stack ( sort keys %stacks ) {
    my @stackfiles = @{$stacks{$stack}};
    # Only stack if there are multiple files
    if (@stackfiles > 1) {

      # Add path to the given NDF extension if necessary
      $Frm->files(@stackfiles);

      my $stackfile = $Frm->inout($suffix);
      orac_print "Creating $stackfile from ".($Frm->nfiles)." files\n";

      orac_print "Determining output bounds for this stack...";
      my $filelist = write_file_list($Frm->files);
      my $args = "in=^$filelist out=! lbnd=! ubnd=! ref=!";
      $Mon{kappa_mon}->obeyw("wcsmosaic","$args");
      my ($ORAC_STATUS, @lbound) = $Mon{kappa_mon}->get("wcsmosaic","lbound");
      ($ORAC_STATUS, my @ubound) = $Mon{kappa_mon}->get("wcsmosaic","ubound");

      my $lbound = join(",",@lbound[0..1]);
      my $ubound = join(",",@ubound[0..1]);

      my $template = ORAC::TempFile->new();
      my $title = $Frm->hdr('OBJECT');
      my $imgargs = "lbound=\[$lbound\] ubound=\[$ubound\] out=$template mode=bl title='Template'";
      $Mon{kappa_mon}->obeyw("creframe","$imgargs");

      orac_print " Resizing input files to match...\n";
      my @copy;
      foreach my $i (1 .. $Frm->nfiles) {
	my ($in, $copy) = $Frm->inout("_grid", $i);
	my $tmp = ORAC::TempFile->new();
	$Mon{ndfpack_mon}->obeyw("ndfcopy","in=$in out=$tmp trimwcs trim");
	$Mon{ndfpack_mon}->obeyw("ndfcopy","in=$tmp out=$copy like=$template");
	push(@copy, $copy);
      }
      $Frm->files(@copy);

      my $inlist = write_file_list($Frm->files);
      my $tmpstack = ORAC::TempFile->new();
      $args = "in=^$inlist out=$tmpstack sort sortby=MJD";
      $Mon{smurf_mon}->obeyw("stackframes", "$args");
      # Trim excess bad pixels
      $Mon{ndfpack_mon}->obeyw("ndfcopy","in=$tmpstack out=$stackfile trim trimbad");
      orac_print "Stack $stackfile created\n";
    } else {
      orac_warn "Output stack has only 1 file - ignoring\n";
    }
  }
  $Frm->tagretrieve("stack_jcmt_frames");

  # Delete the intermediate files
  _DELETE_TEMP_FILES_ KEEP=_stack

} else {
  orac_print "Waiting until last Frame before processing...\n";
  # Make a copy of the input file here to simplify processing and
  # tidy-up
  my ($in, $copy) = $Frm->inout("_copy");
  if (defined $Frm->uhdr("RECPAR_NDF_EXTEN")) {
    $in .= ".more.smurf." .$Frm->uhdr("RECPAR_NDF_EXTEN");
  }
  $Mon{ndfpack_mon}->obeyw("ndfcopy","in=$in out=$copy");
  $Frm->files($copy);
}
