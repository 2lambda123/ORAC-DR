# -*-cperl-*-

=head1 NAME

_UNCALIBRATE_SCUBA2_DATA_ - undo default SCUBA-2 calibration

=head1 SYNOPSIS

_UNCALIBRATE_SCUBA2_DATA_

=head1 DESCRIPTION

Undo the default calibration of SCUBA-2 data. This primitive checks
the input units and divides by the appropriate default FCF (derived
from the FITS header if possible) to return the data to units of pW.

=head1 ARGUMENTS

This primitive has no arguments.

=head1 EXTERNAL TASKS

The following external tasks are used:

=over 4

=item KAPPA

CDIV, NDFTRACE, SETUNITS

=back

=head1 OUTPUT DATA

The Frame is updated to contain the uncalibrated data files and tagged
with C<uncal>.

=head1 OUTPUT FILES

Creates output files with the appended suffix C<_uncal>.

=head1 NOTES

Ideally the history should be searched and the exact FCF used, but for
now assume that the default calibration was applied.

=head1 AUTHOR

Andy Gibb E<lt>agg@astro.ubc.caE<gt>

=head1 COPYRIGHT

Copyright (C) 2010,2013-2014 University of British Columbia.
All Rights Reserved.

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or (at
your option) any later version.

This program is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307,
USA.

=cut

# Only proceed when CALSOURCE is set and the source is known calibrator
return ORAC__OK if ($_PRIM_ARGS_->{CALSOURCE} &&
		    !$Cal->iscalsource($Frm->hdr("OBJECT")));

my $uncalunits = "pW";
my $uncal = $Frm->inout("_uncal");

orac_print "\nCalling _UNCALIBRATE_SCUBA2_DATA_: undo calibration of given data\n", "yellow";

# Obtain the current units for the input file to determine the FCF
# calibration type
my $in = $Frm->file;
$Mon{ndfpack_mon}->obeyw("ndftrace","ndf=$in");
my ($ORAC_STATUS, $units) = $Mon{ndfpack_mon}->get("ndftrace","units");
if ($units eq $uncalunits) {
  # Print a message and exit
  orac_print "File $in already contains uncalibrated data\n";
  $uncal = $in;
} else {
  # Assume units of Jy if not mJy
  my $mult = ($units =~ /^mJy/) ? 1000.0 : 1.0;

  # Undo the calibration of SCUBA-2 data files - use the FCF in the
  # header if present, otherwise assume default calibration and that
  # data are in mJy/beam
  my $fcf_units = ($units =~ /beam/) ? "BEAM" : "ARCSEC";
  my $fcf = ($Frm->hdr("FCF")) ? $Frm->hdr("FCF") :
    $mult * $Cal->gain($Frm->uhdr("ORAC_FILTER"), $fcf_units);

  # If the FCF is not defined, set ORACUT from DATE-OBS and try again
  unless ($fcf) {
    orac_err "Unable to determine FCF - input file is probably a coadd or mosaic\n";
    orac_err "Data should be calibrated before coadding/mosaicking\n";
    _SET_ORACUT_FROM_DATEOBS_
    $fcf = $Cal->gain($Frm->uhdr("ORAC_FILTER"), $units);
  }

  my $args = "in=$in out=$uncal scalar=$fcf";
  $Mon{kappa_mon}->obeyw("cdiv","$args");

  my $calibration = ($Frm->hdr("FCF")) ? "calibration" : "default calibration";
  orac_print "Undoing $calibration of $fcf $units/pW - output units are pW\n";

  # Change units to pW
  $args = "ndf=$uncal units=$uncalunits";
  $Mon{ndfpack_mon}->obeyw("setunits","$args");
  $Frm->uhdr("DATA_UNITS", $uncalunits);
  # Unset the NOCALIB flag so data can be calibrated as necessary
  $Frm->uhdr("NOCALIB",0);

  # Remove the FCF keyword in the FITS header
  my $fitshdr = new Astro::FITS::Header::NDF( File => $uncal );
  if (defined $fitshdr->value("FCF")) {
    $fitshdr->removebyname("FCF");
    $fitshdr->writehdr( File => $uncal );
  }
}

# Store in the current Frame object
$Frm->files($uncal);
$Frm->tagset("uncal");
