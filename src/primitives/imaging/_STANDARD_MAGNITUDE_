#+
# Name:
#    _STANDARD_MAGNITUDE_
#
# Purpose:
#    Obtains the catalogue magnitude of a standard.
#
# Language:
#    Perl5
#
# Description:
#    This primitive reads the faint-standard catalogue or its
#    predecessor.  A case- and space-insensitive comparison of the
#    supplied object name with the entries in the table provides a
#    catalogue magnitude in I, Z, J, H, K, L, or M for a standard star.
#    The catalogue magnitude is returned through an argument.
#
# Arguments:
#    CATCOL = INTEGER (Given)
#       The index of the waveband within the catalogue of standards
#       $ORACDR_CAL/fs_izjhklm.dat most appropriate for the current
#       frame's filter.  Counting starts at zero for the leftmost waveband
#       tabulated.  It comes from _GET_FILTER_PARAMETERS_.
#    CATMAG = REAL (Returned)
#       The catalogue magnitude for the object and current frame's filter.
#       This is set to the NULLMAG if no magnitude is available.
#    CATNAME = CHARACTER (Returned)
#       The object name in the catalogue.  This has superfluous
#       characters removed from the supplied NAME.
#    NAME = CHARACTER (Given).
#       The object name.  This can be UKIRT Faint Standard catalogue
#       number, or the alternative name.
#    NULLMAG = REAL (Given)
#       A null magnitude when the object has no magnitude for the filter.
#       [99.999]
#
# Notes:
#    -  This primitive is suitable for UKIRT near-infra-red imagers.
#    -  Processing should only occur when it is time to perform
#    photometry, i.e. when the steering header DO_APHOT is true,
#    however, this primitive does not make that check.  That is to
#    permit photometry of all the contributing frames of a mosaic.
#    -  An error occurs when the catalogue index is negative or more
#    than 6, or if no object name is supplied.
#    -  The standard-star catalogue used is $ORAC_DATA_CAL/fs_izjhklm.dat.
#    However, if this file is unavailable, the script accesses the old
#    $ORAC_DATA_CAL/fs2000.dat, but only for JHK data.  The indices are
#    reduced by two since there is no preceding I and Z columns.  An error
#    results when both catalogues cannot be opened.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC, Starlink)
#
# Copyright:
#    Copyright (C) 1998-2003 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# For OS-independent filename creation.
    use File::Spec;

# Assign some defaults.
    my $catname = " ";
    my $catmag = 99.999;

# Arguments
# =========

# Obtain the suffix value for the file name in the log.  Used
# for NOD positive and negative images, but normally set to null
    my $objname = ( defined( $_STANDARD_MAGNITUDE_{NAME} ) ?
                     $_STANDARD_MAGNITUDE_{NAME} : "" );
    if ( $objname eq "" ) {
       orac_err "Programming error: _STANDARD_MAGNITUDE_ not " .
                "supplied an object name.\n";
    }

# Obtain the column name for the magnitude.
    my $catcol = ( defined( $_STANDARD_MAGNITUDE_{CATCOL} ) ?
                   $_STANDARD_MAGNITUDE_{CATCOL} : -1 );
    if ( $catcol < -1 || $catcol > 6 ) {
       orac_err "Programming error: _STANDARD_MAGNITUDE_ not " .
                "supplied a valid (0--6) catalogue index $catcol.\n";
    }

# Obtain the column name for the magnitude.
    my $nullmag = ( defined( $_STANDARD_MAGNITUDE_{NULLMAG} ) ? 
                    $_STANDARD_MAGNITUDE_{NULLMAG} : $catmag );

# Extract the system magnitude of faint standards
# ===============================================

# Remove spaces from the name, and convert to uppercase for later
# comparison.
    ( my $starname = uc( $objname ) ) =~ s/ //g;

# Faint standards have name FSn, where n is the number.  Only JHK
# magnitudes are tabulated.
    if ( $catcol > -1 ) {
       my $standards_file;
       my $jhk = 1;

# Define the character to indicate a null value in the standrds' table.
       my $nullcol = "&";

# Open the faint-standards file and store its records in an array.
       my $catpath = $ENV{ "ORAC_DATA_CAL" };
       $standards_file = File::Spec->catfile( $catpath, "fs_izjhklm.dat" );

# For IZLM the raw file must be fs_izjhklm.
       my $fh_standards;
       if ( -e $standards_file ) {
          open( $fh_standards, "<$standards_file" ) || 
            orac_warn "Standards file $standards_file could not be opened.  Error: $!.\n";

# Record the fact that we are reading the new-format IZJHKLM file, not
# the old file of just JHK data.
          $jhk = 0;

# Try the old JHK file incase the latest standards compilation is not
# available.
       } elsif ( $filter =~ /^[JHK]/ ) {
          $standards_file = File::Spec->catfile( $catpath, "fs2000.dat" );
          if ( !-e $standards_file ) {
             $standards_file = "fs2000.dat";
          }
          open( $fh_standards, "<$standards_file" ) || 
            orac_warn "JHK Standards file $standards_file could not be opened.  Error: $!\n";
       }
       my @stds = <$fh_standards>;

# Remove trailing blanks and newline.
       for ( @stds ) {
          chomp;
          s/\s+$//;
       }

# Search through the table finding a line containing the object name.  The
# file currently consists of some column headings, then a relational table.
# Columns in the table include the star name, and the various magnitudes
# or (J-H) and (H-K) colours in the old-format.

# Look at each line.  Extract the name, stripping the blanks.  Proceed
# until an uppercase match is found.  Allow for the observer to append
# garbage at the end of the object name, so don't perform an exact match.
       my $recno = 1;
       my $name = " ";
       my $othername = " ";
       until ( $starname =~ /^$name$/ || $recno > $#stds ||
               $starname =~ /^$othername$/ ) {
          $recno++;

# New file does not have FS prefix in the first column.  Convert to
# uppercase and remove blanks for conmparisons.
          if ( defined( $stds[ $recno ] ) ) {
             if ( $jhk ) {
                $name = uc( substr( $stds[ $recno ], 0, 6 ) );
                $othername = uc( substr( $stds[ $recno ], 8, 9 ) );
             } else {
                $name = "FS" . uc( substr( $stds[ $recno ], 0, 3 ) );
                $othername = uc( substr( $stds[ $recno ], 5,11 ) );
             }
             $name =~ s/ //g;
             $othername =~ s/ //g;
          }
       }

# Check that a match has been found.  Use a special value to indicate
# a null magnitude.
       if ( $recno > $#stds ) {
          if ( -e $standards_file ) {
             orac_warn "Standard $objname has no tabulated $filter magnitude in $standards_file\n";
          } else {
             orac_warn "The standards file $standards_file does not exist\n";
          }
          $catmag = $nullmag;

       } else {

# Remember the actual object name.
          $catname = $othername;
          $catname = $name if ( $name ne "FS&" );
             
# Extract the columns.
          my @fields = split( / +/, $stds[ $recno ] );
          if ( $jhk ) {

# Extract and evaluate the magnitudes from the colours.  This could be done
# more succinctly, but use more friendly intermediate variables.
             my $Kmag = $fields[ 9 ];
             my $Hmag = $Kmag + $fields[ 15 ];
             my $Jmag = $Hmag + $fields[ 12 ];
             my @mags = ( $Jmag, $Hmag, $Kmag );

# Obtain the desired magnitude.  Subtract the two because I and Z are
# omitted.
             $catmag = $mags[ $catcol - 2 ];

# Extract the IZJHKLM magnitudes.
          } else {
             my $Imag = $fields[ 8 ];
             my $Zmag = $fields[ 11 ];
             my $Jmag = $fields[ 14 ];
             my $Hmag = $fields[ 17 ];
             my $Kmag = $fields[ 20 ];
             my $Lmag = $fields[ 23 ];
             my $Mmag = $fields[ 26 ];

             my @mags = ( $Imag, $Zmag, $Jmag, $Hmag, $Kmag, $Lmag, $Mmag );

# Obtain the desired magnitude.
             $catmag = $mags[ $catcol ];
          }
       }

# Close the stamdards data file.
       close( $fh_standards );

# Set a null magnitude if there is no value in the table for the
# chosen star and waveband.
       $catmag = $nullmag  if $catmag eq $nullcol;

# Since we could not locate or open the standards file, the magnitude
# is null.
    } else {
       $catmag = $nullmag;
    }

# Return the catalogue magnitude.
    $_STANDARD_MAGNITUDE_{CATMAG} = $catmag;
    $_STANDARD_MAGNITUDE_{CATNAME} = $catname;


# Podule
# ======

=head1 NAME

_STANDARD_MAGNITUDE_ -- Obtains the catalogue magnitude of a standard.

=head1 DESCRIPTION

This primitive reads the faint-standard catalogue or its
predecessor.  A case- and space-insensitive comparison of the
supplied object name with the entries in the table provides a
catalogue magnitude in I<I>, I<Z>, I<J>, I<H>, I<K>, I<L>, or I<M> for a standard star.

=head1 ARGUMENTS

=over 4

=item CATCOL = INTEGER (Given)

The index of the waveband within the catalogue of standards
F<$ORACDR_CAL/fs_izjhklm.dat> most appropriate for the current
frame's filter.  Counting starts at zero for the leftmost waveband
tabulated.  It comes from _GET_FILTER_PARAMETERS_.

=item CATMAG = REAL (Returned)

The catalogue magnitude for the object and current frame's filter.

=item NAME = CHARACTER (Given).

The object name.  This can be UKIRT Faint Standard catalogue
number, or the alternative name.

=item NULLMAG = REAL (Given)

A null magnitude when the object has no magnitude for the filter.
[99.999]

=back

=head1 NOTES

=over 4

=item *

This primitive is suitable for UKIRT near-infra-red imagers.

=item *

Processing only occurs when it is time to perform photometry,
i.e. when the steering header DO_APHOT is true.

=item *

An error occurs when the catalogue index is negative or more
than 6, or if no object name is supplied.

=item *

The standard-star catalogue used is F<$ORAC_DATA_CAL/fs_izjhklm.dat>.
However, if this file is unavailable, the script accesses the old
F<$ORAC_DATA_CAL/fs2000.dat>, but only for I<JHK> data.  The indices are
reduced by two since there is no preceding I<I> and I<Z> columns.  An error
results when both catalogues cannot be opened.

=head1 AUTHORS

MJC: Malcolm J. Currie (JAC, Starlink)

=head1 COPYRIGHT

Copyright (C) 1998-2003 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut
