#+
# Name:
#    _DEFINE_QUADRANT_MASKS_
#
# Purpose:
#    Create ARD files used to mask each of the quadrants of a frame.
#
# Language:
#    Perl5

# Description:
#    This obtains the bounds of a frame and derives the pixel limits
#    for each quadrant of the frame.  Each set of pixel limits is
#    then written to an ASCII Region Definition file.
#
# Notes:
#    -  This primitive is suitable for UFTI, IRCAM, and Michelle
#    in imaging mode.
#    -  It only operates on object frames.
#    -  For an axis with an odd number of pixels, the lower `half'
#    includes the extra pixel.
#
# Output Files:
#    An ARD file for each quadrant called ard_quad_br.dat, ard_quad_tr.dat,
#    ard_quad_bl.dat, ard_quad_tl.dat, where b stand for bottom, t for top
#    l for left, and r for right.  Any existing files of those names are
#    replaced.
#
# Tasks:
#    KAPPA: NDFTRACE.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC)
#
# Copyright:
#    Copyright (C) 1998-2001 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# Obtain the bounds of an object frame.
# =====================================

# Test whether or not this is an object frame.
    if ( $Frm->uhdr( "ORAC_OBSERVATION_TYPE" ) eq "OBJECT" ) {

# First run a task to obtain details of the frame.
       my $param1 = "ndf=".$Frm->file;
       my $ORAC_STATUS;
       $Mon{ "ndfpack_mon" }->obeyw( "ndftrace", "$param1" );

# Extract the bounds of the frame.
       ( $ORAC_STATUS, my @lbnd ) = $Mon{ "ndfpack_mon" }->get( "ndftrace", "lbound" );
       ( $ORAC_STATUS, my @ubnd ) = $Mon{ "ndfpack_mon" }->get( "ndftrace", "ubound" );

# Find the mid points.
       my $xmid = nint( ( $lbnd[0] + $ubnd[0] ) / 2 );
       my $ymid = nint( ( $lbnd[1] + $ubnd[1] ) / 2 );
       my $xmidp1 = $xmid + 1;
       my $ymidp1 = $ymid + 1;

# Form the ARD files.
# ===================

# Remove any existing quadrant files.
       unlink( "ard_quad_br.dat", "ard_quad_tr.dat" );
       unlink( "ard_quad_bl.dat", "ard_quad_tl.dat" );

# Create four ARD files each of which will mask a different quadrant.
# Note that the co-ordinates supplied are pixel indices, so apply a
# half-pixel shift to convert them to pixel co-ordinates.

# Bottom-right
       open( my $fh_ardfile, ">ard_quad_br.dat" ) ||
         orac_throw "Unable to open ard_quad_br.dat to create an ARD mask of the bottom-right quadrant.  Error: $!.\n";
       print $fh_ardfile "OFFSET( -0.5, -0.5 )\n";
       print $fh_ardfile "RECT($xmidp1,$lbnd[1],$ubnd[0],$ymid)\n";
       close( $fh_ardfile );

# Top-right
       open( $fh_ardfile, ">ard_quad_tr.dat" ) ||
         orac_throw "Unable to open ard_quad_tr.dat to create an ARD mask of the top-right quadrant.  Error: $!.\n";
       print $fh_ardfile "OFFSET( -0.5, -0.5 )\n";
       print $fh_ardfile "RECT($xmidp1,$ymidp1,$ubnd[0],$ubnd[1])\n";
       close( $fh_ardfile );

# Top-left
       open( $fh_ardfile, ">ard_quad_tl.dat" ) ||
         orac_throw "Unable to open ard_quad_tl.dat to create an ARD mask of the top-left quadrant.  Error: $!.\n";
       print $fh_ardfile "OFFSET( -0.5, -0.5 )\n";
       print $fh_ardfile "RECT($lbnd[0],$ymidp1,$xmid,$ubnd[1])\n";
       close( $fh_ardfile );

# Bottom-left
       open( $fh_ardfile, ">ard_quad_bl.dat" ) ||
         orac_throw "Unable to open ard_quad_bl.dat to create an ARD mask of the bottom-left quadrant.  Error: $!.\n";
       print $fh_ardfile "OFFSET( -0.5, -0.5 )\n";
       print $fh_ardfile "RECT($lbnd[0],$lbnd[1],$xmid,$ymid)\n";
       close( $fh_ardfile );
    }

# Podule
# ======

=head1 NAME

_DEFINE_QUADRANT_MASKS_ -- Create ARD files used to mask each of the quadrants of a frame.

=head1 DESCRIPTION

This obtains the bounds of a frame and derives the pixel limits
for each quadrant of the frame.  Each set of pixel limits is
then written to an ASCII Region Definition file.

=head1 NOTES

=over 4

=item *

This primitive is suitable for UFTI, IRCAM, and Michelle in imaging mode.

=item *

It only operates on object frames.

=item *

For an axis with an odd number of pixels, the lower `half' includes
the extra pixel.

=back

=head1 OUTPUT FILES

An ARD file for each quadrant called F<ard_quad_br.dat>, F<ard_quad_tr.dat>,
F<ard_quad_bl.dat>, F<ard_quad_tl.dat>, where b stand for bottom, t for top
l for left, and r for right.  Any existing files of those names are
replaced.

=head1 TASKS

KAPPA: NDFTRACE.

=head1 AUTHORS

MJC: Malcolm J. Currie (JAC)

=head1 COPYRIGHT

Copyright (C) 1998-2001 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut
