#+
# Name:
#    _LCOGT_STANDARD_MAGNITUDE_
#
# Purpose:
#    Obtains the catalogue magnitude of a LCOGT standard.
#
# Language:
#    Perl5
#
# Description:
#    This primitive reads the faint-standard catalogue or its
#    predecessor.  A case- and space-insensitive comparison of the
#    supplied object name with the entries in the table provides a
#    catalogue magnitude in U, B, V, R, or I for a
#    standard star.  The catalogue magnitude is returned through an
#    argument.
#
# Arguments:
#    CATMAG = REAL (Returned)
#       The catalogue magnitude for the object and current frame's filter.
#       This is set to the NULLMAG if no magnitude is available.
#    CATNAME = CHARACTER (Returned)
#       The object name in the catalogue.  This has superfluous
#       characters removed from the supplied NAME.
#    NAME = CHARACTER (Given).
#       The object name.  This can be Landolt (1992) Standard catalogue
#       number, or the alternative name.
#    NULLMAG = REAL (Given)
#       A null magnitude when the object has no magnitude for the filter.
#       [99.999]
#    WAVEBAND = CHARACTER (Given)
#       The name of the waveband.  It may be U, B, V, R, or I.
#       It comes from _GET_FILTER_PARAMETERS_.
#
# Notes:
#    -  This primitive is suitable for LCOGT optical imagers.
#    -  Processing should only occur when it is time to perform
#    photometry, i.e. when the steering header DO_APHOT is true,
#    however, this primitive does not make that check.  That is to
#    permit photometry of all the contributing frames of a mosaic.
#    -  An error occurs when the filter is not one of U, B, V, R, or I.
#    -  The standard-star catalogue used is $ORAC_DATA_CAL/landolt_ubvri.dat.
#    An error results when the catalogue cannot be opened.
#
# Authors:
#    TAL: Tim Lister (LCOGT)
#
# Copyright:
#    Copyright (C) 2012 Las Cumbres Observatory Global Telescope Inc.  All
#    Rights Reserved.
#
#-

# For OS-independent filename creation.
    use File::Spec;

# Assign some defaults.
    my $catname = " ";
    my $defnullmag = 99.999;

# Arguments
# =========

# Obtain the suffix value for the file name in the log.  Used
# for NOD positive and negative images, but normally set to null.
    my $objname = ( defined( $_LCOGT_STANDARD_MAGNITUDE_{NAME} ) ?
                     $_LCOGT_STANDARD_MAGNITUDE_{NAME} : "" );
    if ( $objname eq "" ) {
       orac_err "Programming error: _LCOGT_STANDARD_MAGNITUDE_ not " .
                "supplied an object name.\n";
    }

# Obtain the waveband for the magnitude.
    my $waveband = ( defined( $_LCOGT_STANDARD_MAGNITUDE_{WAVEBAND} ) ?
                   $_LCOGT_STANDARD_MAGNITUDE_{WAVEBAND} : "V" );
    if ( $waveband !~ /^[UBVRI]/ ) {
      orac_err "Programming error: _LCOGT_STANDARD_MAGNITUDE_ " .
               "supplied $waveband, which is not a valid waveband. " .
               "Allowed options are U, B, V, R, I.\n";

      $_LCOGT_STANDARD_MAGNITUDE_{CATMAG} = $defnullmag;
      $_LCOGT_STANDARD_MAGNITUDE_{CATNAME} = $catname;

    } else {

# Obtain the column name for the magnitude.
      my $nullmag = ( defined( $_LCOGT_STANDARD_MAGNITUDE_{NULLMAG} ) ?
                      $_LCOGT_STANDARD_MAGNITUDE_{NULLMAG} : $defnullmag );
      my $catmag = $nullmag;

# Extract the standard's name(s).
# ===============================

# Remove spaces from the name, and convert to uppercase for later
# comparison.
      ( my $starname = uc( $objname ) ) =~ s/ //g;

      print "DBG: starname=$starname, WB=$waveband\n";
# More than one naming is used and some observatories use more than
# one catalogue name in the object name.  Landolt (1992) Standards have
# name SAn, where n is the number.

# Read the catalogue.
# ===================

      my $standards_file;

# Define the character to indicate a null value in the standards' table.
      my $nullcol = "&";

# Open the Landolt-standards file and store its records in an array.
      $standards_file = $Cal->find_file("landolt_ubvri.dat");

      my $fh_standards;
      if ( -e $standards_file ) {
        open( $fh_standards, "<$standards_file" ) ||
          orac_warn "Standards file $standards_file could not be opened.  Error: $!.\n";
      }

      my @stds = <$fh_standards>;

# Remove trailing blanks and newline.
      for ( @stds ) {
        chomp;
        s/\s+$//;
      }

# Look for the standard in the catalogue.
# =======================================

# Search through the table finding a line containing the object name.  The
# file currently consists of some column headings, then a relational table.
# Columns in the table include the star name, and the various magnitudes

# Look at each line.  Extract the name, stripping the blanks.  Proceed
# until an uppercase match is found.  Allow for the observer to append
# garbage at the end of the object name, so don't perform an exact match.
      my $recno = 1;
      my $name = " ";
      my $othername = " ";
      until ( $name eq $starname || $recno > $#stds || $othername eq $starname) {
        $recno++;

# New file does not have FS prefix in the first column.  Convert to
# uppercase and remove blanks for conmparisons.
        if ( defined( $stds[ $recno ] ) ) {
          $name = "SA" . uc( substr( $stds[ $recno ], 0, 12 ) );
          $othername = uc( substr( $stds[ $recno ], 0,12 ) );
          $name =~ s/ //g;
          $othername =~ s/ //g;
        }
      }

# Extract the system magnitude of the standard.
# =============================================

# Check that a match has been found.  Use a special value to indicate
# a null magnitude.
      if ( $recno > $#stds ) {
        if ( -e $standards_file ) {
          orac_warn "Standard $objname has no tabulated $waveband magnitude in $standards_file\n";
        } else {
          orac_warn "The standards file $standards_file does not exist\n";
        }
        $catmag = $nullmag;

      } else {

# Remember the actual object name.
        $catname = $othername;
        $catname = $name if ( $name ne "SA&" );

# Extract the columns.
        my @fields = split( / +/, $stds[ $recno ] );
# Extract the UBVRI magnitudes.

# Set up hash of columns versus filter in the current file.
        my %wavebandindex = ( U => 8,
                                B => 10,
                                V => 12,
                                R => 14,
                                I => 16);
          $catmag = $fields[ $wavebandindex{ $waveband } ];
      }

# Close the stamdards data file.
      close( $fh_standards );

# Set a null magnitude if there is no value in the table for the
# chosen star and waveband.
      $catmag = $nullmag  if $catmag eq $nullcol;
      print "DBG: Cat mag=$catmag\n";

# Return the catalogue magnitude.
      $_LCOGT_STANDARD_MAGNITUDE_{CATMAG} = $catmag;
      $_LCOGT_STANDARD_MAGNITUDE_{CATNAME} = $catname;

    }

# Podule
# ======

=head1 NAME

_LCOGT_STANDARD_MAGNITUDE_ -- Obtains the catalogue magnitude of a LCOGT standard.

=head1 DESCRIPTION

This primitive reads the faint-standard catalogue or its
predecessor.  A case- and space-insensitive comparison of the
supplied object name with the entries in the table provides a
catalogue magnitude in I<U>, I<B>, I<V>, I<R>, or I<I> for a
standard star.

=head1 ARGUMENTS

=over 4

=item CATMAG = REAL (Returned)

The catalogue magnitude for the object and current frame's filter.

=item NAME = CHARACTER (Given).

The object name.  This can be Landolt (1992) Standard catalogue
number, or the alternative name.

=item NULLMAG = REAL (Given)

A null magnitude when the object has no magnitude for the filter.
[99.999]

=item WAVEBAND = CHARACTER (Given)

The name of the waveband.  It may be U, B, V, R, or I.
It comes from L<_GET_FILTER_PARAMETERS_|_GET_FILTER_PARAMETERS_>.

=back

=head1 NOTES

=over 4

=item *

This primitive is suitable for LCOGT optical imagers.

=item *

Processing only occurs when it is time to perform photometry,
i.e. when the steering header DO_APHOT is true.

=item *

An error occurs when the filter is not one of U, B, V, R, or I.

=item *

The standard-star catalogue used is F<$ORAC_DATA_CAL/landolt_ubvri.dat>.
An error results when the catalogue cannot be opened.

=back

=head1 AUTHORS

Tim Lister (LCOGT)

=head1 COPYRIGHT

Copyright (C) 2012 Las Cumbres Observatory Global Telescope Inc.
All Rights Reserved.

=cut
