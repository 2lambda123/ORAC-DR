# -*-perl-*-

use Astro::FITS::CFITSIO qw(:longnames :constants);
use Starlink::HDSPACK qw/ create_hdsobj copy_hdsobj /;
use strict "vars";

# Create input and output names

my $tmpCvt = new ORAC::Convert;

# Create a temporary HDS file in $ORAC_DATA_OUT.
my $tmp = new ORAC::TempFile;
my $tmpfile = $tmp->file;

my $ORAC_STATUS = ( create_hdsobj( $tmpfile, 'NDF', [0]) ? ORAC__OK : ORAC__ERROR );

my $header_copied = 0;

my $exptime = $Frm->uhdr( "ORAC_EXPOSURE_TIME" );

my $out;

# Keywords for extension header unit

my @ehukeys = ("INHERIT","DETECTOR","DETECTID","DROWS","DCOLUMNS",
               "RDOUT_X1","RDOUT_X2","RDOUT_Y1","RDOUT_Y2","PIXLSIZE","GAIN",
               "CAMNUM","HDTFILE2","DET_TEMP","CNFINDEX","PCSYSID","SDSUID",
               "READOUT","CAPPLICN","CAMROLE","CAMPOWER","RUNID","CTYPE1",
               "CTYPE2","CRPIX1","CRPIX2","CRVAL1","CRVAL2","CRUNIT1",
               "CRUNIT2","CD1_1","CD1_2","CD2_1","CD2_2","PV2_1","PV2_2",
               "PV2_3");

my %ehulut = ();
my $kk;
foreach $kk (@ehukeys) {
    $ehulut{$kk} = 1;
}

# Keywords to delete

my @delkeys = ("HDUCLAS1","HDUCLAS2","HDSNAME","HDSTYPE");
my %dellut = ();
foreach $kk (@delkeys) {
    $dellut{$kk} = 1;
}


foreach my $i ( 1 .. $Frm->nfiles ) {
  ( my $in, $out ) = $Frm->inout( "_adu", $i );

  if( ! $header_copied ) {

    $ORAC_STATUS = ( create_hdsobj( "$tmpfile.HEADER", "NDF" ) ? ORAC__OK : ORAC__ERROR );

    my $file = $Frm->raw;
    $file =~ s/\.sdf$//;

    $ORAC_STATUS = ( copy_hdsobj( "$file.HEADER", "$tmpfile.HEADER" ) ? ORAC__OK : ORAC__ERROR );

    $header_copied = 1;
  }

  $ORAC_STATUS = ( create_hdsobj( "$tmpfile.I${i}", "NDF", [0] ) ? ORAC__OK : ORAC__ERROR );

  if( $Frm->uhdr( "ORAC_DATA_UNITS" ) eq "counts/sec" ) {

    orac_print "Adjusting for exposure time.\n";
    $Mon{'kappa_mon'}->obeyw( "cmult", "in=$in scalar=$exptime out=$out" );

    $ORAC_STATUS = ( copy_hdsobj( "$out", "$tmpfile.I${i}" ) ? ORAC__OK : ORAC__ERROR );

  } else {

    my $file = $Frm->raw;
    $file =~ s/\.sdf$//;

    $ORAC_STATUS = ( copy_hdsobj( "$file.I${i}", "$tmpfile.I${i}" ) ? ORAC__OK : ORAC__ERROR );

  }
}

( undef, undef, my $infile ) = File::Spec->splitpath( $tmpfile );

orac_print "Converting $infile to Rice-compressed FITS file.\n";

my $outfile = "c" . $Frm->raw;
my $outfile_proper = $Frm->raw;
$outfile_proper =~ s/\.sdf$/\.fit/;
$infile = $Frm->raw;

my @outfiles = $tmpCvt->convert( $infile, { IN => 'HDS', OUT => 'WFCAM_MEF' } );

# Check to see if it's already been compressed. Rice compressed data looks like
# a FITS table, so check for that...

my $hdutype;
my $status = 0;
my $fptr = Astro::FITS::CFITSIO::open_file($outfiles[1],READONLY,$status);
$fptr->movabs_hdu(2,$hdutype,$status);

# If it's an image then do the compression
if ($hdutype == IMAGE_HDU) {
    my $nread = $Frm->hdr("NEXP");
    $nread = 1 if (! $nread);

    # Get the number of header units

    my ($msg,$nhdu);
    $fptr->get_num_hdus($nhdu,$status);
    orac_err("File $outfiles[1] isn't a MEF (nhdus = $nhdu)\n") if ($nhdu <= 1);

    # Read the headers from the input HDUs

    my ($hdr,$hdr1,$nk,$i);
    $status = 0;
    $fptr->movabs_hdu(1,$hdutype,$status);
    $fptr->hdr2str(1,$hdr,$nk,$status);
    for ($i = 2; $i <= $nhdu; $i++) {
        $fptr->movabs_hdu($i,$hdutype,$status);
        $fptr->hdr2str(1,$hdr1,$nk,$status);
        $hdr .= $hdr1;
    }
    $nk = length $hdr;
    $nk /= 80;

    # Find cards that lack an equal sign and fix them

    my $ist;
    for ($i = 0; $i < $nk; $i++) {
        $ist = $i*80;
        if (substr($hdr,$ist+8,1) ne '=') {
            substr($hdr,$ist+8,1) = '=';
            substr($hdr,$ist+10,1) = '\'';
            substr($hdr,$ist+29,1) = '\'';
        }
    }

    # Set up the new header cards

    my @newphu = ();
    my @newehu = ();
    my ($keyname,$keylength);
    for ($i = 0; $i < $nk; $i++) {
        $kk = substr($hdr,$i*80,80);
        next if (Astro::FITS::CFITSIO::fits_get_keyclass($kk) <= TYP_CKSUM_KEY);
        Astro::FITS::CFITSIO::fits_get_keyname($kk,$keyname,$keylength,$status);
        next if (exists $dellut{$keyname});
        if (exists $ehulut{$keyname}) {
            push @newehu,$kk;
        } else {
            push @newphu,$kk;
        }
    }

    # Create the output file and copy the primary header

    my ($optr,@naxes,$line);
    unlink $outfile if (-f $outfile);
    $optr = Astro::FITS::CFITSIO::create_file($outfile,$status);
    if ($status != 0) {
	Astro::FITS::CFITSIO::fits_get_errstatus($status,$msg);
	$fptr->close_file($status);
	orac_err("Failed to create file: $outfile\n$msg\n");
    }
    $optr->create_img(BYTE_IMG,0,\@naxes,$status);
    foreach $line (@newphu) {
        $optr->write_record($line,$status);
    }
    if ($status != 0) {
	Astro::FITS::CFITSIO::fits_get_errstatus($status,$msg);
	$fptr->close_file($status);
	$optr->close_file($status);
	orac_err("Failed to create primary in $outfile\n$msg\n");
    }

    # Set compression parameters

    $optr->set_compression_type(RICE_1,$status);

    # Now loop for each of the input header units

    my ($bitpix,$naxis,$npix,@fdata,$anynul,$j,$bscale,$bzero);
    my ($nkeys,$card);
    for ($i = 1; $i < $nhdu; $i++) {
	$fptr->movabs_hdu($i+1,$hdutype,$status);

	# Get the image parameters and create a similar output image

	$fptr->get_img_parm($bitpix,$naxis,\@naxes,$status);
	$optr->create_img(LONG_IMG,$naxis,\@naxes,$status);
	if ($status != 0) {
	    Astro::FITS::CFITSIO::fits_get_errstatus($status,$msg);
	    $fptr->close_file($status);
	    $optr->close_file($status);
	    orac_err("Failed to create output image $i in $outfile\n$msg\n");
	}
        foreach $line (@newehu) {
            $optr->write_record($line,$status);
        }

	# Read the input data

	$npix = $naxes[0]*$naxes[1];
	$fptr->read_img(TFLOAT,1,$npix,undef,\@fdata,$anynul,$status);
	if ($status != 0) {
	    Astro::FITS::CFITSIO::fits_get_errstatus($status,$msg);
	    $fptr->close_file($status);
	    $optr->close_file($status);
	    orac_err("Failed to read input image $i in " . $outfiles[1].  "\n$msg\n");
	}
	if ($nread != 1) {
	    for ($j = 0; $j < $npix; $j++) {
		$fdata[$j] *= $nread;
	    }
	}

	# Now write the output data

	$fptr->read_key(TDOUBLE,"BSCALE",$bscale,undef,$status);
	if ($status != 0) {
	    $bscale = 1.0;
	    $status = 0;
	}
	$fptr->read_key(TDOUBLE,"BZERO",$bzero,undef,$status);
	if ($status != 0) {
	    $bzero = 0.0;
	    $status = 0;
	}
	$optr->set_bscale($bscale,$bzero,$status);
	$bscale /= $nread;
	$optr->write_img(TFLOAT,1,$npix,\@fdata,$status);
	if ($status != 0) {
	    Astro::FITS::CFITSIO::fits_get_errstatus($status,$msg);
	    $fptr->close_file($status);
	    orac_err("Failed to write output image $i in $outfile\n$msg\n");
	}

        # Update the bscale parameter

	$optr->update_key(TDOUBLE,"BSCALE",$bscale,undef,$status);
    }

    $fptr->close_file($status);
    $optr->close_file($status);
    orac_print("CASU file $outfile_proper created\n");
    unlink $outfiles[1];
    rename($outfile,$outfile_proper);
} else {
    $fptr->close_file($status);
    orac_print("$outfile_proper is already compressed\n");
}

( my $in, $out ) = $Frm->inout( "_adu" );
$out =~ s/\..*$//;
$out .= ".sdf";
unlink $out;

=head1 NAME

_COPY_COMPRESSED_ -- Make a copy of an input FITS file to a Rice tile
compressed FITS file

=head1 DESCRIPTION

A copy of the current input fits frame is made. This is done with the CFITSIO
Rice tile compression algorithm. The result is written a directory pointed
to by ORAC_DATA_CASU

=head1 ARGUMENTS

None

=head1 NOTES

The environment variable ORAC_DATA_CASU must be defined and must point to
a directory that can be created and written to.

=head1 AUTHORS

JRL: Jim Lewis (CASU, IoA)

=head1 COPYRIGHT

Copyright (C) 2004-2007 Cambridge Astronomy Survey Unit.
All Rights Reserved

=cut
