#+
# Name:
#    _IMAGING_HELLO_
#
# Purpose:
#    Performs global data-reduction tasks for the UFTI imaging instrument.
#
# Language:
#    Perl5
#
# Description:
#    This primitive does the preparatory work for recipes used by UFTI.
#
#    It permits display of the raw data, sets the pixel origin, and switches
#    on history recording.
#
# Notes:
#    -  The readout lower bounds in the frame internal headers
#    ORAC_X_LOWER_BOUND and ORAC_Y_LOWER_BOUND define the pixel origin. 
#    No origin is set if these do not exist, as happens in wavefront
#    sensing with UFTI. 
#    -  Any AXIS structure and title are removed.
#    -  Corrects the acquisition bug which sets ORAC_WAVEPLATE_ANGLE
#    internal header to be a comment instead of value 0.  It changes both
#    the actual headers and the $Frm->uhdr hash.
#
# Tasks:
#    KAPPA: FITSMOD, HISSET, SETORIGIN.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC)
#
# Copyright:
#    Copyright (C) 1998-2002 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# Every imaging recipe must display the raw data.
    _DISPLAY_FRAME_IMAGE_
    orac_print "\n";

# Specify the NDF.
    my ( $in, $param1 );
    $in = $Frm->file;

# Correct the WPLANGLE header.
# ============================

# See if WPLANGLE has a value.
    if ( !defined( $Frm->uhdr( "ORAC_WAVEPLATE_ANGLE" ) ) ) {

# Set the internal hash for WPLANGLE to value 0.  This will allow the
# flat-field rules to work even if WPLANGLE had no value in the raw
# frame.
       $Frm->uhdr( "ORAC_WAVEPLATE_ANGLE", 0 );

# The data also need to be changed so that the flat can be filed.
# Thus correct the FITS header WPLANGLE value too.  This assumes that
# there is a keyword corresponding to the ORAC_WAVEPLATE_ANGLE.
       my %keywords =  $Frm->translate_hdr( "ORAC_WAVEPLATE_ANGLE" );
       my @wplakey = keys( %keywords );
       $param1 = "edit=update keyword=$wplakey[ 0 ] value=0.0 comment=\\\$C position=!"; 
       $Mon{ "ndfpack_mon" }->obeyw( "fitsmod", "ndf=$in $param1" );
    }

# Remove unwanted components.
# ===========================
    _REMOVE_AXES_
    _REMOVE_TITLE_

# Set the bounds of the NDF.
# ==========================

# Wavefront sensing uses ORAC-DR where readout bounds may not
# be present.  The default origin of (1,1) will be used.
    if ( exists $Frm->uhdr->{ "ORAC_X_LOWER_BOUND" } ) {

# Read readout bounds from the headers.
       my $x1 = $Frm->uhdr( "ORAC_X_LOWER_BOUND" );
       my $y1 = $Frm->uhdr( "ORAC_Y_LOWER_BOUND" );

# Set the parameters for the task.
       $param1 = "ndf=$in origin=[$x1,$y1]";

# Set the pixel origin.
       $Mon{ "ndfpack_mon" }->obeyw( "setorigin", "$param1" );

# Report the processing status.
       orac_print "Frame $in has origin set to ($x1,$y1).\n";
    }

# Initiate history recording.
# ===========================
    $Mon{ "ndfpack_mon" }->obeyw( "hisset", "ndf=$in" );

# Report the processing status.
    orac_print "Frame $in has history recording enabled.\n";

# Report completed processing status for UFTI.
    orac_print "Global UFTI tasks performed.\n\n";

# Podule
# ======

=head1 NAME

_IMAGING_HELLO_ -- Performs global data-reduction tasks for the UFTI imaging instrument.

=head1 DESCRIPTION

This primitive does the preparatory work for recipes used by UFTI.

It permits display of the raw data, sets the pixel origin, and switches
on history recording.

=head1 NOTES

=over 4

=item *

The readout lower bounds in the frame internal headers
ORAC_X_LOWER_BOUND and ORAC_Y_LOWER_BOUND define the pixel origin.  No
origin is set if these do not exist, as happens in wavefront sensing
with UFTI.

=item *

Any AXIS structure and title are removed.

=item *

Corrects the acquisition bug which sets ORAC_WAVEPLATE_ANGLE internal
header to be a comment instead of value 0.  It changes both the actual
headers and the $Frm->uhdr hash.

=back

=head1 TASKS

KAPPA: FITSMOD, HISSET, SETORIGIN.

=head1 AUTHORS

MJC: Malcolm J. Currie (JAC)

=head1 COPYRIGHT

Copyright (C) 1998-2002 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut
