#+
# Name:
#    _ARNICA_STANDARD_MAGNITUDE_
#
# Purpose:
#    Obtains the catalogue magnitude of a standard from the ARNICA catalogue.
#
# Language:
#    Perl5
#
# Description:
#    This primitive reads the ARNICA (Hunt et al. 1998, AJ, 115, 2594)
#    faint-standard catalogue.  A case- and space-insensitive comparison of
#    the supplied object name with  the entries in the table provides a
#    catalogue magnitude in J, H, or K for a standard star.  The catalogue
#    magnitude is returned through an argument.
#
# Arguments:
#    CATMAG = REAL (Returned)
#       The catalogue magnitude for the object and current frame's filter.
#       This is set to the NULLMAG if no magnitude is available.
#    CATNAME = CHARACTER (Returned)
#       The object name in the catalogue.  This has superfluous
#       characters removed from the supplied NAME.
#    NAME = CHARACTER (Given).
#       The object name.  This can be ARNICA catalogue identifier
#       number, or the alternative UKIRT Faint-standard (FS) name.
#    NULLMAG = REAL (Given)
#       A null magnitude when the object has no magnitude for the filter.
#       [99.999]
#    WAVEBAND = CHARACTER (Given)
#       The name of the waveband.  It may be J, H, or K.
#       It comes from _GET_FILTER_PARAMETERS_.
#
# Notes:
#    -  This primitive is suitable for near-infra-red imagers.
#    -  Processing should only occur when it is time to perform
#    photometry, i.e. when the steering header DO_APHOT is true,
#    however, this primitive does not make that check.  That is to
#    permit photometry of all the contributing frames of a mosaic.
#    -  An error occurs when the WAVEBAND is not one of the allowed
#    options.
#    -  The standard-star catalogue used is $ORAC_DATA_CAL/arnica_jhk.dat.
#    An error results when the catalogue cannot be opened.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC, Starlink)
#
# Copyright:
#    Copyright (C) 1998-2004 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# For OS-independent filename creation.
    use File::Spec;

# Assign some defaults.
    my $catname = " ";
    my $defnullmag = 99.999;

# Arguments
# =========

# Obtain the suffix value for the file name in the log.  Used
# for NOD positive and negative images, but normally set to null
    my $objname = ( defined( $_ARNICA_STANDARD_MAGNITUDE_{NAME} ) ?
                     $_ARNICA_STANDARD_MAGNITUDE_{NAME} : "" );
    if ( $objname eq "" ) {
       orac_err "Programming error: _ARNICA_STANDARD_MAGNITUDE_ not " .
                "supplied an object name.\n";
    }
 
# Obtain the waveband for the magnitude.
    my $waveband = ( defined( $_ARNICA_STANDARD_MAGNITUDE_{WAVEBAND} ) ?
                   $_ARNICA_STANDARD_MAGNITUDE_{WAVEBAND} : "K" );
    if ( $waveband !~ /^[JHK]/ ) {
       orac_err "Programming error: _ARNICA_STANDARD_MAGNITUDE_ " .
                "supplied $waveband, which is not a valid waveband. " .
                "Allowed options are J, H, K, Ks.\n";
    }

# Obtain the column name for the magnitude.
    my $nullmag = ( defined( $_ARNICA_STANDARD_MAGNITUDE_{NULLMAG} ) ? 
                    $_ARNICA_STANDARD_MAGNITUDE_{NULLMAG} : $defnullmag );

# Set the default magnitude.
    $catmag = $nullmag;

# Extract the standard's name(s).
# ===============================

# Remove spaces from the name, and convert to uppercase for later
# comparison.
    ( my $starname = uc( $objname ) ) =~ s/ //g;

# More than one naming is used and some observatories use more than
# one catalogue name in the object name.  UKIRT Faint Standards have
# name FSn, where n is the number.  The ARNICA format is AS, a two-digit
# number, hyphen and a single-digit number.

# Read the catalogue.
# ===================

# Define the character to indicate a null value in the standards' table.
    my $nullcol = "&";

# Open the ARNICA faint-standards file.
    my $standards_file = $Cal->find_file("arnica_jhk.dat");

    my $fh_standards;
    open( $fh_standards, "<$standards_file" ) || 
      orac_warn "Standards file $standards_file could not be opened.  Error: $!.\n";

# Extract the records into an array.
    my @stds = <$fh_standards>;

# Remove trailing blanks and newline.
    for ( @stds ) {
       chomp;
       s/\s+$//;
    }

# Look for the standard in the catalogue.
# =======================================

# Search through the table finding a line containing the object name.  The
# file currently consists of some column headings, then a relational table.
# Columns in the table include the star name, and the various magnitudes
# and errors.

# Look at each line.  Extract the name, stripping the blanks.  Proceed
# until an uppercase match is found.  Allow for the observer to append
# garbage at the end of the object name, so don't perform an exact match.
    my $recno = 1;
    my $name = " ";
    my $othername = " ";
    until ( $name eq $starname || $recno > $#stds || $othername eq $starname ) {
       $recno++;

# The file does not have a catalogue prefix in the first column.
# Extract the names, removing any blanks.
       if ( defined( $stds[ $recno ] ) ) {
          $name = uc( substr( $stds[ $recno ], 0, 6 ) );
          $othername = uc( substr( $stds[ $recno ], 8, 4 ) );
          $name =~ s/ //g;
          $othername =~ s/ //g;
       }
    }

# Extract the system magnitude of the standard.
# =============================================

# Check that a match has been found.  Use a special value to indicate
# a null magnitude.
    if ( $recno > $#stds ) {
       if ( -e $standards_file ) {
          orac_warn "Standard $objname has no tabulated $waveband magnitude in $standards_file\n";
       } else {
          orac_warn "The standards file $standards_file does not exist\n";
       }
       $catmag = $nullmag;

    } else {

# Remember the actual object name.
       $catname = $name;

# Extract the columns.
       my @fields = split( / +/, $stds[ $recno ] );

# Set up hash of columns versus filter in the current file.
       my %wavebandindex = ( J => 4,
                             H => 6,
                             K => 8 );

# Obtain the desired magnitude.
       $catmag = $fields[ $wavebandindex{ $waveband } ];
    }

# Close the stamdards data file.
    close( $fh_standards );

# Set a null magnitude if there is no value in the table for the
# chosen star and waveband.
    $catmag = $nullmag  if $catmag eq $nullcol;

# Return the catalogue magnitude and name.
    $_ARNICA_STANDARD_MAGNITUDE_{CATMAG} = $catmag;
    $_ARNICA_STANDARD_MAGNITUDE_{CATNAME} = $catname;

# Podule
# ======

=head1 NAME

_ARNICA_STANDARD_MAGNITUDE_ -- Obtains the catalogue magnitude of a
standard from the ARNICA catalogue.

=head1 DESCRIPTION

This primitive reads the ARNICA (Hunt et al. 1998, AJ, 115, 2594)
faint-standard catalogue.  A case- and space-insensitive comparison of
the supplied object name with  the entries in the table provides a
catalogue magnitude in I<J>, I<H>, or I<K> for a standard star.  The catalogue
magnitude is returned through an argument.

=head1 ARGUMENTS

=over 4

=item CATMAG = REAL (Returned)

The catalogue magnitude for the object and current frame's filter.

=item CATNAME = CHARACTER (Returned)

The object name in the catalogue.  This has superfluous
characters removed from the supplied NAME.

=item NAME = CHARACTER (Given).

The object name.  This can be ARNICA catalogue identifier
number, or the alternative UKIRT Faint-standard (FS) name.

=item NULLMAG = REAL (Given)

A null magnitude when the object has no magnitude for the filter.
[99.999]

=item WAVEBAND = CHARACTER (Given)

The name of the waveband.  It may be J, H, or K.
It comes from L<_GET_FILTER_PARAMETERS_|_GET_FILTER_PARAMETERS_>.

=back

=head1 NOTES

=over 4

=item *

This primitive is suitable for near-infra-red imagers.

=item *

Processing should only occur when it is time to perform
photometry, i.e. when the steering header DO_APHOT is true,
however, this primitive does not make that check.  That is to
permit photometry of all the contributing frames of a mosaic.

=item *

An error occurs when the WAVEBAND is not one of the allowed
options.

=item *

The standard-star catalogue used is F<$ORAC_DATA_CAL/arnica_jhk.dat>.
An error results when the catalogue cannot be opened.

=head1 AUTHORS

MJC: Malcolm J. Currie (JAC, Starlink)

=head1 COPYRIGHT

Copyright (C) 1998-2004 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut
