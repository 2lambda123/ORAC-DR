# -*-perl-*-

=head1 NAME

_CALCULATE_IMAGE_STATISTICS_ - Calculate various image quality
statistics based on an input catalogue.

=head1 DESCRIPTION

This primitive calculates image quality statistics based on an input
catalogue. This catalogue is typically the output from either
C<Starlink::Extractor> or C<Starlink::Autoastrom>, so that object
morphology information is available for calculations.

This primitive calculates the mean axial ratio of bright sources, the
average object diameter converted to K-band equivalent, the sky
brightness, the limiting magnitude, and the instrumental zero point.

=head1 ARGUMENTS

=over 4

=item GROUP = LOGICAL (Given)

If true, use the current Group file for sky brightness
calculations. Otherwise, use the current Frame. [0]

=item IMAGE_CATALOGUE = C<Astro::Catalog> object (Given)

=item MAGNITUDE_TYPE = CHARACTER (Given)

The magnitude type to use when  calculating zeropoint. This can be any
of the MAG_* output parameters from SExtractor. [MAG_ISO]

=back

=head1 NOTES

=over 4

=item *

Files results with the Calibration system. FWHM is obtained with the
fwhm() method, axial ratio is obtained with the axial_ratio() method,
sky brightness is obtained with the sky_brightness() method, limiting
magnitude is obtained with the limiting_mag() method, and the zero
point is obtained with the zeropoint() method.

=back

=head1 OUTPUT DATA

=over 4

None.

=back

=head1 TASKS

=head1 REQUIRED PERL MODULES

Astro::Catalog.

=head1 AUTHORS

Brad Cavanagh <b.cavanagh@jach.hawaii.edu>

=head1 COPYRIGHT

Copyright (C) 2005 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut

# Find out if we want to do statistics on the current Group or Frame
# object.
my $group = $_CALCULATE_IMAGE_STATISTICS_{GROUP};
my $Obj;
$group = defined( $group ) ? $group : 0;
if( $group ) {
  $Obj = $Grp;
} else {
  $Obj = $Frm;
}

# Get the magnitude type, defaulting to MAG_ISO.
my $magnitude_type = $_CALCULATE_IMAGE_STATISTICS_{MAGNITUDE_TYPE} || 'MAG_ISO';

# Get the image catalogue.
my $image_catalogue = $_CALCULATE_IMAGE_STATISTICS_{IMAGE_CATALOGUE};

# Only run if we actually have an image catalogue.
if( defined( $image_catalogue ) ) {

  # Strip out any Astro::Catalog::Item objects that don't have a
  # quality of 0 (which means good), or an area greater than 78.5
  # pixels (which is a circle with a radius of five pixels) multiplied
  # by the square of the expansion ratio, or an ellipticity greater
  # than 0.25. This will eliminate any galaxies, saturated stars,
  # blended objects, etc. from the image quality calculations.

  my @gooditems;
  my @catmatchitems;

  _GET_EXPANSION_RATIO_

# increased this to ten pixels due to the not uncommmon rotten SSO seeing

  my $maxarea = 314. * ( $_GET_EXPANSION_RATIO_{X_EXPANSION} ) ** 2;

  foreach my $item ( $image_catalogue->stars ) {

    next if $item->quality;
    next if ( defined( $item->morphology->area ) &&
              $item->morphology->area > $maxarea );
    push @gooditems, $item;

    my $fluxes = $item->fluxes;
    my @allfluxes = $fluxes->allfluxes;
    my $cat_match = 0;
    my $waveband = new Astro::WaveBand( Filter => $Frm->uhdr( "ORAC_FILTER" ) );
    my %standard_filters;

    foreach my $flux ( @allfluxes ) {
      if( uc( $flux->type ) eq 'MAG_CATALOG' ) {
        $standard_filters{uc($flux->waveband)}++;
      }
    }

    if( $standard_filters{'J'} &&
        $standard_filters{'K'} &&
        $standard_filters{'H'} ) {
      push @catmatchitems, $item;
    }
  }

  my $newcat = new Astro::Catalog;
  $newcat->pushstar( @gooditems );
  $image_catalogue = $newcat;

  # Create a temporary file to hold the input catalogue to PSF.
  my $psf_input_temp = new ORAC::TempFile( SUFFIX => '.txt' );

  # Write the catalogue to disk.
  $image_catalogue->write_catalog( File => $psf_input_temp->file,
                                   Format => 'STL' );

  # Set up the parameters for PSF.
  my $params = "incat=" . $psf_input_temp->file . " "
             . "device=! gauss=false logfile=! ";

  if( $group ) {
    $params .= "in=" . $Grp->file;
  } else {
    $params .= "in=" . $Frm->file;
  }

  # Run PSF.
  $Mon{'kappa_mon'}->obeyw( "psf", "$params" );

  # Obtain statistics.
  ( my $ORAC_STATUS, my $axialratio ) = $Mon{'kappa_mon'}->get( "psf", "axisr" );
  ( $ORAC_STATUS, my $fwhm ) = $Mon{'kappa_mon'}->get( "psf", "fwhm" );
  ( $ORAC_STATUS, my $gamma ) = $Mon{'kappa_mon'}->get( "psf", "gamma" );
  ( $ORAC_STATUS, my $orient ) = $Mon{'kappa_mon'}->get( "psf", "orient" );

  my $ellipticity = 1 - ( 1 / $axialratio );

  # Calculate the K-band equivalent seeing. It goes as
  # (wavelength^(-0.2)), so we need to get the current wavelength and
  # the K wavelength, do the division and the power, and scale the
  # measured FWHM.

  _GET_FILTER_PARAMETERS_
  my $wavelength = $_GET_FILTER_PARAMETERS_{WAVELENGTH};
  my $k_wavelength = 2.25;
  my $scale = ( $wavelength / $k_wavelength ) ** ( -0.2 );

  # the equation below was the wrong way around
  my $k_fwhm = $fwhm / $scale;

  # Obtain the mean extinction coefficient.
  my $waveband = $_GET_FILTER_PARAMETERS_{WAVEBAND};
  my $extinction = $_GET_FILTER_PARAMETERS_{EXTINCTION};
  my $filter = $Frm->uhdr( "ORAC_FILTER" );

  # Obtain the airmass from the FITS headers. Find the
  # approximate mean airmass from the start and end values. This
  # should be adequate unless it is a long integration with the
  # source crossing the meridian.

  my $amstart = $Obj->uhdr( "ORAC_AIRMASS_START" );
  my $amend   = $Obj->uhdr( "ORAC_AIRMASS_END" );
  my $airmass = 0.5 * ( $amstart + $amend );

  # Derive the extinction correction using the IR standard of
  # one airmass.
  my $extcor = ( $airmass - 1.0 ) * $extinction;

  # Obtain the exposure time.
  my $exptime = $Frm->uhdr( "ORAC_EXPOSURE_TIME" );

  my @image_mags;
  my @standard_mags;
  my @diffs;
  my @ellipticities;
  my %psf_hist;
  my %diff_hist;
  my $stepsize = 0.01;

  my $phot = 0;
  if( $waveband eq 'Z' || $waveband eq 'Y' || $waveband eq 'J' ||
      $waveband eq 'H' || $waveband eq 'K' ) {
    $phot = 1;
  }

  # Now, go through the list of objects that had catalogue magnitudes
  # and calculate the zeropoint.

  foreach my $catmatchitem ( @catmatchitems ) {

    if( $phot ) {
      my $jmag = $catmatchitem->get_flux_quantity( waveband => 'J',
                                                   type => 'MAG_CATALOG' );
      my $hmag = $catmatchitem->get_flux_quantity( waveband => 'H',
                                                   type => 'MAG_CATALOG' );
      my $kmag = $catmatchitem->get_flux_quantity( waveband => 'K',
                                                   type => 'MAG_CATALOG' );

  # restrict to 2MASS 10 sig. detections or better - could 2MASS quality flags be used here instead e.g. A/B only ?

      if (($jmag<=15.8) && ($hmag<=15.1) && ($kmag<=14.3)) {

      # Calculate the Z, Y, J, H, and K magnitudes in the MK system.

       my $mk_zmag = $jmag + 0.95  * ( $jmag - $hmag );
       my $mk_ymag = $jmag + 0.5   * ( $jmag - $hmag );
 #     my $mk_jmag = $jmag - 0.075 * ( $jmag - $hmag );
 #     my $mk_hmag = $hmag + 0.075 * ( $jmag - $hmag );
 #     my $mk_kmag = $kmag - 0.015 * ( $jmag - $kmag );

    #  Calculate the J, H, and K magnitudes of 2MASS detections in the MK system.
    #  These equations are lifted from the Feb 2007 AAO newsletter article of Stuart
    #  Ryder (AAO)

      my $mk_jmag = $jmag - 0.03 - 0.03 * ( $jmag - $kmag );
      my $mk_hmag = $hmag - 0.01 + 0.05 * ( $hmag - $kmag );
      my $mk_kmag = $kmag - 0.01 - 0.01 * ( $jmag - $kmag );

#     calculated J-K and H-K in MKO system

      my $km_jkcol = $mk_jmag - $mk_kmag;
      my $km_hkcol = $mk_hmag - $mk_kmag;

      # Get the magnitude in the current waveband.

      # Bodged waveband here since observed mag returned from Autoastrom as J band (????)
      # It's beyond my limited knowledge of Perl/ORAC as to how this can be changed

      my $im_mag = $catmatchitem->get_flux_quantity( waveband => 'J',
                                                     type => $magnitude_type );

      # Skip if the magnitude is fainter than 10 (which denotes
      # something bad).

      next if ( ! defined( $im_mag ) );
      next if $im_mag > 10;

      my $st_mag;

      if( $waveband eq 'Z' ) {
        $st_mag = $mk_zmag;
      } elsif( $waveband eq 'Y' ) {
        $st_mag = $mk_ymag;
      } elsif( $waveband eq 'J' ) {
        $st_mag = $mk_jmag;
      } elsif( $waveband eq 'H' ) {
        $st_mag = $mk_hmag;
      } elsif( $waveband eq 'K' ) {
        $st_mag = $mk_kmag;
      }

      next if ( ! defined( $st_mag ) || ! defined( $im_mag ) );

      # Correct the measured magnitude for exposure time.
      $im_mag += 2.5 * log10( $exptime );

      # Correct the measured magnitude for extinction.
      $im_mag -= $extcor;

      # Push the magnitudes onto the arrays, correcting the
      # magnitude from the EXTRACTOR-based catalogue.
      push( @image_mags, $im_mag );
      push( @standard_mags, $st_mag );

      push( @diffs, $st_mag - $im_mag );
      $diff_hist{ int( ( $st_mag - $im_mag ) / $stepsize ) * $stepsize }++;

   }
}
    $psf_hist{ int( $catmatchitem->morphology->ellipticity->value / $stepsize ) * $stepsize }++;
    push( @ellipticities, $catmatchitem->morphology->ellipticity->value );
}

  my $zeropoint = 0;
  my $zeropoint_variance = 0;
  my $zp_calc = 0;

# added zp_calc to flag if there are no suitable stars for photometric stats calculation

  if ( $#diffs > 0 ) {
     $zp_calc = 1;
  }

  if (( $phot ) && ( $zp_calc )){

    # Calculate the mean difference and the variance in the
    # difference between the standard magnitudes and the measured
    # magnitudes.
    my $sum = 0;
    foreach my $diff ( @diffs ) {
      $sum += $diff;
    }

    $zeropoint = $sum / ( $#diffs + 1 );

    my $sumsqu = 0;
    foreach my $diff( @diffs ) {
      $sumsqu += ( $diff - $zeropoint ) * ( $diff - $zeropoint );
    }
    $zeropoint_variance = sqrt( $sumsqu / ( $#diffs + 1 ) );

  }

  my $ext_ell_sum = 0;
  foreach my $ext_ell ( @ellipticities ) {
    $ext_ell_sum += $ext_ell;
  }
  my $ext_ell_mean = $ext_ell_sum / ( $#ellipticities + 1 );

  my $maxell = 0;
  my $maxbin = 0;
  foreach my $ell_bin ( sort keys %psf_hist ) {
    if( $psf_hist{$ell_bin} > $maxell ) {
      $maxell = $psf_hist{$ell_bin};
      $maxbin = $ell_bin;
    }
  }

  my $maxdiff = 0;
  my $maxdiffbin = 0;
  if (( $phot ) && ( $zp_calc )) {
    foreach my $diff_bin ( sort keys %diff_hist ) {
      if( $diff_hist{$diff_bin} > $maxdiff ) {
        $maxdiff = $diff_hist{$diff_bin};
        $maxdiffbin = $diff_bin;
      }
    }
  }

  # We have ellipticity bin that has the most counts in it, so take
  # two on either side and calculate the average of those.

  my $ell_hist_avg = 0;
  if( $maxbin eq '0' ) {
    # Take 0, 0.01 and 0.02.
    my $sum_counts = $psf_hist{0} + $psf_hist{'0.01'} + $psf_hist{'0.02'};
    my $sum_psf = $psf_hist{'0.01'} * 0.01 + $psf_hist{'0.02'} * 0.02;
    $ell_hist_avg = $sum_psf / $sum_counts;
  } elsif( $maxbin eq '0.01' ) {
    # Take 0, 0.01, 0.02, and 0.03.
    my $sum_counts = $psf_hist{0} + $psf_hist{'0.01'} + $psf_hist{'0.02'} + $psf_hist{'0.03'};
    my $sum_psf = $psf_hist{'0.01'} * 0.01 + $psf_hist{'0.02'} * 0.02 + $psf_hist{'0.03'} * 0.03;
    $ell_hist_avg = $sum_psf / $sum_counts;
  } else {

    my $first = $maxbin - 0.02;
    my $second = $maxbin - 0.01;
    my $third = $maxbin + 0.01;
    my $fourth = $maxbin + 0.02;
    if( ! defined( $psf_hist{"$first"} ) ) {
      $psf_hist{"$first"} = 0;
    }
    if( ! defined( $psf_hist{"$second"} ) ) {
      $psf_hist{"$second"} = 0;
    }
    if( ! defined( $psf_hist{"$third"} ) ) {
      $psf_hist{"$third"} = 0;
    }
    if( ! defined( $psf_hist{"$fourth"} ) ) {
      $psf_hist{"$fourth"} = 0;
    }
    my $sum_counts = $psf_hist{"$first"} + $psf_hist{"$second"} + $psf_hist{"$maxbin"} + $psf_hist{"$third"} + $psf_hist{"$fourth"};
    my $sum_psf = $psf_hist{"$first"}  * $first
                + $psf_hist{"$second"} * $second
                + $psf_hist{"$maxbin"} * $maxbin
                + $psf_hist{"$third"}  * $third
                + $psf_hist{"$fourth"} * $fourth
                ;
    $ell_hist_avg = $sum_psf / $sum_counts;
  }

  my $diff_hist_avg = 0;
  if (( $phot ) && ( $zp_calc )){
    my $first = $maxdiffbin - 0.02;
    my $second = $maxdiffbin - 0.01;
    my $third = $maxdiffbin + 0.01;
    my $fourth = $maxdiffbin + 0.02;
    if( ! defined( $diff_hist{"$first"} ) ) {
      $diff_hist{"$first"} = 0;
    }
    if( ! defined( $diff_hist{"$second"} ) ) {
      $diff_hist{"$second"} = 0;
    }
    if( ! defined( $diff_hist{"$third"} ) ) {
      $diff_hist{"$third"} = 0;
    }
    if( ! defined( $diff_hist{"$fourth"} ) ) {
      $diff_hist{"$fourth"} = 0;
    }
    my $sum_diffs = $diff_hist{"$first"} + $diff_hist{"$second"} + $diff_hist{"$maxdiffbin"} + $diff_hist{"$third"} + $diff_hist{"$fourth"};
    my $sum_diff = $diff_hist{"$first"}  * $first
                + $diff_hist{"$second"} * $second
                + $diff_hist{"$maxdiffbin"} * $maxdiffbin
                + $diff_hist{"$third"}  * $third
                + $diff_hist{"$fourth"} * $fourth
                ;
    $diff_hist_avg = $sum_diff / $sum_diffs;
  }

  # Now that we have the zeropoint, we can calculate the sky
  # brightness statistics.

  # Run clipped mean stats on the Object.
  $params = "ndf=" . $Obj->file . " clip=[2,3,3]";
  $Mon{'kappa_mon'}->obeyw( "stats", "$params" );
  ( $ORAC_STATUS, my $sky_mean ) = $Mon{'kappa_mon'}->get( "stats", "mean" );
  ( $ORAC_STATUS, my $sky_stddev ) = $Mon{'kappa_mon'}->get( "stats",  "sigma" );

  # Now to calculate the sky level in magnitudes/arcsec^2.
  my $mag_per_square_arcsecond = 0;
  if (( $phot ) && ( $zp_calc )) {
    _GET_PLATE_SCALE_
    my $plate_scale = $_GET_PLATE_SCALE_{PLATESCALE};
    my $counts_per_square_arcsecond = $sky_mean
                                      / ( $plate_scale * $plate_scale );
    $mag_per_square_arcsecond = $zeropoint
                                 - 2.5
                                 * log10( $counts_per_square_arcsecond /
                                          $exptime );
  }


  # Create printable versions.
  my $p_axial = sprintf( "%.3f", $axialratio );
  my $p_ell = sprintf( "%.3f", $ellipticity );
  my $p_ell_ext = sprintf( "%.3f", $ext_ell_mean );
  my $p_ell_bin = sprintf( "%.3f", $maxbin );
  my $p_ell_bin_avg = sprintf( "%.3f", $ell_hist_avg );
  my $p_fwhm = sprintf( "%.3f", $fwhm );
  my $p_k_fwhm = sprintf( "%.3f", $k_fwhm );
  my $p_orient = sprintf( "%.3f", $orient );
  my $p_gamma = sprintf( "%.3f", $gamma );
  my $p_num_obj = sprintf( "%d", $#diffs + 1 );
  my $p_zp = sprintf( "%.3f", $zeropoint );
  my $p_zp_hist = sprintf( "%.3f", $diff_hist_avg );
  my $p_zpvar = sprintf( "%.3f", $zeropoint_variance );
  my $p_ext = sprintf( "%.3f", $extcor );
  my $p_sky_mean = sprintf( "%.2f", $sky_mean );
  my $p_sky_stddev = sprintf( "%.2f", $sky_stddev );
  my $p_mag = sprintf( "%.2f", $mag_per_square_arcsecond );

  my $p_filter = $Frm->uhdr( "ORAC_FILTER" );
  my $p_exptime = sprintf( "%.1f", $Frm->uhdr( "ORAC_EXPOSURE_TIME" ) );
  my $p_object = $Frm->uhdr( "ORAC_OBJECT" );

  # Output statistics to the user.
  orac_print( "Image information for " .
              ( $group ? $Grp->file : $Frm->file ) .
              ":\n", "blue" );
  orac_print( " Filter:        $p_filter\n", "blue" );
  orac_print( " Exposure time: $p_exptime seconds\n", "blue" );
  orac_print( " Object name:   $p_object\n", "blue" );
  orac_print( "\n" );
  orac_print( "Image quality statistics on " .
              ( $group ? $Grp->file : $Frm->file ) .
              ":\n", "blue" );
  orac_print( " Axial ratio:  $p_axial\n", "blue" );
  orac_print( " Ellipticity:  $p_ell_bin_avg\n", "blue" );
#  orac_print( " Ellipticity:  $p_ell (PSF)\n", "blue" );
#  orac_print( "               $p_ell_ext (mean from EXTRACTOR)\n", "blue" );
#  orac_print( "               $p_ell_bin (histogram EXTRACTOR)\n", "blue" );
#  orac_print( "               $p_ell_bin_avg (histogram EXTRACTOR, average around peak)\n", "blue" );
  orac_print( " FWHM:         $p_fwhm arcseconds\n", "blue" );
  orac_print( " K-band equiv: $p_k_fwhm arcseconds\n", "blue" );
  orac_print( " Orientation:  $p_orient degrees east of north\n", "blue" );
  orac_print( " Gamma:        $p_gamma (2 = gaussian)\n", "blue" );
  orac_print( "\n" );


  if ( ! $zp_calc ) {
  orac_print( "No suitable 2MASS objects found for determining photometric statistics\n", "red" );
} else {


  orac_print( "Photometric statistics on " .
              ( $group ? $Grp->file : $Frm->file ) .
              ":\n", "blue" );
  orac_print( " Instrumental zero-point: $p_zp_hist\n", "blue" );
#  orac_print( " Instrumental zero-point: $p_zp +/- $p_zpvar ($p_num_obj objects used)\n", "blue" );
#  orac_print( "                          $p_zp_hist (histogram)\n", "blue" );
  orac_print( " Background sky level:    $p_sky_mean +/- $p_sky_stddev counts.\n",
              "blue" );
  orac_print( " Sky brightness:          $p_mag magnitudes/arcsec^2.\n",
              "blue" );
  orac_print( " Extinction correction of $p_ext was used.\n", "blue" );
}
  # File the whole shebang with the calibration system.
  my %calhash = ( %{$Obj->hdr},
                  AXIALRATIO => $axialratio,
                  ELLIPTICITY => $ell_hist_avg,
                  FWHM => $fwhm,
                  FWHM_K => $k_fwhm,
                  FILTER => $waveband,
                  GAMMA => $gamma,
                  ORIENT => $orient,
                  SKY_VALUE => $sky_mean,
                  SKY_VALUE_ERROR => $sky_stddev,
                  SKY_VALUE_MAG => $mag_per_square_arcsecond,
                  ZEROPOINT => $diff_hist_avg,
                  ZEROPOINT_ERROR => $zeropoint_variance,
                );
        $Cal->dqcindex->add( $Obj->file, \%calhash );

#  possibly file the FWHM for second pass photometric step with smaller seeing matched aperture

#        my %fwhmhash = ( %{ $Obj->hdr }, FWHM => $fwhm );
#        $Cal->fwhmindex->add( $Obj->file, \%fwhmhash );


}
