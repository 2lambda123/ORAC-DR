=head1 NAME

_FIX_OFFSET_POSITION_HEADERS_ - set internal headers to proper offset values

=head1 DESCRIPTION

This primitive uses an image at a base position of a jitter and modifies
internal ORAC headers of subsequent images to give proper offset values.

=head1 ARGUMENTS

=over 4

=item SKY_FIRST = LOGICAL

Whether or not the first observation in the group is sky. [1]

=back

=head1 NOTES

=over 4

=item *

Editing only occurs for TARGET frames, as specified by the
TARGET_OR_SKY internal header.

=item *

This primitive assumes that members of the current group were taken
in an object-sky sequence, where the first two images in the group
are either object-sky or sky-object. The primitive will use one of
the first two images as a base.

=back

=head1 AUTHORS

Brad Cavanagh <b.cavanagh@jach.hawaii.edu> (JAC)

=head1 COPYRIGHT

Copyright (C) 2002 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut


# Retrieve the SKY_FIRST argument, default to 1 (true).

my $sky_first = $_FIX_OFFSET_POSITION_HEADERS_{SKY_FIRST};
$sky_first = defined( $sky_first ) ? $sky_first : 1;

# Check the TARGET_OR_SKY internal header to make sure we
# should be doing this.

if( $Frm->uhdr("TARGET_OR_SKY") eq "target" ) {

  # Create a temporary frame to hold the reference frame.

  my $tmpFrm = $Grp->members->[$sky_first];
  $tmpFrm->readhdr();

  # Retrieve the base RA and Dec from the reference frame.

  my $baseRA = $tmpFrm->uhdr("ORAC_RA_BASE");
  my $baseDec = $tmpFrm->uhdr("ORAC_DEC_BASE");

  # Retrieve the RA and Dec from the current frame.

  my $currRA = $Frm->uhdr("ORAC_RA_BASE");
  my $currDec = $Frm->uhdr("ORAC_DEC_BASE");

  # Calculate the offset. The offset is the current position minus
  # the base position.

  my $offsetRA = $currRA - $baseRA;
  my $offsetDec = $currDec - $baseDec;

  # Change these offsets into arcseconds.

  $offsetRA *= 3600;
  $offsetDec *= -3600;

  # Store these offsets in the internal headers.

  $Frm->uhdr("ORAC_RA_TELESCOPE_OFFSET" => $offsetRA);
  $Frm->uhdr("ORAC_DEC_TELESCOPE_OFFSET" => $offsetDec);

  my $print = sprintf("RA offset changed to %9.3f\n", $offsetRA);
  orac_print $print;
  $print = sprintf("Dec offset changed to %9.3f\n", $offsetDec);
  orac_print $print;

}

