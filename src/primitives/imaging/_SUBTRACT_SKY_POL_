#+
# Name:
#    _SUBTRACT_SKY_POL_
#
# Purpose:
#    Creates sky-subtracted e- and o-beam images for polarimetry recipes.
#
# Language:
#    Perl5
#
# Description:
#    This primitive creates sky-subtracted e- and o-beam images.  The
#    images are subsets of the current frame in the appropriate region.
#    Modal sky values are determined from e- and o-beam sky areas within
#    the frame.  The sky-subtracted e- and o-beam frames are stored in two
#    groups, which are created if they do not already exist.  References
#    to the groups are stored in the main $Grp user headers called
#    EBEAM_GROUP and OBEAM_GROUP.
#
#    The primitive reports a successful sky subtraction and the frames
#    concerned.  It is a variant of _SUBTRACT_SKY_ for POL recipes.
#
# Notes:
#    -  This primitive is suitable for UFTI, IRCAM, and Michelle
#    in imaging mode.
#    -  Processing only occurs for object frames.
#    -  If the supplied target frame contains a variance array,
#    the error in the sky level is incorporated into the
#    sky-subtracted frame's variance.
#    -  The sky-subtracted images can be displayed.
#    -  The frame title is propagated.
#
# Output Data:
#    -  Sky-subtracted e-beam and o-beam frames inheriting the frame's
#    name but with the _ess and _oss suffices respectively.
#
# Tasks:
#    KAPPA: CSUB, STATS.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC)
#
# Copyright:
#    Copyright (C) 1998-2001 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# This should only be performed on OBJECT frames.
    if ( $Frm->uhdr( "ORAC_OBSERVATION_TYPE" ) eq "OBJECT" ) {

# Access the group of e-beam and o-beam frames.
# =============================================

# Does the group of e-beam frames exist?  If not report the fatal error.
       my $eGrp = $Grp->uhdr( "EBEAM_GROUP" );
       if ( ! defined( $eGrp ) ) {
          orac_throw "_SUBTRACT_SKY_POL_: EBEAM_GROUP does not exist " .
                     "(probable programming error).\n";
       }

# Does the group of o-beam frames exist?  If not report the fatal error.
       my $oGrp = $Grp->uhdr( "OBEAM_GROUP" );
       if ( ! defined( $oGrp ) ) {
          orac_throw "_SUBTRACT_SKY_POL_: OBEAM_GROUP does not exist " .
                     "(probable programming error).\n";
       }

# Obtain bounds of rectangular regions around the target and sky.
# ===============================================================

# Compute the pixel bounds of the target and sky e- and o-beam images.
       _DEFINE_POL_REGIONS_

# Get references to the bounds just calculated.
       my $etbref = $_DEFINE_POL_REGIONS_{ETBOUNDS};
       my $otbref = $_DEFINE_POL_REGIONS_{OTBOUNDS};
       my $esbref = $_DEFINE_POL_REGIONS_{ESBOUNDS};
       my $osbref = $_DEFINE_POL_REGIONS_{OSBOUNDS};

# Convert references into arrays.
       my @etbounds = @$etbref;
       my @otbounds = @$otbref;
       my @esbounds = @$esbref;
       my @osbounds = @$osbref;

# Determine whether the NDF supports variance.
# ============================================
       my $in = $Frm->file;
       $Mon{ "ndfpack_mon" }->obeyw( "ndftrace", "ndf=$in" );
       ( my $ORAC_STATUS, my $variance ) = $Mon{ "ndfpack_mon" }->get( "ndftrace", "variance" );

# Find the modal skies.
# =====================

# Define the clipping to get a reasonable estimate of the mode.
       my $param2 = "clip=[2,3,3]";

# e-beam
# ------
# Set the bounds of the section of the e-beam.
       my $param1 = "ndf=$in($esbounds[ 0 ]:$esbounds[ 1 ],$esbounds[ 2 ]:$esbounds[ 3 ])";

# Calculate the clipped mean of the e-beam section of sky.
       $Mon{ "kappa_mon" }->obeyw( "stats", "$param1 $param2" );

# Extract the clipped mean of the frame.
       ( $ORAC_STATUS, my $esky ) = $Mon{ "kappa_mon" }->get( "stats", "mean" );

# Format for later output.
       my $fs_sky = sprintf( "%11.3f", $esky );
       $fs_sky =~ s/ //g;

# Extract the standard deviation of the frame, and number of pixels used
# to calculate the statistics if there is a variance area.
       my ( $ecount, $esigma, $emeanerr );
       if ( $variance eq "TRUE" ) {
          ( $ORAC_STATUS, $esigma ) = $Mon{ "kappa_mon" }->get( "stats", "sigma" );
          ( $ORAC_STATUS, $ecount ) = $Mon{ "kappa_mon" }->get( "stats", "numgood" );

# Prevent divide-by zero, although it seems impossible to get zero good pixels.
          if ( $ecount == 0 ) {
             orac_throw "All the e-beam sky pixels are bad.\n";
          }

# Derive the error on the mean.
          $emeanerr = $esigma / sqrt( $ecount );

# Report the progress.
          my $fs_err = sprintf( "%11.3f", $emeanerr );
          $fs_err =~ s/ //g;
          orac_print "Modal e-beam sky determined from " .
                     "$in is $fs_sky +/- $fs_err.\n";

       } else {
          orac_print "Modal e-beam sky determined from " .
                     "$in is $fs_sky.\n";

       }

# o-beam
# ------
# Calculate and extract the clipped mean of the section.
       $param1 = "ndf=$in($osbounds[ 0 ]:$osbounds[ 1 ],$osbounds[ 2 ]:$osbounds[ 3 ])";
       $Mon{ "kappa_mon" }->obeyw( "stats", "$param1 $param2" );
        ( $ORAC_STATUS, my $osky ) = $Mon{ "kappa_mon" }->get( "stats", "mean" );

# Format for later output.
       $fs_sky = sprintf( "%11.3f", $osky );
       $fs_sky =~ s/ //g;

# Extract the standard deviation of the frame, and number of pixels used
# to calculate the statistics if there is a variance area.
       my ( $ocount, $osigma, $omeanerr );
       if ( $variance eq "TRUE" ) {
          ( $ORAC_STATUS, $osigma ) = $Mon{ "kappa_mon" }->get( "stats", "sigma" );
          ( $ORAC_STATUS, $ocount ) = $Mon{ "kappa_mon" }->get( "stats", "numgood" );

# Prevent divide-by zero, although it seems impossible to get zero good pixels.
          if ( $ocount == 0 ) {
             orac_throw "All the e-beam sky pixels are bad.\n";
          }

# Derive the error on the mean.
          $omeanerr = $osigma / sqrt( $ocount );

# Report the progress.
          my $fs_err = sprintf( "%11.3f", $omeanerr );
          $fs_err =~ s/ //g;
          orac_print "Modal o-beam sky determined from " .
                     "$in is $fs_sky +/- $fs_err.\n";

       } else {
          orac_print "Modal o-beam sky determined from " .
                     "$in is $fs_sky.\n";

       }

# Subtract the sky from the e-beam.
# =================================

# Define the file suffix.
       ( $in, my $e_out ) = $Frm->inout( "_ess" );

       if ( $variance eq "TRUE" ) {

# Form a constant image at the sky level, with the variance set to the
# square of the uncertainty on the sky level.  Use the bounds of the
# e-beam target region so that overlap is guaranteed, and the image is
# no larger than it need be.  Note that dimensionality is set by the
# number of co-ordinates.  Therefore null xa and xb tokens are included
# in the expression.  The accept is to take the default data type.
          $param1 = "exp='$esky+0*xa+0*xb' lbound=[$etbounds[ 0 ],$etbounds[ 2 ]]";
          $param2 = "out=esky_$$ ubound=[$etbounds[ 1 ],$etbounds[ 3 ]] accept";
          $Mon{ "kappa_mon" }->obeyw( "maths", "$param1 $param2" );

          my $evar = $emeanerr * $emeanerr;
          $Mon{ "ndfpack_mon" }->obeyw( "setvar", "ndf=esky_$$ variance=$evar" );

# Subtract the newly created constant frame from the NDF.
          $param1 = "in1=$in($etbounds[ 0 ]:$etbounds[ 1 ],$etbounds[ 2 ]:$etbounds[ 3 ])";
          $param2 = "in2=esky_$$ out=$e_out";
          $Mon{ "kappa_mon" }->obeyw( "sub", "$param1 $param2" );

# We're finished with the constant frame.  It's only role is propagate the
# variance correctly.
          _DELETE_A_FRAME_ FRAME=esky_$$

       } else {

# Subtract the sky from the required section about the target in the e-beam.
          $param1 = "in=$in($etbounds[ 0 ]:$etbounds[ 1 ],$etbounds[ 2 ]:$etbounds[ 3 ])";
          $param2 = "out=$e_out scalar=$esky";
          $Mon{ "kappa_mon" }->obeyw( "csub", "$param1 $param2" );
       }

       orac_print "E-beam of $in sky subtracted to make $e_out\n";

# Store the sky-subtracted e-beam frame in the group.
# ===================================================

# Create a new frame to store the differenced pair image.
       my $eFrm = $Frm->new;
       $eFrm->file( $e_out );

# Need to propagate the raw frame as ORAC-DR pattern matches for files
# ending in a number to set the $Frm->number, which is needed for the
# template method.
       $eFrm->raw( $Frm->raw );

# Transfer the headers to the differenced frame.
       %{$eFrm->hdr} = ( %{$Frm->hdr} );
       %{$eFrm->uhdr} = ( %{$Frm->uhdr} );

# Store the sky-subtracted e-frame in the e-beam group.
       $eGrp->push( $eFrm );

# Display the sky-subtracted e-beam frame.
       $Display->display_data( $eFrm ) if (defined $Display);

# Subtract the sky from the o-beam.
# =================================

# Define the file suffix.
       ( $in, my $o_out ) = $Frm->inout( "_oss" );

       if ( $variance eq "TRUE" ) {

# Form a constant image at the sky level, with the variance set to the
# square of the uncertainty on the sky level.  Use the bounds of the
# o-beam target region so that overlap is guaranteed, and the image is
# no larger than it need be.  Note that dimensionality is set by the
# number of co-ordinates.  Therefore null xa and xb tokens are included
# in the expression.  The accept is to take the default data type.
          $param1 = "exp='$osky+0*xa+0*xb' lbound=[$otbounds[ 0 ],$otbounds[ 2 ]]";
          $param2 = "out=osky_$$ ubound=[$otbounds[ 1 ],$otbounds[ 3 ]] accept";
          $Mon{ "kappa_mon" }->obeyw( "maths", "$param1 $param2" );

          my $ovar = $omeanerr * $omeanerr;
          $Mon{ "ndfpack_mon" }->obeyw( "setvar", "ndf=osky_$$ variance=$ovar" );

# Subtract the newly created constant frame from the NDF.
          $param1 = "in1=$in($otbounds[ 0 ]:$otbounds[ 1 ],$otbounds[ 2 ]:$otbounds[ 3 ])";
          $param2 = "in2=osky_$$ out=$o_out";
          $Mon{ "kappa_mon" }->obeyw( "sub", "$param1 $param2" );

# We're finished with the constant frame.  It's only role is propagate the
# variance correctly.
          _DELETE_A_FRAME_ FRAME=osky_$$

       } else {

# Subtract the sky from the required section about the target in the e-beam.
          $param1 = "in=$in($otbounds[ 0 ]:$otbounds[ 1 ],$otbounds[ 2 ]:$otbounds[ 3 ])";
          $param2 = "out=$o_out scalar=$osky";
          $Mon{ "kappa_mon" }->obeyw( "csub", "$param1 $param2" );
       }

       orac_print "O-beam of $in sky subtracted to make $o_out\n\n";

# Store the sky-subtracted o-beam frame in the group.
# ===================================================

# Create a new frame to store the differenced pair image.
       my $oFrm = $Frm->new;
       $oFrm->file( $o_out );

# Need to propagate the raw frame as ORAC-DR pattern matches for files
# ending in a number to set the $Frm->number, which is needed for the
# template method.
       $oFrm->raw( $Frm->raw );

# Transfer the headers to the differenced frame.
       %{$oFrm->hdr} = ( %{$Frm->hdr} );
       %{$oFrm->uhdr} = ( %{$Frm->uhdr} );

# Store the sky-subtracted o-frame in the o-beam group.
       $oGrp->push( $oFrm );

# Display the sky-subtracted o-beam frame.
       $Display->display_data( $oFrm ) if (defined $Display);

    }

# Podule
# ======

=head1 NAME

SUBTRACT_SKY_POL -- Creates sky-subtracted e- and o-beam images for polarimetry recipes.

=head1 DESCRIPTION

This primitive creates sky-subtracted e- and o-beam images.  The
images are subsets of the current frame in the appropriate region.
Modal sky values are determined from e- and o-beam sky areas within
the frame.  The sky-subtracted e- and o-beam frames are stored in two
groups, which are created if they do not already exist.  References
to the groups are stored in the main $Grp user headers called
EBEAM_GROUP and OBEAM_GROUP.

The primitive reports a successful sky subtraction and the frames
concerned.  It is a variant of L<_SUBTRACT_SKY_|_SUBTRACT_SKY_> for
POL recipes.

=head1 NOTES

=over 4

=item *

This primitive is suitable for UFTI, IRCAM, and Michelle in imaging mode.

=item *

Processing only occurs for object frames.

=item *

If the supplied target frame contains a variance array,
the error in the sky level is incorporated into the
sky-subtracted frame's variance.

=item *

The sky-subtracted images can be displayed.

=item *

The frame title is propagated.

=back

=head1 OUTPUT DATA

=over 4

=item *

Sky-subtracted e-beam and o-beam frames inheriting the frame's
name but with the _ess and _oss suffices respectively.

=back

=head1 TASKS

KAPPA: CSUB, STATS.

=head1 AUTHORS

MJC: Malcolm J. Currie (JAC)

=head1 COPYRIGHT

Copyright (C) 1998-2001 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut
