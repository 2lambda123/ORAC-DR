#+
# Name:
#    _GET_PLATE_SCALE_
#
# Purpose:
#    Finds the pixel scale measured in arcseconds per pixel for the
#    current Frame.
#
# Language:
#    Perl5
#
# Description:
#    This primitive obtains the pixel increment(s) from the headers of the
#    current frame, and if necessary, converts them to arcseconds per
#    pixel.  This allows CDELTn to be used either in a World Co-ordinate
#    System or as the pixel scale directly.  The scales are returned
#    in two arguments.
#
# Arguments:
#    DECSCALE = REAL (Returned)
#       The telescope offset along the Declination axis of the current 
#       frame measured in arcseconds per pixel.
#    RASCALE = REAL (Returned)
#       The telescope offset along the Right Ascension axis of the current 
#       frame measured in arcseconds per pixel.
#
# Notes:
#    -  This primitive is suitable for both UFTI and IRCAM.
#    Instrument-specific headers are obtained where appropriate.
#    -  Processing only occurs for object and sky frames.
#    -  The platescale headers are CDELT1 and CDELT2 for UFTI; there is a
#    single platescale for IRCAM given by header PIXELSIZ.  CTYPE1 set to
#    'RA---TAN' indicates the presence of the AIPS-convention WCS.
#
# Authors:
#    MJC: Malcolm J. Currie (JAC)
#
# Copyright:
#    Copyright (C) 1998-2000 Particle Physics and Astronomy Research
#    Council.  All Rights Reserved.
#
#-

# This should only be performed on OBJECT frames.
    if ( $Frm->hdr( "OBSTYPE" ) eq "OBJECT" ||
         $Frm->hdr( "OBSTYPE" ) eq "SKY") {

# Obtain the plate scales for the instrument from the headers.
       my ( $ra_pixelscale, $dec_pixelscale );
       my $instrument = $Frm->hdr( "INSTRUME" );
       if ( $instrument =~ /^UFTI/ ) {
          $ra_pixelscale = $Frm->hdr( "CDELT1" );
          $dec_pixelscale = $Frm->hdr( "CDELT2" );

# Allow for D notation, which is not recognised by Perl, so that
# supplied strings are valid numbers.
          $ra_pixelscale =~ s/D/E/;
          $dec_pixelscale =~ s/D/E/;

# The CDELTn headers are either part of a WCS in expressed in the
# AIPS-convention, or the values we require.  Angles for the former
# are measured in degrees.  The sign of the scale may be negative,
# but the platescale must be positive.
          if ( $Frm->hdr( "CTYPE1" ) eq "RA---TAN" ) {
             $ra_pixelscale = abs( $ra_pixelscale  * 3600.0 );
             $dec_pixelscale = abs( $dec_pixelscale  * 3600.0 );
          }

# Note there is only one size for IRCAM.  If CDELTn are set they're
# not used for pixelscale unless PIXELSIZ is absent.
       } else {
          $ra_pixelscale = $Frm->hdr( "PIXELSIZ" );
          if ( ( ! defined( $ra_pixelscale ) ) &&
               $Frm->hdr( "CTYPE1" ) eq "RA---TAN" ) {
             $ra_pixelscale = $Frm->hdr( "CDELT1" );
             $ra_pixelscale = abs( $ra_pixelscale * 3600.0 );
          }
          $dec_pixelscale = $ra_pixelscale;
       }

# Set the returned arguments.
       $_GET_PLATE_SCALE_{RASCALE} = $ra_pixelscale;
       $_GET_PLATE_SCALE_{DECSCALE} = $dec_pixelscale;
    }

# Podule
# ======

=head1 NAME

_GET_PLATE_SCALE_ -- Finds the pixel scale measured in arcseconds per pixel for the current Frame.

=head1 DESCRIPTION

This primitive obtains the pixel increment(s) from the headers of the
current frame, and if necessary, converts them to arcseconds per
pixel.  This allows CDELTn to be used either in a World Co-ordinate
System or as the pixel scale directly.  The scales are returned
in two arguments.

=head1 ARGUMENTS

=over 4

=item DECSCALE = REAL (Returned)

The telescope offset along the Declination axis of the current 
frame measured in arcseconds per pixel.

=item RASCALE = REAL (Returned)

The telescope offset along the Right Ascension axis of the current 
frame measured in arcseconds per pixel.

=back

=head1 NOTES

=over 4

=item *

This primitive is suitable for both UFTI and IRCAM.
Instrument-specific headers are obtained where appropriate.

=item 4

Processing only occurs for object and sky frames.

=item *

The platescale headers are CDELT1 and CDELT2 for UFTI; there is a
single platescale for IRCAM given by header PIXELSIZ.  CTYPE1 set to
'RA---TAN' indicates the presence of the AIPS-convention WCS.

=back

=head1 AUTHORS

MJC: Malcolm J. Currie (JAC)

=head1 COPYRIGHT

Copyright (C) 1998-2000 Particle Physics and Astronomy Research
Council.  All Rights Reserved.

=cut
