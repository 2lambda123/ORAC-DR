    # Load modules

    use IO::File;
    use Time::Local;
    use File::Copy;

    # Short hand for output directory

    my $odirname = $ENV{'ORAC_DATA_OUT'};

    # Set default astrometric and photometric calibration. This can
    # be changed on the command line using the -calib switch.

    $Cal->astrom("2mass");
    $Cal->photom("2mass");

    # Input and output directories. If the input (master) directory doesn't
    # exist, then write a semaphore saying that you've checked and issued
    # a warning once.
 
    my $mdirname = $ENV{'ORAC_WFCAM_MASTER'};
    my $semaphore = File::Spec->catfile($odirname,".checked_master");
    my $keepgoing = 1;
    if (! $mdirname || ! -d $mdirname) {
        if (! -f $semaphore) {
  	    my $fh = IO::File->new(">$semaphore");
	    close $fh;
            orac_warn("No Master calibration directory found!\n");
        }
        $keepgoing = 0;
    }

    # If we are told to keep going, then start by checking the master flat list
    # and the master cpm list

    if ($keepgoing) {
        foreach my $tt ('flat','cpm','dark','readnoise') {
            my $master_index = File::Spec->catfile($mdirname,"index.$tt");
            my $rules_file = File::Spec->catfile($ENV{'ORAC_DATA_CAL'},"rules.$tt");
 	    my $newindfile = File::Spec->catfile($odirname,"index.$tt");
	    
	    if (-f $master_index && ! -f $newindfile) {
		my $index = new ORAC::Index($master_index,$rules_file);
		my @allkeys = $index->indexkeys;
 		my %allfilt = ();
    	        if ($tt eq 'flat' || $tt eq 'cpm') {
		    foreach my $kk (@allkeys) {
		        my $ient = $index->indexentry($kk);
		        $allfilt{$$ient{'FILTER'}} = 1;
		    }
                } else {
    		    %allfilt = ('FILTER'=>'WHOCARES');
                }
		my $newindex = new ORAC::Index($newindfile,$rules_file);
		foreach my $kk (keys %allfilt) { 
		    my %thdr = ('FILTER'=>$kk,'ORACTIME'=>$Frm->hdr("ORACTIME"));
		    my $frm = $index->choosebydt("ORACTIME",\%thdr,0);
		    if ($frm) {
			my $ient = $index->indexentry($frm);
			$newindex->add($frm,$ient);
			my $inf = File::Spec->catfile($mdirname,$frm);
			my $outf = File::Spec->catfile($odirname,$frm);
			copy($inf,$outf);
		    }
		 }
		 $newindex->writeindex;
            }
        }

    }
	        

=head1 NAME
 
_KICKSTART_WFCAM_ -- Look in the WFCAM master calibration directory and get
some master calibration data.
 
=head1 DESCRIPTION
 
It may be necessary for some master calibration data to be available at the
beginning of the night for WFCAM.  If the environment variable 
$ORAC_WFCAM_MASTER is defined, then this routine will peek in there and grab
a flat and a confidence map for each available filter (the closest to the
UT of the current frame). It will also grab a master dark frame and an
estimate of the readnoise.  An index file with the default astrometric and
photometric catalogue will also be created. If any of these indexes already
exist, then they won't be created again. If $ORAC_WFCAM_MASTER doesn't exist
then a warning is issued that no master calibrations are available. A semaphore
is set in the output directory to stop this warning's being issued for every
single file in the run.
 
=head1 ARGUMENTS
 
None
 
=head1 NOTES
 
None
 
=head1 AUTHORS
 
JRL: Jim Lewis (CASU, IoA)
 
=head1 COPYRIGHT
 
Copyright (C) 2003-2006 Cambridge Astronomy Survey Unit.
All Rights Reserved
 
=cut
